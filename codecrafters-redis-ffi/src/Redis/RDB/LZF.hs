{-# LANGUAGE CApiFFI #-}
{-# LANGUAGE ForeignFunctionInterface #-}
{-# OPTIONS_GHC -fno-cse #-}

module Redis.RDB.LZF (
    lzfCompress,
    lzfDecompress,
    LzfCompressedByteString (..),
) where

import Data.ByteString qualified as BS
import Data.ByteString.Unsafe qualified as BSU

import Data.ByteString (ByteString)
import Data.Maybe (fromMaybe)
import Foreign (allocaBytes, mallocBytes)
import Foreign.C (CString, CUInt (..))
import System.IO.Unsafe (unsafePerformIO)

foreign import capi "lzf.h lzf_decompress" lzf_decompress :: CString -> CUInt -> CString -> CUInt -> IO CUInt
foreign import capi "lzf.h lzf_compress" lzf_compress :: CString -> CUInt -> CString -> CUInt -> IO CUInt

-- Use of the unsafe function from Data.ByteString.Unsafe is fine here because neither we nor C mutates the underlying CString representation of the ByteString thus we avoid breaking referential transparency through mutation

-- The underlying C functions (lzf_compress & lzf_decompress) treat the bytestring input as immutable since the parameter for the input pointer has a const modifier on both the pointer and the data referenced by the pointer

--- lzf compression and decompression can fail, so we return Maybe ByteString to express that possibility. We _could_ return the bytestring as is, but there wouldn't be much use for it at that point

-- NOTE: lzf_compress is likely to expand small inputs due to compression metadata overhead, potentially no patterns to compress in small inputs

data LzfCompressedByteString = LzfCompressedByteString
    { lzfCompressedByteString :: ByteString
    , compressedLen :: Int
    , originalLen :: Int
    }
    deriving stock (Eq, Show)

{-# NOINLINE lzfCompress #-}
lzfCompress :: ByteString -> Maybe Int -> Maybe LzfCompressedByteString
lzfCompress bs mLen = unsafePerformIO $ do
    BSU.unsafeUseAsCStringLen bs $ \(cstr, cstrLen) -> do
        let outputBufferSize = fromMaybe (cstrLen + 100) mLen -- Apparently, it is possible for lzf compression to write more data to the output buffer than the input buffer size due to some overhead. This is particularly true for small inputs but may also happen, for some reason, with larger inputs. The (+ 100) is an arbitrary heuristic to account for this.

        -- We do not want to use mallocBytes or callocBytes here because it possible for the lzf_compress function to write more data to the output buffer than the input buffer size due to some compression overhead (poor compression ratio). We also don't want to use either of those functions because they will result in a memory leak if the number of bytes written to the output buffer is smaller than what we allocate. The excess memory will be leaked. With the combination of allocaBytes and packCStringLen, we avoid this issue because the memory allocated by allocaBytes is automatically freed when the function exits and packCStringLen will only use the portion of the buffer that was actually written to.
        allocaBytes outputBufferSize $ \outputBufferPointer -> do
            numOfBytesWrittenToOutputBuffer <- lzf_compress cstr (fromIntegral cstrLen) outputBufferPointer (fromIntegral outputBufferSize)
            if numOfBytesWrittenToOutputBuffer == 0
                then pure Nothing
                else
                    ( \compressedBs ->
                        Just $ LzfCompressedByteString compressedBs (fromIntegral numOfBytesWrittenToOutputBuffer) cstrLen
                    )
                        <$> BS.packCStringLen (outputBufferPointer, fromIntegral numOfBytesWrittenToOutputBuffer)

{-# NOINLINE lzfDecompress #-}
lzfDecompress :: LzfCompressedByteString -> Maybe ByteString
lzfDecompress LzfCompressedByteString{lzfCompressedByteString, originalLen} = unsafePerformIO $ do
    let expectedDecompressedBSLen = originalLen
    BSU.unsafeUseAsCStringLen lzfCompressedByteString $ \(cstr, cstrLen) -> do
        let outputBufferSize = expectedDecompressedBSLen
        outputBufferPointer <- mallocBytes outputBufferSize
        numOfDecompressedBytes <- lzf_decompress cstr (fromIntegral cstrLen) outputBufferPointer (fromIntegral outputBufferSize)
        if numOfDecompressedBytes == 0
            then pure Nothing
            else Just <$> BSU.unsafePackCStringLen (outputBufferPointer, fromIntegral numOfDecompressedBytes)
