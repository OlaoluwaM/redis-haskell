{-# LANGUAGE CApiFFI #-}
{-# LANGUAGE ForeignFunctionInterface #-}
{-# OPTIONS_GHC -fno-cse #-}

module Redis.RDB.CRC64 (
    crc64,
    CheckSum,
    initialChecksum,
    fromChecksum,
    toChecksum,
)
where

import Data.ByteString.Unsafe qualified as BSU

import Data.ByteString (ByteString)
import Data.Word (Word64)
import Foreign.C (CString)
import Foreign.C.Types (CULLong (..))
import System.IO.Unsafe (unsafePerformIO)

foreign import capi unsafe "crc64.h crc64_init" c_crc64_init :: IO ()
foreign import capi unsafe "crc64.h crc64" c_crc64 :: CULLong -> CString -> CULLong -> CULLong

newtype CheckSum = CheckSum {getChecksum :: Word64}
    deriving stock (Show, Eq, Ord)
    deriving newtype (Num)

--- Use of the unsafe function from Data.ByteString.Unsafe is fine here because neither we nor C mutates the underlying CString representation of the ByteString thus we avoid breaking referential transparency through mutation

-- The underlying C function treats the bytestring input as immutable since the parameter is a const pointer

{-# NOINLINE crc64 #-}
crc64 :: CheckSum -> ByteString -> CheckSum
crc64 seed bs = unsafePerformIO $ do
    -- ensure tables are initialized otherwise function will always return 0
    c_crc64_init
    BSU.unsafeUseAsCStringLen bs $ \(cs, len) -> do
        let cksum = c_crc64 (checksumIntegral seed) cs (fromIntegral len)
        pure (toChecksumFromIntegral cksum)

checksumIntegral :: (Num b) => CheckSum -> b
checksumIntegral = fromIntegral . (.getChecksum)

toChecksumFromIntegral :: (Integral a) => a -> CheckSum
toChecksumFromIntegral = CheckSum . fromIntegral @_ @Word64

fromChecksum :: CheckSum -> Word64
fromChecksum = (.getChecksum)

toChecksum :: Word64 -> CheckSum
toChecksum = CheckSum

initialChecksum :: CheckSum
initialChecksum = CheckSum 0
