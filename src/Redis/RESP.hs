module Redis.RESP (
    RESPDataType (..),
    mkNonNullBulkString,
    serializeRESPDataType,
    mkNonNullRESPArray,
    nullBulkString,
    Array (..),
    arrayParser,

    -- * Re-exports from Redis.RESP.BulkString
    BulkString (..),
    bulkStringParser,
    mapBulkString,
    decodeUtf8BulkString',
    toOptionString,

    -- ** Testing
    terminatorSeqParser,
    seqTerminator,
    notTerminatorSeq,
    respArrayToList,
) where

import Data.Attoparsec.ByteString.Char8 qualified as AC
import Data.ByteString qualified as BS
import Data.Vector qualified as V

import Control.Applicative (Alternative ((<|>)))
import Control.Monad (when)
import Data.Attoparsec.ByteString (Parser)
import Data.ByteString (ByteString)
import Data.Functor (($>))
import Data.String.Interpolate (i)
import Data.Text (Text)
import Data.Vector (Vector)
import Redis.Helpers (withCustomError)
import Redis.RESP.BulkString (
    BulkString (..),
    bulkStringParser,
    decodeUtf8BulkString',
    mapBulkString,
    notTerminatorSeq,
    seqTerminator,
    terminatorSeqParser,
    toOptionString,
 )

-- Spec: https://redis.io/docs/latest/develop/reference/protocol-spec/

-- All supported RESP data types
data RESPDataType = SimpleString Text | MkBulkStringResponse BulkString | MkArrayResponse Array | RESPInteger Int | Null
    deriving stock (Eq, Show)

-- https://redis.io/docs/latest/develop/reference/protocol-spec/#arrays
-- This type represents an RESP data type array as a response value, NOT as a carrier for a command/request.
-- This type represents an RESP data type array as a response value, NOT as a carrier for a command/request.
data Array = Array (Vector RESPDataType) | NullArray
    deriving stock (Eq, Show)

arrayParser :: Parser Array
arrayParser = nullArrayParser <|> nonNullArrayParser

nullArrayParser :: Parser Array
nullArrayParser = withCustomError (AC.char '*' *> (AC.char '-' *> AC.char '1') *> terminatorSeqParser $> NullArray) "Invalid Null RESP Array"

nonNullArrayParser :: Parser Array
nonNullArrayParser = do
    len <- withCustomError (AC.char '*' *> AC.signed AC.decimal <* terminatorSeqParser) "Invalid RESP Array string"
    when (len < 0) $ fail "Invalid. Expected a potential non-null array but found the beginnings of a null array instead"

    isEndOfInput <- AC.atEnd

    case (len == 0, isEndOfInput) of
        (False, True) -> fail "Array is not supposed to be empty"
        _ -> pure ()

    elemList <- withCustomError (AC.count len respParser) (genLenMismatchErr len)
    pure $ Array . V.fromList $ elemList
  where
    genLenMismatchErr :: Int -> String
    genLenMismatchErr len' = [i|There seems to be mismatch between the purported Array length (#{len'}) and it's actual length|]

respParser :: Parser RESPDataType
respParser = (MkBulkStringResponse <$> bulkStringParser) <|> (MkArrayResponse <$> arrayParser)

serializeRESPDataType :: RESPDataType -> ByteString
serializeRESPDataType = \case
    SimpleString txt -> [i|+#{txt}#{seqTerminator}|]
    MkBulkStringResponse (BulkString bytes) -> let len = BS.length bytes in [i|$#{len}#{seqTerminator}#{bytes}#{seqTerminator}|]
    MkBulkStringResponse NullBulkString -> [i|$-1#{seqTerminator}|]
    RESPInteger int -> [i|:#{int}#{seqTerminator}|]
    Null -> [i|_#{seqTerminator}|]
    MkArrayResponse NullArray -> [i|*-1#{seqTerminator}|]
    MkArrayResponse (Array otherResTypes) ->
        let arrLen = V.length otherResTypes
            serializedElems = V.foldr (\a b -> serializeRESPDataType a <> b) "" otherResTypes
         in [i|*#{arrLen}#{seqTerminator}#{serializedElems}|]

mkNonNullBulkString :: ByteString -> RESPDataType
mkNonNullBulkString = MkBulkStringResponse . BulkString

nullBulkString :: RESPDataType
nullBulkString = MkBulkStringResponse NullBulkString

mkNonNullRESPArray :: [RESPDataType] -> RESPDataType
mkNonNullRESPArray = MkArrayResponse . Array . V.fromList

respArrayToList :: Array -> [RESPDataType]
respArrayToList NullArray = []
respArrayToList (Array v) = V.toList v
