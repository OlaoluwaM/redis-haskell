module Redis.Commands.Parser (
    commandParser,
    mkInvalidCommand,
    Command (..),
) where

import Data.Attoparsec.ByteString.Char8 qualified as AC
import Data.ByteString.Char8 qualified as BS
import Data.List.NonEmpty qualified as NE

import Control.Applicative.Combinators (count)
import Data.Aeson (Options (constructorTagModifier, sumEncoding), SumEncoding (..), ToJSON (toJSON), defaultOptions, genericToJSON)
import Data.Attoparsec.ByteString (Parser)
import Data.ByteString (ByteString)
import Data.Char (toUpper)
import Data.List.NonEmpty (NonEmpty)
import Data.String.Interpolate (i)
import Data.Text (Text)
import GHC.Generics (Generic)
import Redis.Commands.Echo (EchoCmdArg, mkEchoCmdArg)
import Redis.Commands.Ping (PingCmdArg, mkPingCmdArg)
import Redis.Commands.Set (SetCmdArg, mkSetCmdArg)
import Redis.Helpers (withCustomError)
import Redis.RESP (
    BulkString (..),
    bulkStringParser,
    terminatorSeqParser,
 )
import Redis.Commands.Get (GetCmdArg, mkGetCmdArg)

data ParsedCommandRequest = ParsedCommandRequest {command :: BulkString, args :: [BulkString]}
    deriving stock (Eq, Show)

-- NOTE: Remember to update toCommand if you add new commands
data Command = Ping PingCmdArg | Echo EchoCmdArg | Set SetCmdArg | Get GetCmdArg | InvalidCommand Text
    deriving stock (Eq, Show, Generic)

instance ToJSON Command where
    toJSON =
        genericToJSON
            defaultOptions
                { constructorTagModifier = map toUpper
                , sumEncoding = TaggedObject{tagFieldName = "command", contentsFieldName = "arguments"}
                }

-- NOTE: For now, we're working with the assumption that a request can only have one command at any given time even when pipelining
commandParser :: Parser Command
commandParser = do
    arrLength <- withCustomError (AC.char '*' *> (AC.signed @Int) AC.decimal <* terminatorSeqParser) "Invalid RESP Command"
    isEndOfInput <- AC.atEnd

    case (arrLength == 0, isEndOfInput) of
        (False, True) -> fail "RESP command string input ended too early"
        (True, False) -> fail "RESP command string input ended too early"
        (True, True) -> fail "RESP command string should not be empty"
        _ -> pure ()

    commandInfo <- withCustomError (NE.fromList <$> count arrLength bulkStringParser) [i|Mismatch between purported RESP array length (#{arrLength}) and actual length|] >>= toParsedCommandReq
    toCommand commandInfo

toParsedCommandReq :: (MonadFail m) => NonEmpty BulkString -> m ParsedCommandRequest
toParsedCommandReq (NullBulkString NE.:| _) = fail "A null bulk string is an invalid command"
toParsedCommandReq (cmdBulkStr NE.:| bulkStrArgs)
    | NullBulkString `elem` bulkStrArgs = fail "Command arguments cannot contain null bulk strings"
    | otherwise = pure $ ParsedCommandRequest cmdBulkStr bulkStrArgs

toCommand :: (MonadFail m) => ParsedCommandRequest -> m Command
toCommand (ParsedCommandRequest NullBulkString _) = fail "A null bulk string is not a valid command"
toCommand (ParsedCommandRequest (BulkString rawCmdStr) parsedArgs) =
    let normalizedCmdStr = BS.map toUpper rawCmdStr
     in case (normalizedCmdStr, parsedArgs) of
            ("PING", x) -> Ping <$> mkPingCmdArg x
            ("ECHO", x) -> Echo <$> mkEchoCmdArg x
            ("SET", x) -> Set <$> mkSetCmdArg x
            ("GET", x) -> Get <$> mkGetCmdArg x
            (unimplementedCommandStr, _) -> handleUnimplementedCmd unimplementedCommandStr

handleUnimplementedCmd :: (MonadFail m) => ByteString -> m Command
handleUnimplementedCmd unimplementedCommandStr =
    let validButNotImplementedCmds = []
     in if unimplementedCommandStr `elem` validButNotImplementedCmds
            then fail [i|The command '#{unimplementedCommandStr}' has not yet been implemented|]
            else fail [i|The command '#{unimplementedCommandStr}' is invalid|]

mkInvalidCommand :: Text -> Command
mkInvalidCommand = InvalidCommand
