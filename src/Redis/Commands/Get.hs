module Redis.Commands.Get (
    GetCmdArg (..),
    mkGetCmdArg,
    handleGet,
) where

import Data.HashMap.Strict qualified as HashMap

import Control.Concurrent.STM (atomically, readTVar)
import Control.Monad.IO.Class (MonadIO (..))
import Control.Monad.Reader (MonadReader (ask))
import Data.Aeson (ToJSON (..))
import Data.Bool (bool)
import Data.ByteString (ByteString)
import Data.Either (fromRight)
import Data.String.Interpolate (i)
import Data.Text (Text)
import Data.Text.Encoding (decodeUtf8')
import Data.Time (getCurrentTime)
import GHC.Generics (Generic)
import Network.Socket (Socket)
import Optics (A_Lens, LabelOptic, view)
import Redis.RESP (BulkString (BulkString), RESPDataType (Null), mkNonNullBulkString, nullBulkString, serializeRESPDataType)
import Redis.Server.ServerT (MonadSocket (..))
import Redis.Store (StoreState, StoreValue (StoreValue))
import Redis.Store.Data (
    RedisDataType (..),
    RedisStr (..),
    getStrTypeRepForRedisDataType,
 )

-- https://redis.io/docs/latest/commands/get/

newtype GetCmdArg = GetCmdArg {targetKey :: ByteString}
    deriving stock (Eq, Show, Generic)

instance ToJSON GetCmdArg where
    toJSON (GetCmdArg targetKey) = toJSON @Text $ fromRight "(blob)" $ decodeUtf8' targetKey

mkGetCmdArg :: (MonadFail m) => [BulkString] -> m GetCmdArg
mkGetCmdArg [] = fail "GET command requires an argument"
mkGetCmdArg [BulkString targetKey] = pure $ GetCmdArg targetKey
mkGetCmdArg _ = fail "GET command requires only 1 argument"

handleGet ::
    ( LabelOptic "clientSocket" A_Lens r r Socket Socket
    , LabelOptic "store" A_Lens r r StoreState StoreState
    , MonadIO m
    , MonadReader r m
    , MonadSocket m b
    ) =>
    GetCmdArg -> m b
handleGet (GetCmdArg targetKey) = do
    env <- ask
    currentTime <- liftIO getCurrentTime

    let socket = view #clientSocket env
    let kvStoreState = view #store env

    output <- liftIO $ atomically $ do
        kvStore <- readTVar kvStoreState
        let targetItemM = HashMap.lookup targetKey kvStore
        case targetItemM of
            Nothing -> pure . Right $ Null
            Just targetItemVar -> do
                (StoreValue valForTargetKey _ ttlM) <- readTVar targetItemVar
                case valForTargetKey of
                    (MkRedisStr (RedisStr val)) -> do
                        case ttlM of
                            Nothing -> pure . Right $ mkNonNullBulkString val
                            Just ttl -> do
                                let keyValTTLHasExpired = currentTime > ttl
                                pure . bool (Right $ mkNonNullBulkString val) (Right nullBulkString) $ keyValTTLHasExpired
                    x -> do
                        let actualValType = getStrTypeRepForRedisDataType @ByteString x
                        pure . Left $ [i|(error) GET command only supports string values, but value at key #{targetKey} was of type #{actualValType}|]

    sendThroughSocket socket . either id serializeRESPDataType $ output
