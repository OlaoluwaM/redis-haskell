module Redis.RESPSpec where

import Test.Hspec

import Data.ByteString.Char8 qualified as BS
import Data.Vector qualified as V
import Hedgehog qualified as H
import Hedgehog.Gen qualified as Gen
import Hedgehog.Range qualified as Range

import Data.Attoparsec.ByteString (parseOnly)
import Data.ByteString (ByteString)
import Data.Either (isLeft, isRight)
import Data.Foldable (for_)
import Data.String.Interpolate (i)
import Data.Text (Text)
import Hedgehog (MonadGen)
import Redis.RESP (Array (..), BulkString (..), RESPDataType (..), RESPInt (RESPInt), arrayParser, bulkStringParser, notTerminatorSeq, seqTerminator, serializeRESPDataType)
import Test.Tasty (TestTree)
import Test.Tasty.Hedgehog (testProperty)

data RawBulkString = NonNullRawBulkString ByteString | NullRawBulkString ByteString
    deriving stock (Show)

genValidRawBulkString :: (MonadGen m) => m RawBulkString
genValidRawBulkString = do
    strLength <- Gen.frequency [(1, pure (-1)), (2, Gen.integral (Range.linear 1 1000))]
    if strLength == (-1)
        then pure . NullRawBulkString $ [i|$-1#{seqTerminator}|]
        else do
            mainStr <- Gen.filterT (BS.all notTerminatorSeq) (Gen.bytes (Range.singleton strLength))
            pure . NonNullRawBulkString $ [i|$#{strLength}#{seqTerminator}#{mainStr}#{seqTerminator}|]

genInvalidRawBulkString :: (MonadGen m) => m ByteString
genInvalidRawBulkString = do
    strLength <- Gen.integral (Range.linear (-100) 100)

    -- We derive the bytestring range from `strLength` to allow us avoid an overlap between the bytestring length and the `strLength` value without the need for a manual filter
    -- (+ 1) in case strLength is 0 to avoid having an invalid range (0 * n = 0). This way whenever strLength is 0, the upper bound of this range will be 100
    mainStr <- Gen.bytes (Range.linear (abs strLength + 1) (abs (strLength * 100 + 1)))

    {-
    Note: We have this condition here because our parsing is permissive. For example, something like `-1\r\naa\r\n` would be valid since it subsumes a valid null bulk string (`-1\r\n`) (at least when it is standalone). Therefore, whenever our generated strLength is (-1), we want to modify or more accurately "skip over" that length to avoid accidentally generating a valid null bulk string in an invalid bulk string generator
    -}
    let rawBulkString =
            if strLength == (-1)
                then [i|$#{strLength * 100}#{seqTerminator}#{mainStr}#{seqTerminator}|]
                else [i|$#{strLength}#{seqTerminator}#{mainStr}#{seqTerminator}|]

    pure rawBulkString

test_bulkstring_parser_prop_tests :: [TestTree]
test_bulkstring_parser_prop_tests =
    [ testProperty "RESP bulkstring parser succeeds when provided a valid raw bulk string" validBulkStringPropTest
    , testProperty "RESP bulkstring parser fails when provided an invalid raw bulk string" invalidBulkStringPropTest
    ]

validBulkStringPropTest :: H.Property
validBulkStringPropTest = H.property $ do
    rawBulkstring <- H.forAll genValidRawBulkString
    H.classify "Null bulkstring" $ isNullRawBulkstring rawBulkstring
    H.classify "Non null bulkstring" $ (not . isNullRawBulkstring) rawBulkstring
    let result = parseOnly bulkStringParser . fromRawBulkStringToByteString $ rawBulkstring
    H.footnoteShow result
    H.assert (isRight result)

invalidBulkStringPropTest :: H.Property
invalidBulkStringPropTest = H.property $ do
    bs <- H.forAll genInvalidRawBulkString
    H.assert (isLeft . parseOnly bulkStringParser $ bs)

spec_bulkstring_parser_unit_tests :: Spec
spec_bulkstring_parser_unit_tests = do
    describe "Bulk string RESP data type parser" do
        it "Succeeds when provided an empty raw bulk string input" do
            let input = [i|$0#{seqTerminator}#{seqTerminator}|]
            let result = parseOnly bulkStringParser input
            result `shouldSatisfy` isRight

        it "Succeeds when provided a raw bulk string input with spaces" do
            let input = [i|$13#{seqTerminator}Hello, Redis!#{seqTerminator}|]
            let result = parseOnly bulkStringParser input
            result `shouldSatisfy` isRight

        it "Succeeds when provided a null raw bulk string input" do
            let input = [i|$-1#{seqTerminator}|]
            let result = parseOnly bulkStringParser input
            result `shouldSatisfy` isRight

        it "Succeeds even when raw null bulk string contains bytes (since we're being permissive here)" do
            let input = [i|$-1#{seqTerminator}fefrefer|]
            let result = parseOnly bulkStringParser input
            result `shouldSatisfy` isRight

        context "Fails even when provided an invalid null raw bulk string input" $ do
            it "Fails when raw null bulk string length indicator is not -1" do
                let input = [i|$-12#{seqTerminator}|]
                let result = parseOnly bulkStringParser input
                result `shouldSatisfy` isLeft

        for_ [(2 :: Int, "thequickbrownfoxjumpedoverthelazydog" :: Text), (1, "")] $ \(len, str) ->
            it [i|Fails when provided an raw bulk string input with a mismatch between its length and content: len: #{len}, content: #{str} |] do
                let input = [i|$#{len}#{seqTerminator}#{str}#{seqTerminator}|]
                let result = parseOnly bulkStringParser input
                result `shouldSatisfy` isLeft

spec_array_parser_unit_tests :: Spec
spec_array_parser_unit_tests = do
    describe "Array RESP data type parser" do
        it "Succeeds when provided an empty array" do
            let input = [i|*0#{seqTerminator}|]
            let result = parseOnly arrayParser input
            result `shouldBe` Right (Array $ V.fromList [])

        it "Succeeds when provided an array with a single bulk string" do
            let input = [i|*1#{seqTerminator}$5#{seqTerminator}hello#{seqTerminator}|]
            let result = parseOnly arrayParser input
            result `shouldBe` Right (Array $ V.fromList [MkBulkStringResponse (BulkString "hello")])

        it "Succeeds when provided an array with multiple bulk strings" do
            let input = [i|*2#{seqTerminator}$5#{seqTerminator}hello#{seqTerminator}$5#{seqTerminator}world#{seqTerminator}|]
            let result = parseOnly arrayParser input
            result `shouldBe` Right (Array $ V.fromList [MkBulkStringResponse (BulkString "hello"), MkBulkStringResponse (BulkString "world")])

        it "Succeeds when provided a null array" do
            let input = [i|*-1#{seqTerminator}|]
            let result = parseOnly arrayParser input
            result `shouldBe` Right NullArray

        it "Succeeds when provided a nested array" do
            let input = [i|*2#{seqTerminator}*1#{seqTerminator}$5#{seqTerminator}inner#{seqTerminator}$5#{seqTerminator}outer#{seqTerminator}|]
            let result = parseOnly arrayParser input
            let expected =
                    Array $
                        V.fromList
                            [ MkArrayResponse (Array $ V.fromList [MkBulkStringResponse (BulkString "inner")])
                            , MkBulkStringResponse (BulkString "outer")
                            ]
            result `shouldBe` Right expected

        it "Succeeds when provided an array with an empty bulk string" do
            let input = [i|*1#{seqTerminator}$0#{seqTerminator}#{seqTerminator}|]
            let result = parseOnly arrayParser input
            result `shouldBe` Right (Array $ V.fromList [MkBulkStringResponse (BulkString "")])

        it "Succeeds when provided an array with null bulk string" do
            let input = [i|*1#{seqTerminator}$-1#{seqTerminator}|]
            let result = parseOnly arrayParser input
            result `shouldBe` Right (Array $ V.fromList [MkBulkStringResponse NullBulkString])

        it "Succeeds when provided a complex nested array with mixed element types including null arrays" do
            let input = [i|*4#{seqTerminator}*3#{seqTerminator}$5#{seqTerminator}first#{seqTerminator}$-1#{seqTerminator}$6#{seqTerminator}second#{seqTerminator}*-1#{seqTerminator}*2#{seqTerminator}$4#{seqTerminator}test#{seqTerminator}$-1#{seqTerminator}$5#{seqTerminator}third#{seqTerminator}|]
            let result = parseOnly arrayParser input
            let expected =
                    Array $
                        V.fromList
                            [ MkArrayResponse
                                ( Array $
                                    V.fromList
                                        [ MkBulkStringResponse (BulkString "first")
                                        , MkBulkStringResponse NullBulkString
                                        , MkBulkStringResponse (BulkString "second")
                                        ]
                                )
                            , MkArrayResponse NullArray
                            , MkArrayResponse
                                ( Array $
                                    V.fromList
                                        [ MkBulkStringResponse (BulkString "test")
                                        , MkBulkStringResponse NullBulkString
                                        ]
                                )
                            , MkBulkStringResponse (BulkString "third")
                            ]

            result `shouldBe` Right expected

        context "Fails with invalid array formats" $ do
            it "Fails when provided an array with mismatched length indicator" do
                let input = [i|*3#{seqTerminator}$5#{seqTerminator}hello#{seqTerminator}$5#{seqTerminator}world#{seqTerminator}|]
                let result = parseOnly arrayParser input
                result `shouldSatisfy` isLeft

            it "Fails when provided an array with invalid header" do
                let input = [i|*x#{seqTerminator}|]
                let result = parseOnly arrayParser input
                result `shouldSatisfy` isLeft

            it "Fails when provided an array with invalid bulk string" do
                let input = [i|*1#{seqTerminator}$5#{seqTerminator}hi#{seqTerminator}|]
                let result = parseOnly arrayParser input
                result `shouldSatisfy` isLeft

            it "Fails when provided an array with multiple invalid bulk strings" do
                let input = [i|*2#{seqTerminator}$-1#{seqTerminator}hi#{seqTerminator}$0#{seqTerminator}$3#{seqTerminator}|]
                let result = parseOnly arrayParser input
                result `shouldSatisfy` isLeft

            it "Fails when provided an array with incomplete data" do
                let input = [i|*1#{seqTerminator}|]
                let result = parseOnly arrayParser input
                result `shouldSatisfy` isLeft

            it "Fails when provided an invalid null array format" do
                let input = [i|*-2#{seqTerminator}|]
                let result = parseOnly arrayParser input
                result `shouldSatisfy` isLeft

spec_RESP_serializer_unit_test :: Spec
spec_RESP_serializer_unit_test = describe "RESP data type serializer" do
    let testCasesWithValidRESPDataTypes =
            [ ("a simple string" :: Text, SimpleString "averynaughtyhare", [i|+averynaughtyhare#{seqTerminator}|])
            , ("an empty simple string", SimpleString "", [i|+#{seqTerminator}|])
            , ("a bulk string", MkBulkStringResponse (BulkString "helloworld"), [i|$10#{seqTerminator}helloworld#{seqTerminator}|])
            , ("an empty bulk string", MkBulkStringResponse (BulkString ""), [i|$0#{seqTerminator}#{seqTerminator}|])
            , ("an null bulk string", MkBulkStringResponse NullBulkString, [i|$-1#{seqTerminator}|])
            , ("a positive RESP Integer", RESPInteger (RESPInt 99), [i|:99#{seqTerminator}|])
            , ("a negative RESP Integer", RESPInteger (RESPInt (-34)), [i|:-34#{seqTerminator}|])
            , ("a null array", MkArrayResponse NullArray, [i|*-1#{seqTerminator}|])
            , ("a null value", Null, [i|_#{seqTerminator}|])
            ,
                ( "a flat array"
                , MkArrayResponse . Array . V.fromList $ [MkBulkStringResponse . BulkString $ "hello", RESPInteger (RESPInt 4), MkBulkStringResponse NullBulkString]
                , [i|*3#{seqTerminator}$5#{seqTerminator}hello#{seqTerminator}:4#{seqTerminator}$-1#{seqTerminator}|]
                )
            ,
                ( "a nested array"
                , MkArrayResponse . Array . V.fromList $
                    [ MkArrayResponse . Array . V.fromList $ [RESPInteger (RESPInt 1), RESPInteger (RESPInt 2), RESPInteger (RESPInt 3)]
                    , MkArrayResponse . Array . V.fromList $ [SimpleString "Hello", SimpleString "World"]
                    ]
                , "*2\r\n*3\r\n:1\r\n:2\r\n:3\r\n*2\r\n+Hello\r\n+World\r\n"
                )
            ]

    for_ testCasesWithValidRESPDataTypes $ \(testName, input, expected) ->
        it [i|Succeeds when passed #{testName}|] $ serializeRESPDataType input `shouldBe` expected

-- -------------------------------------------------------------------------- --
--                                    Utils                                   --
-- -------------------------------------------------------------------------- --

fromRawBulkStringToByteString :: RawBulkString -> ByteString
fromRawBulkStringToByteString (NullRawBulkString bs) = bs
fromRawBulkStringToByteString (NonNullRawBulkString bs) = bs

isNullRawBulkstring :: RawBulkString -> Bool
isNullRawBulkstring (NullRawBulkString _) = True
isNullRawBulkstring _ = False
