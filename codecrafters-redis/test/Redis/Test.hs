{-# LANGUAGE FieldSelectors #-}
{-# LANGUAGE UndecidableInstances #-}

module Redis.Test (
    PassableTestContext (..),
    runTestServer,
) where

import Data.List.NonEmpty qualified as NE
import Effect.Communication qualified as Eff
import Effect.Time qualified as Eff
import Effectful qualified as Eff
import Effectful.Concurrent.STM qualified as Eff
import Effectful.FileSystem qualified as Eff
import Effectful.Log qualified as Eff
import Effectful.Reader.Static qualified as ReaderEff

import Control.Concurrent.STM (atomically, newTVarIO)
import Data.ByteString (ByteString)
import Data.Maybe (fromMaybe)
import Data.Monoid (Last (..))
import Effectful (Eff)
import Effectful.Log (
    LogLevel (..),
    LoggerEnv (..),
 )
import GHC.Generics (Generic)
import Log.Backend.StandardOutput (withStdOutLogger)

import Network.Socket (
    AddrInfo (addrFamily, addrFlags, addrProtocol, addrSocketType),
    AddrInfoFlag (..),
    Socket,
    SocketType (..),
    defaultHints,
    getAddrInfo,
    socket,
 )
import Redis.Effects (ServerEffects)
import Redis.Server.Context (ServerContext (..))
import Redis.Server.Settings (ServerSettings, defaultServerSettings)
import Redis.ServerState (ServerState (..), genInitialServerStateEff)

data PassableTestContext = PassableTestContext
    { serverState :: Maybe ServerState
    , settings :: Maybe ServerSettings
    }
    deriving stock (Generic)

runTestServer :: Eff (ServerEffects ServerContext) a -> PassableTestContext -> IO ByteString
runTestServer action testContext =
    do
        loopbackSocket <- mkLoopbackSocket
        initialServerState <- atomically $ genInitialServerStateEff Nothing
        serverSettings <- newTVarIO $ fromMaybe defaultServerSettings testContext.settings

        let serverState = fromMaybe initialServerState testContext.serverState

        mRes <- withStdOutLogger $ \logger -> do
            let loggerEnv =
                    LoggerEnv
                        { leMaxLogLevel = LogAttention
                        , leLogger = logger
                        , leDomain = []
                        , leData = []
                        , leComponent = "redis-server-hs-test"
                        }
            let env = ServerContext loopbackSocket serverState serverSettings
            runServer env loggerEnv action

        pure $ fromMaybe "We got nothing bro. This probably shouldn't have happened" $ getLast mRes
  where
    runServer :: ServerContext -> LoggerEnv -> Eff (ServerEffects ServerContext) a -> IO (Last ByteString)
    runServer env loggerEnv =
        Eff.runEff
            . Eff.runCommunicationPure
            . Eff.runLog loggerEnv.leComponent loggerEnv.leLogger loggerEnv.leMaxLogLevel
            . Eff.runGetTimeIO
            . Eff.runConcurrent
            . Eff.runFileSystem
            . ReaderEff.runReader env

mkLoopbackSocket :: IO Socket
mkLoopbackSocket = do
    let hints = defaultHints{addrFlags = [AI_NUMERICHOST, AI_NUMERICSERV], addrSocketType = Stream}
    addr <- NE.head <$> getAddrInfo (Just hints) (Just "127.0.0.1") (Just "5000")
    socket (addrFamily addr) (addrSocketType addr) (addrProtocol addr)
