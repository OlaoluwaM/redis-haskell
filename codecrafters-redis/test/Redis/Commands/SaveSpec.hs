module Redis.Commands.SaveSpec where

import Path
import Redis.RDB.Config
import Redis.Server.Settings
import Redis.ServerState
import Redis.Store.Data
import Redis.Store.Timestamp
import Test.Hspec

import Data.HashMap.Strict qualified as HashMap
import Effectful qualified as Eff
import Effectful.FileSystem qualified as Eff
import Hedgehog qualified as H
import Hedgehog.Gen qualified as Gen
import Hedgehog.Range qualified as Range
import Redis.RDB.Binary qualified as Binary

import Control.Concurrent.STM (atomically, newTMVar, newTVar, readTVarIO)
import Data.Attoparsec.ByteString (parseOnly)
import Data.Foldable (for_)
import Data.String (IsString (..))
import Data.String.Interpolate (i)
import Data.Text (Text)
import Hedgehog (withTests)
import Redis.Commands.Parser (Command (..), commandParser)
import Redis.Handler (handleCommandReq)
import Redis.Helper (isInvalidCommand, mkBulkString, mkCmdReqStr, saveCmd)
import Redis.RDB.Format (RDBFile (..))
import Redis.RDB.Load (loadRDBFile)
import Redis.RESP (RESPDataType (..), serializeRESPDataType)
import Redis.Server.Context (ServerContext)
import Redis.Test (PassableTestContext (..), runTestServer)
import System.Directory (doesFileExist, removeFile)
import Test.Tasty (TestTree)
import Test.Tasty.Hedgehog (testProperty)

-- Helper function to check if a parsed command is an Echo command
isSaveCommand :: Command -> Bool
isSaveCommand Save = True
isSaveCommand _ = False

test_save_cmd_prop_tests :: [TestTree]
test_save_cmd_prop_tests =
    [ testProperty "SAVE command creates correct RDB snapshot" saveCmdPropTest
    ]

testRdbOutputDir :: Path Rel Dir
testRdbOutputDir = [reldir|test/Redis/Commands/Save/output|]

genTestSettingArgs :: (H.MonadGen m, MonadFail m) => m MkTestSettingsArg
genTestSettingArgs = do
    useCompression <- Gen.bool
    generateChecksum <- Gen.bool
    filenameSuffix <- Gen.word16 Range.linearBounded
    rdbFilename <-
        maybe (fail "Failed to parse RDB filename for some reason. Check it maybe?") pure $
            parseRelFile @Maybe ("save_command_test_dump_prop_" <> show filenameSuffix <> ".rdb")
    pure $
        MkTestSettingsArg
            { useCompression
            , generateChecksum
            , rdbFilename
            }

genStoreEntry :: (H.MonadGen m) => m (StoreKey, StoreValue)
genStoreEntry = do
    key <- StoreKey <$> Gen.bytes (Range.linear 1 20)
    valueStr <-
        Gen.choice
            [ RedisStr <$> Gen.bytes (Range.linear 0 100)
            , RedisStr . fromString . show <$> Gen.int (Range.linear (-1000000) 1000000)
            ]
    timestamp <- Gen.maybe $ UnixTimestampMS <$> Gen.word (Range.linearBounded @Word)
    let storeValue = mkStoreValue (MkRedisStr valueStr) timestamp
    pure (key, storeValue)

saveCmdPropTest :: H.Property
saveCmdPropTest = withTests 75 $ H.property $ do
    mkTestSettingArgs@MkTestSettingsArg{rdbFilename} <- H.forAll genTestSettingArgs

    initialStore <- HashMap.fromList <$> H.forAll (Gen.list (Range.linear 1 10) genStoreEntry)

    initialServerState <- H.evalIO $ initializeServerState initialStore

    let saveCmdReq = mkCmdReqStr [saveCmd]
    let testSettingsForSnapshot = mkTestSettings mkTestSettingArgs
    let testRdbConfigForSnapshot = mkRDBConfigFromTestSettingsArgs mkTestSettingArgs

    result <-
        H.evalIO $
            runTestServer
                (handleCommandReq @ServerContext saveCmdReq)
                ( PassableTestContext
                    { settings = Just testSettingsForSnapshot
                    , serverState = Just initialServerState
                    }
                )

    snapshotExists <- H.evalIO $ doesFileExist . toFilePath $ (testRdbOutputDir </> rdbFilename)
    snapshot <- H.evalIO $ Eff.runEff . Eff.runFileSystem $ Binary.decodeFile @_ @RDBFile testRdbConfigForSnapshot (toFilePath $ testRdbOutputDir </> rdbFilename)

    let storeFromSnapshot = fst $ loadRDBFile snapshot
    let expectedServerResponse = serializeRESPDataType (SimpleString "OK")

    storeFromServerState <- H.evalIO $ getStoreFromServerState initialServerState

    snapshotExists H.=== True
    result H.=== expectedServerResponse
    storeFromSnapshot H.=== storeFromServerState
    -- Clean up the generated RDB file if the test passed
    H.evalIO $ removeFile . toFilePath $ (testRdbOutputDir </> rdbFilename)

spec_save_cmd_tests :: Spec
spec_save_cmd_tests = do
    describe "Save Command Parser Tests" $ do
        it "should parse Save command" $ do
            let cmdReq = mkCmdReqStr [saveCmd]
            let result = parseOnly commandParser cmdReq
            result `shouldBe` Right Save

        it "should fail when unexpected argument is provided" $ do
            let cmdReq = mkCmdReqStr [saveCmd, mkBulkString "Hello"]
            let result = parseOnly commandParser cmdReq
            either (const True) isInvalidCommand result `shouldBe` True

        it "should consider too many arguments as invalid" $ do
            let cmdReq = mkCmdReqStr [saveCmd, mkBulkString "arg1", mkBulkString "arg2"]
            let result = parseOnly commandParser cmdReq
            either (const True) isInvalidCommand result `shouldBe` True

        context "recognizes various SAVE command formats" $ do
            for_
                [ ("despite casing" :: Text, mkCmdReqStr [mkBulkString "SAVE"])
                , ("despite casing (2)", mkCmdReqStr [mkBulkString "SaVe"])
                , ("despite casing (3)", mkCmdReqStr [mkBulkString "save"])
                ]
                $ \(testDesc, input) ->
                    it [i|Can parse a SAVE command string #{testDesc}|] $ do
                        let result = parseOnly commandParser input
                        either (const False) isSaveCommand result `shouldBe` True

    describe "SAVE Command Handler Tests" $ do
        it "can create a snapshot of the key value store" $ do
            let rdbFilename = [relfile|save_command_test_dump_non_prop.rdb|]

            let initialStore =
                    HashMap.fromList
                        [
                            ( StoreKey "key1"
                            , mkStoreValue
                                (MkRedisStr . RedisStr $ "value1")
                                Nothing
                            )
                        ,
                            ( StoreKey "key3"
                            , mkStoreValue
                                (MkRedisStr . RedisStr $ "value1")
                                (Just $ mkUnixTimestampMSFromUTCTime (read "2024-01-02 00:00:00 UTC"))
                            )
                        ]

            initialServerState <- initializeServerState initialStore

            let setCmdReq1 = mkCmdReqStr [mkBulkString "SET", mkBulkString "key2", mkBulkString "value2"]
            let setCmdReq2 = mkCmdReqStr [mkBulkString "SET", mkBulkString "key8", mkBulkString "value8", mkBulkString "PX", mkBulkString "100"]

            runTestServer
                (handleCommandReq @ServerContext setCmdReq1)
                ( PassableTestContext
                    { settings = Nothing
                    , serverState = Just initialServerState
                    }
                )

            runTestServer
                (handleCommandReq @ServerContext setCmdReq2)
                ( PassableTestContext
                    { settings = Nothing
                    , serverState = Just initialServerState
                    }
                )

            let saveCmdReq = mkCmdReqStr [saveCmd]
            let testSettingsArgs =
                    MkTestSettingsArg
                        { useCompression = False
                        , generateChecksum = True
                        , rdbFilename
                        }
            let testSettingsForSnapshot = mkTestSettings testSettingsArgs
            let expectedSaveCmdResponse = serializeRESPDataType (SimpleString "OK")
            let testRdbConfigForSnapshot = mkRDBConfigFromTestSettingsArgs testSettingsArgs

            result <-
                runTestServer
                    (handleCommandReq @ServerContext saveCmdReq)
                    ( PassableTestContext
                        { settings = Just testSettingsForSnapshot
                        , serverState = Just initialServerState
                        }
                    )

            snapshotExists <- doesFileExist . toFilePath $ (testRdbOutputDir </> rdbFilename)
            snapshot <- Eff.runEff . Eff.runFileSystem $ Binary.decodeFile @_ @RDBFile testRdbConfigForSnapshot (toFilePath $ testRdbOutputDir </> rdbFilename)

            let storeFromSnapshot = fst $ loadRDBFile snapshot

            storeFromNonSnapshotServerState <- getStoreFromServerState initialServerState

            storeFromSnapshot `shouldBe` storeFromNonSnapshotServerState
            snapshotExists `shouldBe` True
            result `shouldBe` expectedSaveCmdResponse
            removeFile . toFilePath $ (testRdbOutputDir </> rdbFilename)

data MkTestSettingsArg = MkTestSettingsArg
    { useCompression :: Bool
    , generateChecksum :: Bool
    , rdbFilename :: Path Rel File
    }
    deriving stock (Eq, Show)

mkTestSettings :: MkTestSettingsArg -> ServerSettings
mkTestSettings MkTestSettingsArg{..} =
    ServerSettings $
        HashMap.fromList
            [ (rdbFileDirectorySettingKey, DirPathVal . Rel $ testRdbOutputDir)
            , (rdbFilenameSettingKey, FilePathVal . Rel $ rdbFilename)
            , (rdbCompressionSettingKey, BoolVal useCompression)
            , (rdbChecksumSettingKey, BoolVal generateChecksum)
            ]

mkRDBConfigFromTestSettingsArgs :: MkTestSettingsArg -> RDBConfig
mkRDBConfigFromTestSettingsArgs MkTestSettingsArg{..} =
    mkRDBConfig
        MkRDBConfigArg
            { useCompression = useCompression
            , generateChecksum = generateChecksum
            }

initializeServerState :: Store -> IO ServerState
initializeServerState store = do
    atomically $ do
        lastRDBSaveCurrent <- newTMVar Nothing
        kvStore <- newTVar store
        lastRDBSave <- newTVar $ LastRDBSave lastRDBSaveCurrent Nothing
        pure $ ServerState kvStore lastRDBSave

getStoreFromServerState :: ServerState -> IO Store
getStoreFromServerState serverState = readTVarIO serverState.keyValueStoreRef
