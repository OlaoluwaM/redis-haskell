module Redis.Commands.BGSaveSpec where

import Path
import Redis.RDB.Config
import Redis.Server.Settings
import Redis.ServerState
import Redis.Store.Data
import Redis.Test
import Test.Hspec

import Data.HashMap.Strict qualified as HashMap
import Effectful qualified as Eff
import Effectful.FileSystem qualified as Eff
import Redis.RDB.Binary qualified as Binary

import Control.Concurrent (threadDelay)
import Control.Concurrent.STM (
    atomically,
    newTMVar,
    newTVar,
    readTVarIO,
 )
import Data.Attoparsec.ByteString (parseOnly)
import Data.Foldable (for_)
import Data.String.Interpolate (i)
import Data.Text (Text)
import Redis.Commands.BGSave (BGSaveCmdArg (BGSaveCmdArg))
import Redis.Commands.Parser (Command (..), commandParser)
import Redis.Handler (handleCommandReq)
import Redis.Helper (bgSaveCmd, isInvalidCommand, mkBulkString, mkCmdReqStr)
import Redis.RDB.Format (RDBFile)
import Redis.RDB.Load (loadRDBFile)
import Redis.Server (ServerContext)
import Redis.Store.Timestamp (mkUnixTimestampMSFromUTCTime)
import System.Directory (doesFileExist, removeFile)

-- Helper function to check if a parsed command is an Echo command
isBgSaveCommand :: Command -> Bool
isBgSaveCommand (BGSave _) = True
isBgSaveCommand _ = False

testRdbOutputDir :: Path Rel Dir
testRdbOutputDir = [reldir|test/Redis/Commands/BGSave/output|]

spec_bg_save_cmd_tests :: Spec
spec_bg_save_cmd_tests = do
    describe "BGSave Command Parser Tests" $ do
        it "should parse BGSave command with required argument" $ do
            let cmdReq = mkCmdReqStr [bgSaveCmd]
            let result = parseOnly commandParser cmdReq
            result `shouldBe` Right (BGSave (BGSaveCmdArg Nothing))

        it "should fail when unexpected argument is provided" $ do
            let cmdReq = mkCmdReqStr [bgSaveCmd, mkBulkString "Hello"]
            let result = parseOnly commandParser cmdReq
            either (const True) isInvalidCommand result `shouldBe` True

        it "should consider too many arguments as invalid" $ do
            let cmdReq = mkCmdReqStr [bgSaveCmd, mkBulkString "arg1", mkBulkString "arg2"]
            let result = parseOnly commandParser cmdReq
            either (const True) isInvalidCommand result `shouldBe` True

        context "recognizes various BGSAVE command formats" $ do
            for_
                [ ("despite casing" :: Text, mkCmdReqStr [mkBulkString "BGSave"])
                , ("despite casing (2)", mkCmdReqStr [mkBulkString "BgSAvE"])
                , ("despite casing (3)", mkCmdReqStr [mkBulkString "bgsave"])
                ]
                $ \(testDesc, input) ->
                    it [i|Can parse a BGSAVE command string #{testDesc}|] $ do
                        let result = parseOnly commandParser input
                        either (const False) isBgSaveCommand result `shouldBe` True

    describe "BGSAVE Command Handler Tests" $ do
        it "can create a snapshot of the key value store asynchronously" $ do
            let rdbFilename = [relfile|save_command_test_dump_bgsave.rdb|]

            let initialStore =
                    HashMap.fromList
                        [
                            ( StoreKey "key1"
                            , mkStoreValue
                                (MkRedisStr . RedisStr $ "value1")
                                Nothing
                            )
                        ,
                            ( StoreKey "key3"
                            , mkStoreValue
                                (MkRedisStr . RedisStr $ "value1")
                                (Just $ mkUnixTimestampMSFromUTCTime (read "2024-01-02 00:00:00 UTC"))
                            )
                        ]

            initialServerState <- initializeServerState initialStore

            let setCmdReq1 = mkCmdReqStr [mkBulkString "SET", mkBulkString "key2", mkBulkString "value2"]
            let setCmdReq2 = mkCmdReqStr [mkBulkString "SET", mkBulkString "key8", mkBulkString "value8", mkBulkString "PX", mkBulkString "100"]

            runTestServer
                (handleCommandReq @ServerContext setCmdReq1)
                ( PassableTestContext
                    { settings = Nothing
                    , serverState = Just initialServerState
                    }
                )

            runTestServer
                (handleCommandReq @ServerContext setCmdReq2)
                ( PassableTestContext
                    { settings = Nothing
                    , serverState = Just initialServerState
                    }
                )

            let bgSaveCmdReq = mkCmdReqStr [bgSaveCmd]
            let testSettingsArgs =
                    MkTestSettingsArg
                        { useCompression = False
                        , generateChecksum = True
                        , rdbFilename
                        }
            let testSettingsForSnapshot = mkTestSettings testSettingsArgs
            let testRdbConfigForSnapshot = mkRDBConfigFromTestSettingsArgs testSettingsArgs

            runTestServer
                (handleCommandReq @ServerContext bgSaveCmdReq)
                ( PassableTestContext
                    { settings = Just testSettingsForSnapshot
                    , serverState = Just initialServerState
                    }
                )

            -- TODO: Is there a better way to await the async process?
            threadDelay 100000 -- Hack to give the async snapshot process time to complete
            snapshotExists <- doesFileExist . toFilePath $ (testRdbOutputDir </> rdbFilename)
            snapshot <- Eff.runEff . Eff.runFileSystem $ Binary.decodeFile @_ @RDBFile testRdbConfigForSnapshot (toFilePath $ testRdbOutputDir </> rdbFilename)

            let storeFromSnapshot = fst $ loadRDBFile snapshot

            storeFromNonSnapshotServerState <- getStoreFromServerState initialServerState

            storeFromSnapshot `shouldBe` storeFromNonSnapshotServerState
            snapshotExists `shouldBe` True
            removeFile . toFilePath $ (testRdbOutputDir </> rdbFilename)

data MkTestSettingsArg = MkTestSettingsArg
    { useCompression :: Bool
    , generateChecksum :: Bool
    , rdbFilename :: Path Rel File
    }
    deriving stock (Eq, Show)

mkTestSettings :: MkTestSettingsArg -> ServerSettings
mkTestSettings MkTestSettingsArg{..} =
    ServerSettings $
        HashMap.fromList
            [ (Setting "dir", DirPathVal . Rel $ testRdbOutputDir)
            , (Setting "dbfilename", FilePathVal . Rel $ rdbFilename)
            , (Setting "rdbcompression", BoolVal useCompression)
            , (Setting "rdbchecksum", BoolVal generateChecksum)
            ]

mkRDBConfigFromTestSettingsArgs :: MkTestSettingsArg -> RDBConfig
mkRDBConfigFromTestSettingsArgs MkTestSettingsArg{..} =
    mkRDBConfig
        MkRDBConfigArg
            { useCompression = useCompression
            , generateChecksum = generateChecksum
            }

initializeServerState :: Store -> IO ServerState
initializeServerState store = do
    atomically $ do
        lastRDBSaveCurrent <- newTMVar Nothing
        kvStore <- newTVar store
        lastRDBSave <- newTVar $ LastRDBSave lastRDBSaveCurrent Nothing
        pure $ ServerState kvStore lastRDBSave

getStoreFromServerState :: ServerState -> IO Store
getStoreFromServerState serverState = readTVarIO serverState.keyValueStoreRef
