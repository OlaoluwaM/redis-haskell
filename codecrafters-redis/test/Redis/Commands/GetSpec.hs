module Redis.Commands.GetSpec where

import Data.Time
import Redis.Store.Timestamp
import Test.Hspec

import Data.ByteString qualified as BS
import Data.HashMap.Strict qualified as HashMap
import Data.Sequence qualified as Seq

import Control.Concurrent (threadDelay)
import Control.Concurrent.STM (atomically, newTMVar, newTVar)
import Control.Monad.IO.Class (MonadIO (..))
import Data.Attoparsec.ByteString (parseOnly)
import Data.Foldable (for_)
import Data.String.Interpolate (i)
import Data.Text (Text)
import Redis.Commands.Get (GetCmdArg (..))
import Redis.Commands.Parser (Command (..), commandParser)
import Redis.Handler (handleCommandReq)
import Redis.Helper (getCmd, isInvalidCommand, mkBulkString, mkCmdReqStr, setCmd)
import Redis.RESP (RESPDataType (..), nullBulkString, serializeRESPDataType)
import Redis.Server (ServerContext)
import Redis.ServerState (
    LastRDBSave (..),
    ServerState (..),
    StoreKey (..),
    mkStoreValue,
 )
import Redis.Store.Data (
    RedisDataType (..),
    RedisList (..),
    RedisStr (..),
 )
import Redis.Test (PassableTestContext (..), runTestServer)

-- Helper function to check if a parsed command is a Get command
isGetCommand :: Command -> Bool
isGetCommand (Get _) = True
isGetCommand _ = False

spec_get_cmd_tests :: Spec
spec_get_cmd_tests = do
    describe "GET Command Parser Tests" $ do
        it "should parse GET command with required key argument" $ do
            let cmdReq = mkCmdReqStr [getCmd, mkBulkString "mykey"]
            let result = parseOnly commandParser cmdReq
            result `shouldBe` Right (Get (GetCmdArg . StoreKey $ "mykey"))

        it "should be case-insensitive for command name" $ do
            let cmdReq = mkCmdReqStr [mkBulkString "GeT", mkBulkString "mykey"]
            let result = parseOnly commandParser cmdReq
            result `shouldBe` Right (Get (GetCmdArg . StoreKey $ "mykey"))

        it "should fail when no argument provided" $ do
            let cmdReq = mkCmdReqStr [getCmd]
            let result = parseOnly commandParser cmdReq
            either (const True) isInvalidCommand result `shouldBe` True

        it "should consider too many arguments as invalid" $ do
            let cmdReq = mkCmdReqStr [getCmd, mkBulkString "key1", mkBulkString "key2"]
            let result = parseOnly commandParser cmdReq
            either (const True) isInvalidCommand result `shouldBe` True

        it "should accept non-text key argument" $ do
            let cmdReq = mkCmdReqStr [getCmd, mkBulkString "123"]
            let result = parseOnly commandParser cmdReq
            result `shouldBe` Right (Get (GetCmdArg (StoreKey "123")))

        context "recognizes various GET command formats" $ do
            for_
                [ ("with valid key" :: Text, mkCmdReqStr [getCmd, mkBulkString "mykey"])
                , ("despite casing", mkCmdReqStr [mkBulkString "get", mkBulkString "mykey"])
                , ("despite casing (2)", mkCmdReqStr [mkBulkString "Get", mkBulkString "mykey"])
                , ("despite casing (3)", mkCmdReqStr [mkBulkString "gEt", mkBulkString "mykey"])
                , ("despite casing (4)", mkCmdReqStr [mkBulkString "GET", mkBulkString "mykey"])
                ]
                $ \(testDesc, input) ->
                    it [i|Can parse a GET command string #{testDesc}|] $ do
                        let result = parseOnly commandParser input
                        either (const False) isGetCommand result `shouldBe` True

        context "rejects invalid GET command formats" $ do
            for_
                [ ("malformed command" :: Text, mkCmdReqStr [mkBulkString "GE", mkBulkString "mykey"])
                ]
                $ \(testDesc, input) ->
                    it [i|Fails to parse a GET command string #{testDesc}|] $ do
                        let result = parseOnly commandParser input
                        either (const True) isInvalidCommand result `shouldBe` True

    beforeAll initializeServerState $ do
        describe "GET Command Handler Tests" $ do
            it "should return NULL for non-existent key" $ \_ -> do
                let key = "nonexistent"
                let cmdReq = mkCmdReqStr [getCmd, mkBulkString key]
                let expected = serializeRESPDataType Null

                result <- runTestServer (handleCommandReq @ServerContext cmdReq) (PassableTestContext{settings = Nothing, serverState = Nothing})

                result `shouldBe` expected

            it "should retrieve a previously stored string value" $ \initialServerState -> do
                -- First SET a key
                let key = "bike:1"
                let value = "testvalue"
                let setCmdReq = mkCmdReqStr [setCmd, mkBulkString key, mkBulkString value]

                -- Then GET the key
                let getCmdReq = mkCmdReqStr [getCmd, mkBulkString key]
                let expected = serializeRESPDataType (mkBulkString value)

                -- Run the commands in sequence
                runTestServer (handleCommandReq @ServerContext setCmdReq) (PassableTestContext{settings = Nothing, serverState = Just initialServerState})

                result <- runTestServer (handleCommandReq @ServerContext getCmdReq) (PassableTestContext{settings = Nothing, serverState = Just initialServerState})

                result `shouldBe` expected

            it "should handle keys with special characters" $ \initialServerState -> do
                let key = "special:key!@#"
                let value = "special-value"

                let setCmdReq = mkCmdReqStr [setCmd, mkBulkString key, mkBulkString value]
                let getCmdReq = mkCmdReqStr [getCmd, mkBulkString key]
                let expected = serializeRESPDataType (mkBulkString value)

                runTestServer (handleCommandReq @ServerContext setCmdReq) (PassableTestContext{settings = Nothing, serverState = Just initialServerState})

                result <- runTestServer (handleCommandReq @ServerContext getCmdReq) (PassableTestContext{settings = Nothing, serverState = Just initialServerState})

                result `shouldBe` expected

            it "should handle unicode characters in key and value" $ \initialServerState -> do
                let key = "你好"
                let value = "世界！"
                let setCmdReq = mkCmdReqStr [setCmd, mkBulkString key, mkBulkString value]
                let getCmdReq = mkCmdReqStr [getCmd, mkBulkString key]
                let expected = serializeRESPDataType (mkBulkString value)

                runTestServer (handleCommandReq @ServerContext setCmdReq) (PassableTestContext{settings = Nothing, serverState = Just initialServerState})

                result <- runTestServer (handleCommandReq @ServerContext getCmdReq) (PassableTestContext{settings = Nothing, serverState = Just initialServerState})

                result `shouldBe` expected

            it "should return an error for non-string value types" $ \initialServerState -> do
                let key = "car:1"
                let cmdReq = mkCmdReqStr [getCmd, mkBulkString key]
                result <- runTestServer (handleCommandReq @ServerContext cmdReq) (PassableTestContext{settings = Nothing, serverState = Just initialServerState})
                result `shouldSatisfy` BS.isPrefixOf "(error)"

            context "Supports key expiry" $ do
                it "should return a null bulkstring for a key with an expired TTL" $ \initialServerState -> do
                    let key = "expired:key"
                    let value = "this-will-expire"
                    -- Use a timestamp for May 10, 2025 (past date) - 1715385600000 milliseconds since epoch
                    let setCmdReq = mkCmdReqStr [setCmd, mkBulkString key, mkBulkString value, mkBulkString "PXAT", mkBulkString "1715385600000"]

                    -- Set the key with an already expired TTL
                    runTestServer (handleCommandReq @ServerContext setCmdReq) (PassableTestContext{settings = Nothing, serverState = Just initialServerState})

                    -- Then try to GET the key - should return NULL since it's expired
                    let getCmdReq = mkCmdReqStr [getCmd, mkBulkString key]
                    let expected = serializeRESPDataType nullBulkString

                    result <- runTestServer (handleCommandReq @ServerContext getCmdReq) (PassableTestContext{settings = Nothing, serverState = Just initialServerState})
                    result `shouldBe` expected

                it "should return value for a key with a TTL that has not expired" $ \serverState -> do
                    let key = "cam:4"
                    let value = "some value"
                    let setCmdReq = mkCmdReqStr [setCmd, mkBulkString key, mkBulkString value, mkBulkString "px", mkBulkString "100"]

                    runTestServer (handleCommandReq @ServerContext setCmdReq) (PassableTestContext{settings = Nothing, serverState = Just serverState})

                    let getCmdReq = mkCmdReqStr [getCmd, mkBulkString key]

                    result <- runTestServer (handleCommandReq @ServerContext getCmdReq) (PassableTestContext{settings = Nothing, serverState = Just serverState})
                    result `shouldBe` serializeRESPDataType (mkBulkString value)

                it "should handle a key with a short TTL" $ \initialServerState -> do
                    -- Set a key with a very short expiry (100ms)
                    let key = "very:short:ttl"
                    let value = "gone-quickly"
                    let setCmdReq = mkCmdReqStr [setCmd, mkBulkString key, mkBulkString value, mkBulkString "PX", mkBulkString "100"]

                    -- Set the key with short TTL
                    runTestServer (handleCommandReq @ServerContext setCmdReq) (PassableTestContext{settings = Nothing, serverState = Just initialServerState})

                    -- Wait for the TTL to expire
                    -- threaDelay takes time in microseconds so we must convert from milliseconds to microseconds for it to work as we want
                    liftIO $ threadDelay 150000 -- 150ms

                    -- Then try to GET the key - should return NULL since it's expired
                    let getCmdReq = mkCmdReqStr [getCmd, mkBulkString key]
                    let expected = serializeRESPDataType nullBulkString

                    result <- runTestServer (handleCommandReq @ServerContext getCmdReq) (PassableTestContext{settings = Nothing, serverState = Just initialServerState})
                    result `shouldBe` expected

initializeServerState :: IO ServerState
initializeServerState = do
    now <- getCurrentTime
    atomically $ do
        let storeItems =
                [ (StoreKey "bike:1", mkStoreValue (MkRedisStr . RedisStr $ "red") Nothing)
                , (StoreKey "bike:2", mkStoreValue (MkRedisStr . RedisStr $ "blue") Nothing)
                , (StoreKey "car:1", mkStoreValue (MkRedisList . RedisList . Seq.fromList $ ["some", "random", "text"]) Nothing)
                , (StoreKey "bike:3", mkStoreValue (MkRedisStr . RedisStr $ "green") Nothing)
                , (StoreKey "car:2", mkStoreValue (MkRedisStr . RedisStr $ "black") (Just $ mkUnixTimestampMSFromUTCTime (addUTCTime 3600 now)))
                , (StoreKey "user:1", mkStoreValue (MkRedisStr . RedisStr $ "john") Nothing)
                , (StoreKey "temp:key", mkStoreValue (MkRedisStr . RedisStr $ "ephemeral") (Just $ mkUnixTimestampMSFromUTCTime (addUTCTime 60 now)))
                , (StoreKey "counter", mkStoreValue (MkRedisStr . RedisStr $ "42") Nothing)
                , (StoreKey "colors", mkStoreValue (MkRedisList . RedisList . Seq.fromList $ ["red", "blue", "green"]) Nothing)
                ]
        lastRDBSaveCurrent <- newTMVar Nothing
        kvStore <- newTVar $ HashMap.fromList storeItems
        lastRDBSave <- newTVar $ LastRDBSave lastRDBSaveCurrent Nothing
        pure $ ServerState kvStore lastRDBSave
