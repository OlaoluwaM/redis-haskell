module Effect.Communication where

import Data.ByteString (ByteString)
import Data.Monoid (Last (..))
import Effectful (Eff, Effect, IOE, MonadIO (liftIO), (:>))
import Effectful.Dispatch.Dynamic qualified as Eff
import Effectful.TH qualified as Eff
import Effectful.Writer.Static.Local qualified as Eff
import Network.Socket (Socket)
import Network.Socket.ByteString (sendAll)

data Communication :: Effect where
    SendMessage :: Socket -> ByteString -> Communication m ()

Eff.makeEffect ''Communication

runCommunicationIO :: (IOE :> es) => Eff (Communication ': es) r -> Eff es r
runCommunicationIO = Eff.interpret_ $ \case
    SendMessage sock msg -> liftIO $ sendAll sock msg

runCommunicationPure :: Eff (Communication ': es) w -> Eff es (Last ByteString)
runCommunicationPure = Eff.reinterpret_ Eff.execWriter $ \case
    SendMessage _ msg -> Eff.tell (Last (Just msg))
