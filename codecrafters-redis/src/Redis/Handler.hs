module Redis.Handler (
    handleCommandReq,
) where

import Redis.Commands.BGSave
import Redis.Commands.Config.Get
import Redis.Commands.Echo
import Redis.Commands.Get
import Redis.Commands.Set
import Redis.Effects

import Data.Text qualified as T
import Effectful.Reader.Static qualified as ReaderEff
import Log qualified

import Data.Attoparsec.ByteString (parseOnly)
import Data.ByteString (ByteString)
import Data.String.Interpolate (i)
import Effect.Communication (sendMessage)
import Effectful (Eff, (:>))
import Effectful.FileSystem qualified as Eff
import Effectful.Log (Log)
import Optics (view)
import Redis.Commands.LastSave
import Redis.Commands.Parser (
    Command (..),
    ConfigSubCommand (ConfigGet),
    commandParser,
    mkInvalidCommand,
 )
import Redis.Commands.Ping (handlePing)
import Redis.Utils (fromEither, mapLeft)

handleCommandReq ::
    forall r es.
    ( RedisClientCommunication r es
    , RedisServerState r es
    , RedisServerSettings r es
    , Eff.FileSystem :> es
    , Log :> es
    ) =>
    ByteString -> Eff es ()
handleCommandReq rawCmdReq = do
    let command = fromEither . mapLeft (mkInvalidCommand . T.pack) . parseOnly commandParser $ rawCmdReq
    Log.logInfo "Handling req for command: " command
    dispatchCmd @r command

dispatchCmd ::
    forall r es.
    ( RedisClientCommunication r es
    , RedisServerState r es
    , RedisServerSettings r es
    , Eff.FileSystem :> es
    , Log :> es
    ) =>
    Command -> Eff es ()
dispatchCmd (Ping pingCmdArgs) = handlePing @r pingCmdArgs
dispatchCmd (Echo echoCmdArgs) = handleEcho @r echoCmdArgs
dispatchCmd (Set setCmdArgs) = handleSet @r setCmdArgs
dispatchCmd (Get getCmdArgs) = handleGet @r getCmdArgs
dispatchCmd (Config (ConfigGet configGetCmdArgs)) = handleConfigGet @r configGetCmdArgs
dispatchCmd Save = handleSave @r
dispatchCmd (BGSave bgSaveCmdArgs) = handleBGSave @r bgSaveCmdArgs
dispatchCmd LastSave = handleLastSave @r
dispatchCmd (InvalidCommand msg) = do
    env <- ReaderEff.ask @r
    let socket = view #clientSocket env
    sendMessage socket [i|(error). Invalid Command or command not yet implemented: #{msg}|]
