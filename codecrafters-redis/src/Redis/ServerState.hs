module Redis.ServerState (
    StoreValue (..),
    StoreKey (..),
    Store,
    ServerState (..),
    LastRDBSave (..),
    genInitialServerStateEff,
    getItemFromStore,
    addItemToStore,
    mkStoreValue,
    getItemTTLValue,
) where

import Redis.Store.Timestamp

import Control.Concurrent.STM qualified as STM
import Data.HashMap.Strict qualified as HashMap

import Data.ByteString (ByteString)
import Data.HashMap.Strict (HashMap)
import Data.Hashable (Hashable)
import Data.Maybe (fromMaybe)
import Data.Time (UTCTime)
import GHC.Generics (Generic)
import Redis.Store.Data (RedisDataType)

data ServerState = ServerState
    { keyValueStoreRef :: STM.TVar Store
    , lastRDBSaveRef :: STM.TVar LastRDBSave
    }

type Store = HashMap StoreKey StoreValue

-- Redis keys are binary safe strings https://redis.io/docs/latest/develop/using-commands/keyspace/#:~:text=Although,Redis,-%2E
newtype StoreKey = StoreKey {key :: ByteString}
    deriving stock (Eq, Show, Generic)
    deriving newtype (Hashable)

-- Redis does not store insert time/time of creation
data StoreValue = StoreValue {value :: RedisDataType, ttlTimestamp :: Maybe UnixTimestampMS}
    deriving stock (Eq, Show, Generic)

data LastRDBSave = LastRDBSave
    { inProgress :: STM.TMVar (Maybe UTCTime)
    {- ^ Timestamp of the current in-progress RDB save, if any. There can be only one active RDB save at a time.
        We use a TMVar, 1 for compatibility with STM, and 2 to allow only one thread to perform the save operation at a time per https://redis.io/docs/latest/commands/bgsave/#:~:text=An%20error%20is%20returned%20if%20there%20is%20already%20a%20background%20save%20running%20or%20if%20there%20is%20another%20non%2Dbackground%2Dsave%20process%20running%2C%20specifically%20an%20in%2Dprogress%20AOF%20rewrite

        Also, we want this to be a `Maybe` to distinguish between when an RDB save has been performed before and when it hasn't been performed yet. If we ever end up with a `Maybe (Maybe UTCTime)` in this context, we do not want to `join`
    -}
    , lastCompleted :: Maybe UTCTime
    -- ^ Timestamp of last RDB save operation that completed successfully, if any
    }
    deriving stock (Generic)

getItemFromStore :: StoreKey -> Store -> Maybe StoreValue
getItemFromStore = HashMap.lookup

addItemToStore :: StoreKey -> StoreValue -> Store -> Store
addItemToStore = HashMap.insert

mkStoreValue :: RedisDataType -> Maybe UnixTimestampMS -> StoreValue
mkStoreValue value ttl = StoreValue{value, ttlTimestamp = ttl}

getItemTTLValue :: StoreValue -> Maybe UnixTimestampMS
getItemTTLValue = (.ttlTimestamp)

genInitialServerStateEff :: Maybe Store -> STM.STM ServerState
genInitialServerStateEff mStore = do
    let initialLastRDBSave = Nothing
    kvStore <- STM.newTVar . fromMaybe HashMap.empty $ mStore
    inProgress <- STM.newTMVar initialLastRDBSave
    lastRDBSave <- STM.newTVar $ LastRDBSave inProgress initialLastRDBSave
    pure $ ServerState kvStore lastRDBSave
