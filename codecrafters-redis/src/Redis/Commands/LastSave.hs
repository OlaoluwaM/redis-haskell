module Redis.Commands.LastSave (
    handleLastSave,
    mkLastSaveCmdArg,
) where

import Redis.Effects

import Effectful.Concurrent.STM qualified as STMEff
import Effectful.Reader.Static qualified as ReaderEff

import Control.Applicative ((<|>))
import Control.Monad (join)
import Data.Time (UTCTime)
import Data.Time.Clock.POSIX (utcTimeToPOSIXSeconds)
import Effect.Communication (sendMessage)
import Effectful (Eff)
import Optics (view)
import Redis.RESP (RESPDataType (..), serializeRESPDataType)
import Redis.RESP.BulkString (BulkString)
import Redis.ServerState (LastRDBSave (..), ServerState (..))

mkLastSaveCmdArg :: (MonadFail m) => [BulkString] -> m ()
mkLastSaveCmdArg [] = pure ()
mkLastSaveCmdArg _ = fail "LASTSAVE command does not accept any arguments"

handleLastSave :: forall r es. (RedisClientCommunication r es, RedisServerState r es) => Eff es ()
handleLastSave = do
    env <- ReaderEff.ask @r

    let socket = view #clientSocket env
    let serverState = view #serverState env

    mLastSaveTime <- STMEff.atomically $ do
        lastRDBSaveState <- STMEff.readTVar serverState.lastRDBSaveRef
        fmap join (STMEff.tryReadTMVar lastRDBSaveState.inProgress) <|> pure lastRDBSaveState.lastCompleted

    case mLastSaveTime of
        Just lastSaveTime -> sendMessage socket . serializeRESPDataType $ RESPInteger (utcToPOSIXSecondsInt lastSaveTime)
        -- This is non-standard. The redis protocol does not define what to return if no save has been performed yet. In fact, redis considers the database saved successfully at startup, meaning last save time should always exists and never be nothing. It's initial value should be the server start time. However, I chose to go down the route of modeling the absence of a "last save" explicitly because it just made more sense as the OG semantics seemed more like the result of some technical limitation rather than a deliberate design choice.
        Nothing -> sendMessage socket . serializeRESPDataType $ SimpleString "No RDB save has been performed yet"

utcToPOSIXSecondsInt :: UTCTime -> Int
utcToPOSIXSecondsInt = floor . utcTimeToPOSIXSeconds
