module Redis.Commands.BGSave (
    handleBGSave,
    handleSave,
    mkBGSaveCmdArg,
    mkSaveCmdArg,
    ShouldSchedule (..),
    BGSaveCmdArg (..),
) where

import Path
import Redis.Server.Settings
import Redis.ServerState

import Data.HashMap.Strict qualified as HashMap
import Effectful.Concurrent qualified as Eff
import Effectful.Concurrent.STM qualified as STMEff
import Effectful.Error.Static qualified as Eff
import Effectful.Exception qualified as Eff
import Effectful.FileSystem qualified as Eff
import Effectful.Reader.Static qualified as ReaderEff
import Redis.RDB.Binary qualified as RDB

import Control.Concurrent.STM (
    modifyTVar,
    putTMVar,
    tryTakeTMVar,
 )
import Control.Exception (Exception (..))
import Control.Monad (void)
import Data.Aeson (ToJSON)
import Data.Text (Text)
import Effect.Communication (Communication, sendMessage)
import Effect.Time (Time, getCurrentTime, getPosixTime)
import Effectful (Eff, (:>))
import Effectful.Log (Log)
import GHC.Generics (Generic)
import GHC.Stack (HasCallStack)
import Network.Socket (Socket)
import Optics (set, view)
import Redis.Effects (RDBWrite)
import Redis.RDB.Config (MkRDBConfigArg (..), RDBConfig, mkRDBConfig)
import Redis.RDB.Save (saveRedisStoreToRDB)
import Redis.RESP (BulkString (..), RESPDataType (SimpleString), serializeRESPDataType)
import Redis.Utils (logInternalServerError)

-- https://redis.io/docs/latest/commands/save/
-- https://redis.io/docs/latest/commands/bgsave/

newtype BGSaveCmdArg = BGSaveCmdArg (Maybe ShouldSchedule)
    deriving stock (Eq, Show, Generic)
    deriving anyclass (ToJSON)

-- Modeled but not used
newtype ShouldSchedule = ShouldSchedule Bool
    deriving stock (Eq, Show)
    deriving newtype (ToJSON)

data SaveFailure
    = MissingRDBFilePathSetting
    | MissingRDBCompressionSetting
    | MissingRDBChecksumSetting
    | IncompatibleRDBCompressionOrChecksumSetting
    | MissingDirSetting
    | MissingDBFilenameSetting
    | IncompatibleDirAndFilePathTypes
    | BackgroundSaveInProgress
    | RDBSaveFailed String
    deriving stock (Eq, Show)

instance Exception SaveFailure

mkBGSaveCmdArg :: (MonadFail m) => [BulkString] -> m BGSaveCmdArg
mkBGSaveCmdArg [] = pure $ BGSaveCmdArg Nothing
mkBGSaveCmdArg [BulkString "SCHEDULE"] = pure $ BGSaveCmdArg (Just $ ShouldSchedule True)
mkBGSaveCmdArg [BulkString invalidArg] = fail $ "Invalid argument to BGSAVE command: " <> show invalidArg <> ". Only SCHEDULE is supported."
mkBGSaveCmdArg _ = fail "BGSAVE command accepts only 1 argument"

mkSaveCmdArg :: (MonadFail m) => [BulkString] -> m ()
mkSaveCmdArg [] = pure ()
mkSaveCmdArg _ = fail "SAVE command does not accept any arguments"

handleSave ::
    forall r es.
    (RDBWrite r es) =>
    Eff es ()
handleSave = do
    env <- ReaderEff.ask @r

    let socket = view #clientSocket env
    let serverState = view #serverState env
    let serverSettingsRef = view #serverSettingsRef env

    let onSuccess = sendMessage socket . serializeRESPDataType $ SimpleString "OK"

    Eff.runErrorNoCallStackWith @SaveFailure
        (\e -> notifyOfSaveError socket e "Saving failed")
        (performRDBSave serverState serverSettingsRef socket onSuccess)

handleBGSave ::
    forall r es.
    (RDBWrite r es) =>
    BGSaveCmdArg -> Eff es ()
handleBGSave _ = do
    env <- ReaderEff.ask @r

    let socket = view #clientSocket env
    let serverState = view #serverState env
    let serverSettingsRef = view #serverSettingsRef env

    sendMessage socket . serializeRESPDataType $ SimpleString "Starting"

    void $
        Eff.forkFinally
            (performRDBBackgroundSave socket serverState serverSettingsRef)
            ( \case
                Left err -> notifyOfSaveError socket err "Background saving failed"
                Right _ -> sendMessage socket . serializeRESPDataType $ SimpleString "Background saving finished"
            )
  where
    performRDBBackgroundSave socket serverState serverSettingsRef =
        Eff.runErrorNoCallStackWith @SaveFailure
            Eff.throwIO -- If an error is thrown by performRDBSave, we handle it by rethrowing so we can satisfy/pop off the Error effect constraint and have the outer forkFinally handle it correctly
            (performRDBSave serverState serverSettingsRef socket (onSuccess socket))

    onSuccess socket = sendMessage socket . serializeRESPDataType $ SimpleString "Background saving completed"

performRDBSave ::
    ( HasCallStack
    , Log :> es
    , Eff.FileSystem :> es
    , Eff.Concurrent :> es
    , Communication :> es
    , Eff.Error SaveFailure :> es
    , Time :> es
    ) =>
    ServerState -> ServerSettingsRef -> Socket -> Eff es () -> Eff es ()
performRDBSave serverState serverSettingsRef socket onSuccess = do
    settings <- STMEff.readTVarIO serverSettingsRef
    rdbDirPath <- getRDBFilePathFromSettings settings
    rdbConfig <- mkRDBConfigFromSettings settings

    kvStore <- STMEff.readTVarIO serverState.keyValueStoreRef
    lastRDBSave <- STMEff.readTVarIO serverState.lastRDBSaveRef

    let notifyUserOfExistingBackgroundSave = sendMessage socket . serializeRESPDataType $ SimpleString "A background save is already in progress"

    -- We need to do all this to protect against an asynchronous exception occurring after we've potentially taken a lock on our TMVar but before we've put it back as such an interruption could lead us to a deadlock
    Eff.bracketOnError
        (STMEff.atomically $ tryTakeTMVar lastRDBSave.inProgress)
        ( \case
            Nothing -> do
                -- Rather than throwing a user exception here with error regarding how this state should be impossible, we log it out as an error and send a message to the user's socket. This way we can be made aware of a critical failure without crashing
                -- Specializing to Text because I think the ToJSON instance for Text is more performant than that of String, since Aeson's Value type uses Text for strings internally
                logInternalServerError @_ @Text "Impossible exception: bracketOnError should always succeed if tryTakeTMVar fails with a Nothing. If not then something has gone very or wrong, or maybe the act of sending data to the user's socket failed or was interrupted somehow?"
                notifyUserOfExistingBackgroundSave
            Just mLastRDBSaveTimeM -> do
                logInternalServerError @_ @Text "RDB save failed due to an exception"
                STMEff.atomically $ putTMVar lastRDBSave.inProgress mLastRDBSaveTimeM
        )
        ( \case
            Nothing -> do
                notifyUserOfExistingBackgroundSave
            Just _ -> do
                currentTime <- getPosixTime
                let rdbFile = saveRedisStoreToRDB currentTime kvStore

                RDB.encodeFile rdbConfig (fromSomeFile rdbDirPath) rdbFile

                saveTime <- getCurrentTime
                STMEff.atomically $ do
                    putTMVar lastRDBSave.inProgress (Just saveTime)
                    modifyTVar serverState.lastRDBSaveRef (set #lastCompleted (Just saveTime))

                onSuccess
        )

notifyOfSaveError :: (Log :> es, Communication :> es, Exception e) => Socket -> e -> Text -> Eff es ()
notifyOfSaveError socket e returnMsg = do
    logInternalServerError $ "Error occured while attempting to save RDB file: " <> displayException e
    sendMessage socket . serializeRESPDataType $ SimpleString returnMsg

getRDBFilePathFromSettings ::
    ( Eff.Error SaveFailure :> es
    , HasCallStack
    ) =>
    ServerSettings -> Eff es (SomeBase File)
getRDBFilePathFromSettings (ServerSettings settings) = do
    dirPath <- maybe (Eff.throwError MissingDirSetting) pure $ HashMap.lookup rdbFileDirectorySettingKey settings
    fileName <- maybe (Eff.throwError MissingDBFilenameSetting) pure $ HashMap.lookup rdbFilenameSettingKey settings

    case (dirPath, fileName) of
        (DirPathVal (Abs dir), FilePathVal (Rel file)) -> pure $ Abs $ dir </> file
        (DirPathVal (Rel dir), FilePathVal (Rel file)) -> pure $ Rel $ dir </> file
        _ -> Eff.throwError IncompatibleDirAndFilePathTypes

mkRDBConfigFromSettings ::
    ( Eff.Error SaveFailure :> es
    , HasCallStack
    ) =>
    ServerSettings -> Eff es RDBConfig
mkRDBConfigFromSettings (ServerSettings settings) = do
    rawRDBCompressionSetting <- maybe (Eff.throwError MissingRDBCompressionSetting) pure $ HashMap.lookup rdbCompressionSettingKey settings
    rawRDBChecksumSetting <- maybe (Eff.throwError MissingRDBChecksumSetting) pure $ HashMap.lookup rdbChecksumSettingKey settings

    case (rawRDBCompressionSetting, rawRDBChecksumSetting) of
        (BoolVal rdbCompressionSettingVal, BoolVal rdbChecksumSettingVal) ->
            pure $
                mkRDBConfig $
                    MkRDBConfigArg{useCompression = rdbCompressionSettingVal, generateChecksum = rdbChecksumSettingVal}
        _ -> Eff.throwError IncompatibleRDBCompressionOrChecksumSetting
