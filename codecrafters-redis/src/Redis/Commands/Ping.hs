module Redis.Commands.Ping (
    PingCmdArg (..),
    handlePing,
    mkPingCmdArg,
) where

import Effectful.Reader.Static qualified as ReaderEff

import Data.Aeson (ToJSON)
import Data.Either.Extra (eitherToMaybe)
import Data.Text (Text)
import Data.Text.Encoding (decodeUtf8', encodeUtf8)
import Effect.Communication (sendMessage)
import Effectful (Eff)
import GHC.Generics (Generic)
import Optics (view)
import Redis.Effects (RedisClientCommunication)
import Redis.RESP (BulkString (BulkString), RESPDataType (..), mkNonNullBulkString, serializeRESPDataType)

-- https://redis.io/docs/latest/commands/ping/
-- We assume the option `message` argument of the ping command should only ever be considered as text

newtype PingCmdArg = PingCmdArg (Maybe Text)
    deriving stock (Eq, Show, Generic)
    deriving anyclass (ToJSON)

mkPingCmdArg :: (MonadFail m) => [BulkString] -> m PingCmdArg
mkPingCmdArg [] = pure . PingCmdArg $ Nothing
mkPingCmdArg [BulkString txt] = pure . PingCmdArg . eitherToMaybe $ decodeUtf8' txt
mkPingCmdArg _ = fail "PING command does accept multiple arguments"

handlePing :: forall r es. (RedisClientCommunication r es) => PingCmdArg -> Eff es ()
handlePing (PingCmdArg x) = do
    env <- ReaderEff.ask @r
    let socket = view #clientSocket env
    case x of
        (Just txt) -> sendMessage socket . serializeRESPDataType . mkNonNullBulkString . encodeUtf8 $ txt
        Nothing -> sendMessage socket . serializeRESPDataType . SimpleString $ "PONG"
