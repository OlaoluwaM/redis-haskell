module Redis.Commands.Get (
    GetCmdArg (..),
    mkGetCmdArg,
    handleGet,
) where

import Control.Concurrent.STM
import Redis.RESP
import Redis.ServerState
import Redis.Store.Timestamp

import Data.HashMap.Strict qualified as HashMap
import Effect.Time qualified as Time
import Effectful.Concurrent.STM qualified as Eff
import Effectful.Reader.Static qualified as ReaderEff

import Data.Aeson (ToJSON (..))
import Data.Bool (bool)
import Data.ByteString (ByteString)
import Data.Either (fromRight)
import Data.String.Interpolate (i)
import Data.Text (Text)
import Data.Text.Encoding (decodeUtf8')
import Effect.Communication (sendMessage)
import Effectful (Eff)
import GHC.Generics (Generic)
import Optics (view)
import Redis.Effects (RedisClientCommunication, RedisServerState)
import Redis.Store.Data (
    RedisDataType (..),
    RedisStr (..),
    showRedisDataType,
 )

-- https://redis.io/docs/latest/commands/get/

newtype GetCmdArg = GetCmdArg {targetKey :: StoreKey}
    deriving stock (Eq, Show, Generic)

instance ToJSON GetCmdArg where
    toJSON (GetCmdArg (StoreKey targetKey)) = toJSON @Text $ fromRight "(blob)" $ decodeUtf8' targetKey

mkGetCmdArg :: (MonadFail m) => [BulkString] -> m GetCmdArg
mkGetCmdArg [] = fail "GET command requires an argument"
mkGetCmdArg [BulkString targetKey] = pure $ GetCmdArg (StoreKey targetKey)
mkGetCmdArg _ = fail "GET command requires only 1 argument"

handleGet ::
    forall r es.
    ( RedisClientCommunication r es
    , RedisServerState r es
    ) =>
    GetCmdArg -> Eff es ()
handleGet (GetCmdArg targetKey) = do
    env <- ReaderEff.ask @r
    currentTime <- Time.getCurrentTime

    let socket = view #clientSocket env
    let serverState = view #serverState env

    output <- Eff.atomically $ do
        kvStore <- readTVar serverState.keyValueStoreRef
        let targetItemM = HashMap.lookup targetKey kvStore
        case targetItemM of
            Nothing -> pure . Right $ Null
            Just (StoreValue valForTargetKey ttlM) -> do
                case valForTargetKey of
                    (MkRedisStr (RedisStr val)) -> do
                        case ttlM of
                            Nothing -> pure . Right $ mkNonNullBulkString val
                            Just ttl -> do
                                let keyValTTLHasExpired = mkUnixTimestampMSFromUTCTime currentTime > ttl
                                pure . bool (Right $ mkNonNullBulkString val) (Right nullBulkString) $ keyValTTLHasExpired
                    x -> do
                        let actualValType = showRedisDataType @ByteString x
                        pure . Left $ [i|(error) GET command only supports string values, but value at key #{targetKey} was of type #{actualValType}|]

    sendMessage socket . either id serializeRESPDataType $ output
