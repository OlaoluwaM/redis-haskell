{-# OPTIONS_GHC -Wno-unrecognised-pragmas #-}

{-# HLINT ignore "Use newtype instead of data" #-}

module Redis.Commands.Parser (
    commandParser,
    mkInvalidCommand,
    Command (..),

    -- ** Testing
    ConfigSubCommand (..),
) where

import Data.Attoparsec.ByteString.Char8 qualified as AC
import Data.List.NonEmpty qualified as NE

import Control.Applicative.Combinators (count)
import Data.Aeson (Options (constructorTagModifier, sumEncoding), SumEncoding (..), ToJSON (toJSON), defaultOptions, genericToJSON)
import Data.Attoparsec.ByteString (Parser)
import Data.ByteString (ByteString)
import Data.Char (toUpper)
import Data.Either.Extra (maybeToEither)
import Data.Functor (($>))
import Data.List.NonEmpty (NonEmpty)
import Data.String.Interpolate (i)
import Data.Text (Text)
import GHC.Generics (Generic)
import Redis.Commands.Config.Get (ConfigGetCmdArg, mkConfigGetCmdArg)
import Redis.Commands.Echo (EchoCmdArg, mkEchoCmdArg)
import Redis.Commands.Get (GetCmdArg, mkGetCmdArg)
import Redis.Commands.Ping (PingCmdArg, mkPingCmdArg)
import Redis.Commands.Save (mkSaveCmdArg)
import Redis.Commands.Set (SetCmdArg, mkSetCmdArg)
import Redis.Helpers (withCustomError)
import Redis.Orphans ()
import Redis.RESP (
    BulkString (..),
    bulkStringParser,
    mapBulkString,
    terminatorSeqParser,
 )
import Redis.Utils (toUpperBs)

data ParsedCommand = ParsedCommand {command :: BulkString, args :: [BulkString]}
    deriving stock (Eq, Show)

-- NOTE: Remember to update the `mkCommand` function if you add new commands
data Command = Ping PingCmdArg | Echo EchoCmdArg | Set SetCmdArg | Get GetCmdArg | Config ConfigSubCommand | Save | InvalidCommand Text
    deriving stock (Eq, Show, Generic)

data ConfigSubCommand = ConfigGet ConfigGetCmdArg
    deriving stock (Eq, Show, Generic)
    deriving anyclass (ToJSON)

instance ToJSON Command where
    toJSON =
        genericToJSON
            defaultOptions
                { constructorTagModifier = map toUpper
                , sumEncoding = TaggedObject{tagFieldName = "command", contentsFieldName = "arguments"}
                }

-- NOTE: For now, we're working with the assumption that a request can only have one command at any given time even when pipelining
commandParser :: Parser Command
commandParser = do
    arrLength <- withCustomError (AC.char '*' *> (AC.signed @Int) AC.decimal <* terminatorSeqParser) "Invalid RESP Command"
    isEndOfInput <- AC.atEnd

    case (arrLength == 0, isEndOfInput) of
        (False, True) -> fail "RESP command string input ended too early"
        (True, False) -> fail "RESP command string input ended too early"
        (True, True) -> fail "RESP command string should not be empty"
        _ -> pure ()

    commandInfo <- withCustomError (NE.fromList <$> count arrLength bulkStringParser) [i|Mismatch between purported RESP array length (#{arrLength}) and actual length|] >>= mkParsedCommand
    mkCommand commandInfo

mkParsedCommand :: (MonadFail m) => NonEmpty BulkString -> m ParsedCommand
mkParsedCommand (NullBulkString NE.:| _) = fail "A null bulk string is an invalid command"
mkParsedCommand (cmdBulkStr NE.:| bulkStrArgs)
    | NullBulkString `elem` bulkStrArgs = fail "Command arguments cannot contain null bulk strings"
    | otherwise = pure $ ParsedCommand (mapBulkString toUpperBs cmdBulkStr) bulkStrArgs

mkCommand :: ParsedCommand -> Parser Command
mkCommand (ParsedCommand NullBulkString _) = fail "A null bulk string is not a valid command"
mkCommand (ParsedCommand (BulkString cmdStr) parsedArgs) = case (cmdStr, parsedArgs) of
    ("PING", xs) -> Ping <$> mkPingCmdArg xs
    ("ECHO", xs) -> Echo <$> mkEchoCmdArg xs
    ("SET", xs) -> Set <$> mkSetCmdArg xs
    ("GET", xs) -> Get <$> mkGetCmdArg xs
    ("CONFIG", xs) -> do
        let subCommandE = parseSubCommandFor "CONFIG" xs
        subCommand <- case subCommandE of
            Left errStr -> fail errStr
            Right (ParsedCommand NullBulkString _) -> fail "The CONFIG command requires a sub command"
            Right (ParsedCommand (BulkString "GET") x) -> ConfigGet <$> mkConfigGetCmdArg x
            Right (ParsedCommand (BulkString unimplementedSubCommandStr) _) -> handleUnimplementedSubCmd "CONFIG" unimplementedSubCommandStr ["SET"]
        pure $ Config subCommand
    ("SAVE", xs) -> mkSaveCmdArg xs $> Save
    (unimplementedCommandStr, _) -> handleUnimplementedCmd unimplementedCommandStr

parseSubCommandFor :: String -> [BulkString] -> Either String ParsedCommand
parseSubCommandFor topLevelCmd xs = maybeToEither [i|"The #{topLevelCmd} command requires a sub command"|] (NE.nonEmpty xs) >>= mkParsedCommand @(Either String)

handleUnimplementedCmd :: ByteString -> Parser Command
handleUnimplementedCmd unimplementedCommandStr =
    let validButNotImplementedCmds = []
     in if unimplementedCommandStr `elem` validButNotImplementedCmds
            then fail [i|The command '#{unimplementedCommandStr}' has not yet been implemented|]
            else fail [i|The command '#{unimplementedCommandStr}' is invalid|]

handleUnimplementedSubCmd :: ByteString -> ByteString -> [ByteString] -> Parser a
handleUnimplementedSubCmd unimplementedSubCommandStr topLevelCmd validButNotImplementedSubCmds =
    if unimplementedSubCommandStr `elem` validButNotImplementedSubCmds
        then fail [i|The sub command '#{unimplementedSubCommandStr}' for the #{topLevelCmd} command has not yet been implemented|]
        else fail [i|The command '#{unimplementedSubCommandStr}' is not a valid sub command for the #{topLevelCmd} command|]

mkInvalidCommand :: Text -> Command
mkInvalidCommand = InvalidCommand
