module Redis.Commands.Set (
    SetCmdArg (..),
    mkSetCmdArg,
    handleSet,

    -- ** Testing
    setupTTLCalculation,
    TTLOption (..),
    SetCmdOpts (..),
    SetCondition (..),
) where

import Data.Attoparsec.ByteString.Char8 qualified as AC
import Data.HashMap.Strict qualified as HashMap

import Control.Applicative (Alternative (some, (<|>)), asum)
import Control.Applicative.Combinators (skipSomeTill)
import Control.Applicative.Permutations (
    intercalateEffect,
    toPermutationWithDefault,
 )
import Control.Concurrent.STM (atomically, newTVar, readTVar, readTVarIO, writeTVar)
import Control.Monad.IO.Class (MonadIO (..))
import Control.Monad.Reader (MonadReader (..))
import Data.Aeson (ToJSON (..), object, (.=))
import Data.Attoparsec.ByteString (Parser)
import Data.Attoparsec.Combinator ((<?>))
import Data.Bool (bool)
import Data.ByteString (ByteString)
import Data.Default (Default (def))
import Data.Either (fromRight)
import Data.Either.Extra (fromEither)
import Data.Functor (($>), (<&>))
import Data.String.Interpolate (i)
import Data.Tagged (Tagged (Tagged), untag)
import Data.Text (Text)
import Data.Text.Encoding (decodeUtf8')
import Data.Time (NominalDiffTime, UTCTime, addUTCTime)
import Data.Time.Clock.POSIX (POSIXTime, getCurrentTime, posixSecondsToUTCTime)
import GHC.Generics (Generic)
import Network.Socket (Socket)
import Optics (A_Lens, LabelOptic, view)
import Redis.RESP (BulkString (..), RESPDataType (Null, SimpleString), mkNonNullBulkString, serializeRESPDataType, toOptionString)
import Redis.Server.ServerT (MonadSocket (..))
import Redis.Store (StoreKey (..), StoreState, StoreValue (..), mkStoreValue)
import Redis.Store.Data (
    RedisDataType (..),
    RedisStr (..),
    showRedisDataType,
 )
import Redis.Utils (millisecondsToSeconds)

data SetCmdArg = SetCmdArg {key :: StoreKey, val :: ByteString, opts :: SetCmdOpts}
    deriving stock (Eq, Show, Generic)

instance ToJSON SetCmdArg where
    toJSON (SetCmdArg (StoreKey key) val opts) =
        let optsAsJSON = toJSON opts
         in object
                [ "key" .= fromRight ("(blob)" :: Text) (decodeUtf8' key)
                , "value" .= fromRight ("(blob)" :: Text) (decodeUtf8' val)
                , "options" .= optsAsJSON
                ]

data SetCmdOpts = SetCmdOpts {setCondition :: SetCondition, returnOldVal :: Bool, ttlOption :: TTLOption}
    deriving stock (Eq, Show, Generic)
    deriving anyclass (ToJSON)

-- "Any previous time to live associated with the key is discarded on successful SET operation". That is from the redis documentation on the SET command
-- By default items set without an explicit TTL have no TTL, that is there is no default TTL in redis: https://stackoverflow.com/questions/49133314/whats-default-ttl-in-redis

-- We are making the PX and PXAT types Doubles because they're in milliseconds and we don't want to lose precision when converting to seconds (we lose precision if we convert to seconds using integer division).
-- We *could* make EX a Double too, if only to allow users to ability to express more precise TTL offsets with the option, but then what would be the point of the PX option? Following this line of reasoning, I think it would be best to just keep EX as an integer
-- TODO: Explore using POSIXTime for the PXAT option here, you'll need to convert it to milliseconds before storing/using it
data TTLOption = EX (Tagged Seconds Integer) | PX (Tagged MilliSeconds Double) | EXAT (Tagged UnixEpochTimeSeconds POSIXTime) | PXAT (Tagged UnixEpochTimeMilliSeconds Double) | KeepTTL | DiscardTTL
    deriving stock (Eq, Show, Ord, Generic)
    deriving anyclass (ToJSON)

-- Determines how the SET command works. Does it a) set the target key to the specified value regardless of whether the key already exists (Always, the default)
-- b) only set if the target key exists (OnlyIfKeyExists)
-- c) only set if the target key does not exist (OnlyIfKeyDoesNotExist)
data SetCondition = Always | OnlyIfKeyExists | OnlyIfKeyDoesNotExist
    deriving stock (Eq, Show, Generic)
    deriving anyclass (ToJSON)

data Seconds
data MilliSeconds
data UnixEpochTimeSeconds
data UnixEpochTimeMilliSeconds

instance Default SetCmdOpts where
    def = SetCmdOpts{setCondition = Always, returnOldVal = False, ttlOption = def @TTLOption}

instance Default TTLOption where
    def = DiscardTTL

parseSetCmdOptions :: Parser SetCmdOpts
parseSetCmdOptions =
    let defaultCmdOpts = def @SetCmdOpts
     in intercalateEffect
            (some AC.space)
            ( mkSetCmdOpts
                <$> toPermutationWithDefault defaultCmdOpts.setCondition nxOrXXParser
                <*> toPermutationWithDefault defaultCmdOpts.returnOldVal getOptionParser
                <*> toPermutationWithDefault defaultCmdOpts.ttlOption ttlOptionParser
            )
  where
    getOptionParser = AC.stringCI "GET" $> True <?> "GET option parser"
    nxOrXXParser = (AC.stringCI "NX" $> OnlyIfKeyDoesNotExist) <|> (AC.stringCI "XX" $> OnlyIfKeyExists) <?> "NX || XX option parser"

    ttlOptionParser :: Parser TTLOption
    ttlOptionParser =
        asum
            [ numberedTTLOptionParser "EX" <&> EX . Tagged . floor -- rounding down to avoid creating time out of thin air
            , numberedTTLOptionParser "PX" <&> PX . Tagged
            , numberedTTLOptionParser "EXAT" <&> EXAT . Tagged . fromInteger . floor
            , numberedTTLOptionParser "PXAT" <&> PXAT . Tagged
            , KeepTTL <$ AC.stringCI "KEEPTTL"
            ]
            <?> "TTL option parser"

    numberedTTLOptionParser :: ByteString -> Parser Double
    numberedTTLOptionParser numberedTTLOptionStr = AC.stringCI numberedTTLOptionStr *> skipSomeTill AC.space AC.double

mkSetCmdOpts :: SetCondition -> Bool -> TTLOption -> SetCmdOpts
mkSetCmdOpts = SetCmdOpts

mkSetCmdArg :: (MonadFail m) => [BulkString] -> m SetCmdArg
mkSetCmdArg [] = fail "SET command requires at least 2 arguments. None were provided"
mkSetCmdArg [BulkString key, BulkString val] = pure $ SetCmdArg{key = StoreKey key, val, opts = def @SetCmdOpts}
mkSetCmdArg (BulkString key : BulkString val : cmdOpts) = do
    let cmdOptsBytestring = toOptionString cmdOpts
    let result = AC.parseOnly parseSetCmdOptions cmdOptsBytestring
    either fail (\setCmdOpts -> pure $ SetCmdArg{key = StoreKey key, val, opts = setCmdOpts}) result
mkSetCmdArg _ = fail "SET command requires at least 2 arguments. None were provided"

handleSet ::
    ( LabelOptic "clientSocket" A_Lens r r Socket Socket
    , LabelOptic "store" A_Lens r r StoreState StoreState
    , MonadReader r m
    , MonadSocket m b
    , MonadIO m
    ) =>
    SetCmdArg -> m b
handleSet (SetCmdArg key newVal opts) = do
    env <- ask

    let socket = view #clientSocket env
    let kvStoreState = view #store env
    let shouldReturnOldKeyValue = opts.returnOldVal
    let setCondition = opts.setCondition

    currentTime <- liftIO getCurrentTime
    kvStore <- liftIO $ readTVarIO kvStoreState

    let calculateTTL = setupTTLCalculation currentTime
    let defaultResponse = SimpleString "OK"

    output <- liftIO $ atomically $ do
        currentStoreValAtKeyM <- maybe (pure Nothing) (fmap Just . readTVar) $ HashMap.lookup key kvStore

        case (currentStoreValAtKeyM, setCondition) of
            (Nothing, OnlyIfKeyExists) -> pure (Right Null)
            (Just _, OnlyIfKeyDoesNotExist) -> pure (Right Null)
            (Nothing, _) -> do
                let newTTLForItemAtKey = calculateTTL Nothing opts.ttlOption
                newStoreValAtKey <- newTVar $ mkStoreValue (MkRedisStr . RedisStr $ newVal) currentTime newTTLForItemAtKey
                writeTVar kvStoreState (HashMap.insert key newStoreValAtKey kvStore)
                pure . Right . bool defaultResponse Null $ shouldReturnOldKeyValue
            (Just currentStoreValAtKey, _) -> do
                let (StoreValue currentValAtKey _ currentTTLForItem) = currentStoreValAtKey
                let newTTLForItemAtKey = calculateTTL currentTTLForItem opts.ttlOption
                case currentValAtKey of
                    (MkRedisStr (RedisStr prevValAtKey)) -> do
                        newStoreValAtKey <- newTVar $ mkStoreValue (MkRedisStr . RedisStr $ newVal) currentTime newTTLForItemAtKey
                        writeTVar kvStoreState (HashMap.insert key newStoreValAtKey kvStore)
                        pure . Right . bool defaultResponse (mkNonNullBulkString prevValAtKey) $ shouldReturnOldKeyValue
                    x -> pure $ do
                        let valTypeAsStr = showRedisDataType @ByteString x
                        Left @ByteString [i|(error) SET can only used on keys holding string values, but the value at key #{key} is of type #{valTypeAsStr}|]

    sendThroughSocket socket . fromEither . fmap serializeRESPDataType $ output

setupTTLCalculation :: UTCTime -> Maybe UTCTime -> TTLOption -> Maybe UTCTime
setupTTLCalculation currentTime currentTTLForItem = \case
    (EX t) -> Just $ addUTCTime (fromInteger @NominalDiffTime (untag t)) currentTime
    (PX t) -> Just $ addUTCTime (realToFrac @_ @NominalDiffTime $ millisecondsToSeconds (untag t)) currentTime -- t is in milliseconds so we must convert to seconds
    (EXAT t) -> Just $ posixSecondsToUTCTime (untag t)
    (PXAT t) -> Just $ posixSecondsToUTCTime . realToFrac . millisecondsToSeconds $ untag t -- t is POSIX milliseconds so we must convert it to seconds
    KeepTTL -> currentTTLForItem
    DiscardTTL -> Nothing
