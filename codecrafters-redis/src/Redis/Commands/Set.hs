module Redis.Commands.Set (
    SetCmdArg (..),
    mkSetCmdArg,
    handleSet,
    defaultSetCmdOpts,
    defaultTTLOption,

    -- ** Testing
    setupTTLCalculation,
    TTLOption (..),
    SetCmdOpts (..),
    SetCondition (..),
) where

import Control.Concurrent.STM
import Redis.RESP
import Redis.ServerState
import Redis.Store.Timestamp

import Data.Attoparsec.ByteString.Char8 qualified as AC
import Data.HashMap.Strict qualified as HashMap
import Effect.Time qualified as Time
import Effectful.Concurrent.STM qualified as STMEff
import Effectful.Reader.Static qualified as ReaderEff

import Control.Applicative (Alternative (some, (<|>)), asum)
import Control.Applicative.Combinators (skipSomeTill)
import Control.Applicative.Permutations (
    intercalateEffect,
    toPermutationWithDefault,
 )
import Data.Aeson (ToJSON (..), object, (.=))
import Data.Attoparsec.ByteString (Parser)
import Data.Attoparsec.Combinator ((<?>))
import Data.Bool (bool)
import Data.ByteString (ByteString)
import Data.Either (fromRight)
import Data.Either.Extra (fromEither)
import Data.Functor (($>), (<&>))
import Data.String.Interpolate (i)
import Data.Tagged (Tagged (Tagged), untag)
import Data.Text (Text)
import Data.Text.Encoding (decodeUtf8')
import Effect.Communication (sendMessage)
import Effectful (Eff)
import GHC.Generics (Generic)
import Optics (view)
import Redis.Effects (RedisClientCommunication, RedisServerState)
import Redis.Store.Data (
    RedisDataType (..),
    RedisStr (..),
    showRedisDataType,
 )
import Redis.Utils (secondsToMilliseconds)

data SetCmdArg = SetCmdArg {key :: StoreKey, val :: ByteString, opts :: SetCmdOpts}
    deriving stock (Eq, Show, Generic)

instance ToJSON SetCmdArg where
    toJSON (SetCmdArg (StoreKey key) val opts) =
        let optsAsJSON = toJSON opts
         in object
                [ "key" .= fromRight ("(blob)" :: Text) (decodeUtf8' key)
                , "value" .= fromRight ("(blob)" :: Text) (decodeUtf8' val)
                , "options" .= optsAsJSON
                ]

data SetCmdOpts = SetCmdOpts {setCondition :: SetCondition, returnOldVal :: Bool, ttlOption :: TTLOption}
    deriving stock (Eq, Show, Generic)
    deriving anyclass (ToJSON)

-- "Any previous time to live associated with the key is discarded on successful SET operation". That is from the redis documentation on the SET command
-- By default items set without an explicit TTL have no TTL, that is there is no default TTL in redis: https://stackoverflow.com/questions/49133314/whats-default-ttl-in-redis

-- We are making the PX and PXAT types Doubles because they're in milliseconds and we don't want to lose precision when converting to seconds (we lose precision if we convert to seconds using integer division).
-- We *could* make EX a Double too, if only to allow users to ability to express more precise TTL offsets with the option, but then what would be the point of the PX option? Following this line of reasoning, I think it would be best to just keep EX as an integer
-- TODO: Explore using POSIXTime for the PXAT option here, you'll need to convert it to milliseconds before storing/using it
data TTLOption = EX (Tagged Seconds Word) | PX (Tagged MilliSeconds Word) | EXAT (Tagged UnixEpochTimeSeconds Word) | PXAT (Tagged UnixEpochTimeMilliSeconds Word) | KeepTTL | DiscardTTL
    deriving stock (Eq, Show, Ord, Generic)
    deriving anyclass (ToJSON)

-- Determines how the SET command works. Does it a) set the target key to the specified value regardless of whether the key already exists (Always, the default)
-- b) only set if the target key exists (OnlyIfKeyExists)
-- c) only set if the target key does not exist (OnlyIfKeyDoesNotExist)
data SetCondition = Always | OnlyIfKeyExists | OnlyIfKeyDoesNotExist
    deriving stock (Eq, Show, Generic)
    deriving anyclass (ToJSON)

data Seconds
data MilliSeconds
data UnixEpochTimeSeconds
data UnixEpochTimeMilliSeconds

defaultSetCmdOpts :: SetCmdOpts
defaultSetCmdOpts = SetCmdOpts{setCondition = Always, returnOldVal = False, ttlOption = defaultTTLOption}

defaultTTLOption :: TTLOption
defaultTTLOption = DiscardTTL

parseSetCmdOptions :: Parser SetCmdOpts
parseSetCmdOptions =
    let defaultCmdOpts = defaultSetCmdOpts
     in intercalateEffect
            (some AC.space)
            ( mkSetCmdOpts
                <$> toPermutationWithDefault defaultCmdOpts.setCondition nxOrXXParser
                <*> toPermutationWithDefault defaultCmdOpts.returnOldVal getOptionParser
                <*> toPermutationWithDefault defaultCmdOpts.ttlOption ttlOptionParser
            )
  where
    getOptionParser = AC.stringCI "GET" $> True <?> "GET option parser"
    nxOrXXParser = (AC.stringCI "NX" $> OnlyIfKeyDoesNotExist) <|> (AC.stringCI "XX" $> OnlyIfKeyExists) <?> "NX || XX option parser"

    ttlOptionParser :: Parser TTLOption
    ttlOptionParser =
        -- We can probably keep asum here since we're okay with returning empty should all the parsers fail
        asum
            [ numberedTTLOptionParser "EX" <&> EX . Tagged -- rounding down to avoid creating time out of thin air
            , numberedTTLOptionParser "PX" <&> PX . Tagged
            , numberedTTLOptionParser "EXAT" <&> EXAT . Tagged
            , numberedTTLOptionParser "PXAT" <&> PXAT . Tagged
            , KeepTTL <$ AC.stringCI "KEEPTTL"
            ]
            <?> "TTL option parser"

    numberedTTLOptionParser :: ByteString -> Parser Word
    numberedTTLOptionParser numberedTTLOptionStr = AC.stringCI numberedTTLOptionStr *> skipSomeTill AC.space AC.decimal

mkSetCmdOpts :: SetCondition -> Bool -> TTLOption -> SetCmdOpts
mkSetCmdOpts = SetCmdOpts

mkSetCmdArg :: (MonadFail m) => [BulkString] -> m SetCmdArg
mkSetCmdArg [] = fail "SET command requires at least 2 arguments. None were provided"
mkSetCmdArg [BulkString key, BulkString val] = pure $ SetCmdArg{key = StoreKey key, val, opts = defaultSetCmdOpts}
mkSetCmdArg (BulkString key : BulkString val : cmdOpts) = do
    let cmdOptsBytestring = toOptionString cmdOpts
    let result = AC.parseOnly parseSetCmdOptions cmdOptsBytestring
    either fail (\setCmdOpts -> pure $ SetCmdArg{key = StoreKey key, val, opts = setCmdOpts}) result
mkSetCmdArg _ = fail "SET command requires at least 2 arguments. None were provided"

handleSet ::
    forall r es.
    ( RedisClientCommunication r es
    , RedisServerState r es
    ) =>
    SetCmdArg -> Eff es ()
handleSet (SetCmdArg key newVal opts) = do
    env <- ReaderEff.ask @r

    let socket = view #clientSocket env
    let serverState = view #serverState env

    let shouldReturnOldKeyValue = opts.returnOldVal
    let setCondition = opts.setCondition

    currentTime <- fmap mkUnixTimestampMSFromUTCTime Time.getCurrentTime

    let calculateTTL = setupTTLCalculation currentTime
    let defaultResponse = SimpleString "OK"

    output <- STMEff.atomically $ do
        let kvStoreStateRef = serverState.keyValueStoreRef
        kvStore <- readTVar kvStoreStateRef
        let currentStoreValAtKeyM = HashMap.lookup key kvStore

        case (currentStoreValAtKeyM, setCondition) of
            (Nothing, OnlyIfKeyExists) -> pure (Right Null)
            (Just _, OnlyIfKeyDoesNotExist) -> pure (Right Null)
            (Nothing, _) -> do
                let newTTLForItemAtKey = calculateTTL Nothing opts.ttlOption
                let newStoreValAtKey = mkStoreValue (MkRedisStr . RedisStr $ newVal) newTTLForItemAtKey
                writeTVar kvStoreStateRef (HashMap.insert key newStoreValAtKey kvStore)
                pure . Right . bool defaultResponse Null $ shouldReturnOldKeyValue
            (Just currentStoreValAtKey, _) -> do
                let (StoreValue currentValAtKey currentTTLForItem) = currentStoreValAtKey
                let newTTLForItemAtKey = calculateTTL currentTTLForItem opts.ttlOption
                case currentValAtKey of
                    (MkRedisStr (RedisStr prevValAtKey)) -> do
                        let newStoreValAtKey = mkStoreValue (MkRedisStr . RedisStr $ newVal) newTTLForItemAtKey
                        writeTVar kvStoreStateRef (HashMap.insert key newStoreValAtKey kvStore)
                        pure . Right . bool defaultResponse (mkNonNullBulkString prevValAtKey) $ shouldReturnOldKeyValue
                    x -> pure $ do
                        let valTypeAsStr = showRedisDataType @ByteString x
                        Left @ByteString [i|(error) SET can only used on keys holding string values, but the value at key #{key} is of type #{valTypeAsStr}|]

    sendMessage socket . fromEither . fmap serializeRESPDataType $ output

setupTTLCalculation :: UnixTimestampMS -> Maybe UnixTimestampMS -> TTLOption -> Maybe UnixTimestampMS
setupTTLCalculation currentTimestampMS existingItemTTL = \case
    (EX ttlOffsetInSeconds) -> Just (UnixTimestampMS (secondsToMilliseconds (untag ttlOffsetInSeconds)) + currentTimestampMS)
    (PX ttlOffsetInMilliseconds) -> Just (UnixTimestampMS (untag ttlOffsetInMilliseconds) + currentTimestampMS)
    (EXAT ttlTimestampInSeconds) -> Just $ UnixTimestampMS (secondsToMilliseconds (untag ttlTimestampInSeconds))
    (PXAT ttlTimestampInMilliseconds) -> Just $ UnixTimestampMS (untag ttlTimestampInMilliseconds)
    KeepTTL -> existingItemTTL
    DiscardTTL -> Nothing
