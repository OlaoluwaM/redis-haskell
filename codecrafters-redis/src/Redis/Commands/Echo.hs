module Redis.Commands.Echo (
    EchoCmdArg (..),
    handleEcho,
    mkEchoCmdArg,
) where

import Effectful.Reader.Static qualified as ReaderEff

import Data.Aeson (ToJSON)
import Data.Text (Text)
import Data.Text.Encoding (decodeUtf8', encodeUtf8)
import Effect.Communication (sendMessage)
import Effectful (Eff)
import GHC.Generics (Generic)
import Optics (view)
import Redis.Effects (RedisClientCommunication)
import Redis.RESP (BulkString (BulkString), mkNonNullBulkString, serializeRESPDataType)

-- https://redis.io/docs/latest/commands/echo/
-- We assume the option `message` argument of the echo command should only ever be considered as text

newtype EchoCmdArg = EchoCmdArg Text
    deriving stock (Eq, Show, Generic)
    deriving anyclass (ToJSON)

mkEchoCmdArg :: (MonadFail m) => [BulkString] -> m EchoCmdArg
mkEchoCmdArg [] = fail "ECHO command requires at least 1 argument. None were provided"
mkEchoCmdArg [BulkString txt] = case decodeUtf8' txt of
    (Right msg) -> pure . EchoCmdArg $ msg
    Left _ -> fail "Message received as argument for ECHO command is not valid text"
mkEchoCmdArg _ = fail "ECHO command requires only 1 argument"

handleEcho :: forall r es. (RedisClientCommunication r es) => EchoCmdArg -> Eff es ()
handleEcho (EchoCmdArg msg) = do
    env <- ReaderEff.ask @r
    let socket = view #clientSocket env
    sendMessage socket . serializeRESPDataType . mkNonNullBulkString . encodeUtf8 $ msg
