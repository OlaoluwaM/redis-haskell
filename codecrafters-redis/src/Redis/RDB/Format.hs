module Redis.RDB.Format (
    KeyValueOpCode (..),
    EOF (..),
    KeyValWithExpiryInMS (..),
    KeyValWithExpiryInS (..),
    KeyValWithNoExpiryInfo (..),
    AuxField (..),
    ValueType (..),
    RDBFile (..),
    RDBMagicString (..),
    RDBVersion (..),
    RDbEntry (..),
    SelectDB (..),
    ResizeDB (..),
    RedisVersion (..),
    RedisBits (..),
    CTime (..),
    UsedMem (..),

    -- ** For Testing
    defaultChecksum,
) where

import Redis.RDB.Binary
import Redis.RDB.Data

import Control.Monad.State.Strict qualified as State
import Data.ByteString.Char8 qualified as BSC

import Bits.Show (showFiniteBits)
import Control.Applicative (asum, (<|>))
import Control.Applicative.Combinators (many, manyTill)
import Control.Monad (when)
import Control.Monad.Reader (ask)
import Control.Monad.Trans (lift)
import Data.Attoparsec.ByteString.Char8 qualified as AC
import Data.Binary.Get (
    getByteString,
    getWord64le,
 )
import Data.Binary.Put (
    putByteString,
    putWord64le,
 )
import Data.Default (Default (..))
import Data.Word (Word8)
import Redis.RDB.CRC64 (CheckSum, fromChecksum, toChecksum)
import Redis.RDB.Config (RDBConfig (..))
import Redis.Utils (genericShow)
import Rerefined (unrefine)
import Rerefined.Refine.TH (refineTH)

{- | Redis RDB Format Implementation

    This module provides an implementation of the Redis RDB (Redis Database)
    binary file format used for database persistence and snapshots.

    The RDB format is one of Redis's primary methods for creating point-in-time snapshots
    of the entire database state, enabling backup, replication, and restart functionality.

    Core capabilities:
    - Compact variable-length encoding to minimize file size
    - Support for some Redis data types (strings, and numbers for now, lists, sets, hashes, and sorted sets tbd)
    - Support for lzf compression
    - Precise expiry tracking (second or millisecond granularity)
    - Rich metadata including Redis version, architecture, and creation time
    - Data integrity protection via optional CRC64 checksums
    - Superficial multi-database support

    Binary File Structure:
    The RDB file follows a strictly defined binary layout:

    1. File Header:
       - Magic signature: "REDIS" (5 ASCII bytes)
       - Format version: 4-digit ASCII string (e.g., "0003", "0009")

    2. Global Metadata (Auxiliary Fields):
       - Redis version that created the file
       - Server architecture (32-bit vs 64-bit)
       - File creation timestamp
       - Memory usage at save time
       - Other custom auxiliary fields

    3. Database Sections (one per Redis database):
       - Database selector (specifies which DB: 0-15)
       - Size hints for memory pre-allocation
       - Key-value entries with optional expiry times

    4. File Termination:
       - EOF marker (single 0xFF byte)
       - Optional CRC64 checksum for integrity verification

    Space-Efficient Variable Length Encoding:
    Redis employs an encoding scheme to minimize storage overhead.
    String lengths and integer values use a compact representation where the first
    2 bits of the length byte determine the encoding strategy:

    - 00xxxxxx: Short strings (0-63 bytes) - length in remaining 6 bits
    - 01xxxxxx xxxxxxxx: Medium strings (0-16383 bytes) - length in 14 bits
    - 10xxxxxx [4 bytes]: Large strings (up to 4GB) - length in next 32 bits
    - 11xxxxxx: Special encodings for integers stored as strings

    This approach can reduce file size by 20-40% compared to fixed-width encoding since data is stored with only as much space as it needs

    Control Opcodes:
    The RDB format uses single-byte opcodes to mark different section types:

    - 0xFF: END_OF_FILE - Signals the end of RDB data
    - 0xFE: SELECT_DB - Switches to a different Redis database (0-15)
    - 0xFD: EXPIRE_TIME - Key expires at specific Unix timestamp (seconds)
    - 0xFC: EXPIRE_TIME_MS - Key expires at specific timestamp (milliseconds)
    - 0xFB: RESIZE_DB - Memory allocation hints for hash table sizing
    - 0xFA: AUXILIARY - Metadata fields (version, architecture, timestamps)

    These opcodes allow parsers to efficiently navigate the file structure and
    handle different data sections appropriately.

    https://rdb.fnordig.de/file_format.html
-}

{- | Redis RDB file representation.

    This is the top-level structure that represents an entire RDB file in memory.
    The structure mirrors the binary file format exactly:

    - File identification through magic string and version
    - Global metadata via auxiliary fields
    - Database contents organized by database index
    - Optional integrity checking via CRC64 checksum

    Most Redis deployments use only database 0, but the format supports up to 16
    databases (0-15) to enable logical data separation within a single Redis instance.
    Each database is completely independent with its own key space.
-}
data RDBFile = RDBFile
    { magicString :: RDBMagicString
    -- ^ File format identifier - must be exactly "REDIS"
    , version :: RDBVersion
    -- ^ RDB format version (e.g., "0003", "0009") determining encoding rules
    , auxFieldEntries :: [AuxField]
    -- ^ Global metadata: Redis version, architecture, creation time, memory stats, etc...
    , dbEntries :: [RDbEntry]
    -- ^ Database contents - typically just database 0, but can include 0-15
    }
    deriving stock (Show, Eq)

{- | Individual Redis database within an RDB file.

    Redis supports multiple logical databases (0-15) within a single instance,
    though most applications use one database as having multiple in a single instance is discouraged in favor of multiple instances.
    https://stackoverflow.com/questions/16221563/whats-the-point-of-multiple-redis-databases

    Nevertheless, each database has:

    - A unique identifier (database index)
    - Memory optimization hints for efficient loading
    - A collection of key-value pairs with optional expiry times

    The resize hints help Redis pre-allocate appropriate hash table sizes during
    loading, improving performance especially for large databases. These hints
    are estimates and the actual number of keys may differ.
-}
data RDbEntry = RDbEntry
    { entryId :: SelectDB
    -- ^ Database identifier (0-15) - which logical Redis database these keys belong to
    , resizeDBEntry :: ResizeDB
    -- ^ Performance hints for memory allocation during database loading
    , keyValEntries :: [KeyValueOpCode]
    -- ^ All key-value pairs stored in this database, with optional expiry data
    }
    deriving stock (Show, Eq)

{- | File format identifier for Redis RDB files.
    Every valid RDB file must begin with exactly these 5 ASCII bytes: "REDIS".
    This is a quick sanity check to know we are dealing with a redis rdb file.
    If these bytes are missing or incorrect, the file is probably not a valid RDB file.
-}
data RDBMagicString = Redis
    deriving stock (Show, Eq)

{- | RDB format version identifier.

    The version is stored as exactly 4 ASCII digits (e.g., "0003", "0009") and
    determines which encoding schemes, opcodes, and data layouts are valid.
    Higher version numbers may introduce new features while maintaining backward
    compatibility for reading older files.

    Version evolution:
    - "0001": Redis 1.0 (basic string support)
    - "0002": Redis 1.2 (added list and set types)
    - "0003": Redis 2.0+ (hash support, improved encoding)
    - "0006": Redis 2.6+ (sorted sets, expires)
    - "0007": Redis 3.2+ (quicklist, geospatial)
    - "0009": Redis 5.0+ (streams, modules)

    Each version builds upon previous ones, adding new capabilities while
    ensuring older RDB files remain readable.

    https://rdb.fnordig.de/version_history.html
-}

-- Currently, we only support version 7, though, for now, we only support a subset of the features of version 7
-- We need to add support for non standard RDB versions for the codecrafters tests
data RDBVersion = RDBv4 | RDBv5 | RDBv7 | CustomVer (Char, Char, Char, Char)
    deriving stock (Show, Eq, Ord)

{- | Key-value entry types with different expiry behaviors.

    Redis stores three fundamentally different types of key-value entries:

    1. Keys with millisecond-precision expiry (0xFC opcode)
       - Useful for short-lived keys requiring precise timing
       - Timestamp stored as 64-bit milliseconds since Unix epoch

    2. Keys with second-precision expiry (0xFD opcode)
       - Useful for longer-lived keys where exact timing isn't critical
       - Timestamp stored as 32-bit seconds since Unix epoch (more compact)

    3. Keys without expiry (no opcode prefix)
       - Persistent keys that live until explicitly deleted
       - Most common type in typical Redis usage

    The choice between millisecond and second precision depends on the application's
    timing requirements and storage efficiency considerations.
-}

-- https://github.com/redis/redis/blob/38d16a82eb4a8b0e393b51cc3e9144dc7b413fd1/src/rdb.c#L1195
data KeyValueOpCode = FCOpCode KeyValWithExpiryInMS | KeyValOpCode KeyValWithNoExpiryInfo | FDOpcode KeyValWithExpiryInS
    deriving stock (Show, Eq)

{- | File termination marker.

    The EOF marker (single 0xFF byte) serves multiple critical purposes:
    - Definitively marks the end of RDB data content
    - Allows parsers to detect truncated or corrupted files
    - Enables validation that the entire file was written successfully
    - Separates data content from the optional checksum

    After the EOF marker, some RDB versions append an 8-byte CRC64 checksum.
-}
data EOF = EOF
    deriving stock (Show, Eq)

{- | Key-value pair with millisecond-precision expiry.

    This entry type is used when applications need precise control over key
    lifetimes, such as:
    - Session tokens with exact expiry times
    - Rate limiting with sub-second windows
    - Real-time cache entries
    - Time-sensitive security credentials

    Storage format: [0xFC][8-byte timestamp][value type][key][value]

    Trade-off: Uses 4 extra bytes (64 bits for millisecond vs 32 bits for seconds, 4 bytes difference) compared to second precision, but enables
    precise timing for applications that need it.
-}
data KeyValWithExpiryInMS = KeyValWithExpiryInMS
    { expiryTimeMs :: RDBUnixTimestampMS
    -- ^ Expiry timestamp (stored internally as seconds, converted to/from milliseconds during serialization)
    , valueType :: ValueType
    -- ^ Redis data type identifier (string, list, set, hash, etc.)
    , encodedKey :: RDBStringOrIntVal
    -- ^ Key name using Redis variable-length string encoding
    , encodedValue :: RDBStringOrIntVal
    -- ^ Value data encoded according to its specific type rules
    }
    deriving stock (Show, Eq)

{- | Key-value pair with second-precision expiry.

    This is the standard expiry format for most Redis use cases where exact
    sub-second timing isn't required:
    - Cache entries with minute/hour lifetimes
    - User sessions
    - Temporary data with longer durations
    - Application-level TTL management

    Storage format: [0xFD][4-byte timestamp][value type][key][value]

    This format saves 4 bytes per entry compared to millisecond precision,
    providing meaningful storage savings for large databases.

    Most Redis deployments use this format as it balances precision with efficiency.
-}
data KeyValWithExpiryInS = KeyValWithExpiryInS
    { expiryTimeS :: RDBUnixTimestampS
    -- ^ Expiry timestamp in Unix seconds (matches POSIXTime's native resolution)
    , valueType :: ValueType
    -- ^ Redis data type identifier (string, list, set, hash, etc.)
    , encodedKey :: RDBStringOrIntVal
    -- ^ Key name using Redis variable-length string encoding
    , encodedValue :: RDBStringOrIntVal
    -- ^ Value data encoded according to its specific type rules
    }
    deriving stock (Show, Eq)

{- | Persistent key-value pair (no expiry).

    This represents the most common type of Redis data - keys that persist
    indefinitely until explicitly deleted by application code or admin commands.

    Examples include:
    - Application configuration settings
    - User profile data
    - Permanent cache entries
    - Reference data and lookup tables

    Storage format: [value type][key][value]

    No opcode prefix is used with this entry type
-}
data KeyValWithNoExpiryInfo = KeyValWithNoExpiryInfo
    { valueType :: ValueType
    -- ^ Redis data type identifier (string, list, set, hash, etc.)
    , encodedKey :: RDBStringOrIntVal
    -- ^ Key name using Redis variable-length string encoding
    , encodedValue :: RDBStringOrIntVal
    -- ^ Value data encoded according to its specific type rules
    }
    deriving stock (Show, Eq)

{- | Database selection marker (opcode 0xFE).

    Redis supports up to 16 independent logical databases (numbered 0-15) within
    a single instance. This marker indicates which database the following key-value
    entries belong to.

    https://github.com/redis/redis/blob/acbcaae530035aab5480756e8e29e075398ea931/redis.conf#L381

    Database usage patterns:
    - Database 0: Default database used by most applications
    - Databases 1-15: Often used for testing, staging, or logical data separation
    - Many Redis deployments use only database 0 for simplicity

    The database selection remains active until another SelectDB marker is
    encountered or the file ends.
-}
newtype SelectDB
    = -- | Database index (0-15) identifying which logical Redis database contains the following keys
      SelectDB {dbIndex :: RDBLengthPrefix6} -- Using this type because this is how it is encoded
    deriving stock (Show, Eq)

instance Default SelectDB where
    def = SelectDB $$(refineTH 0) -- Default to database 0. Most redis instances use only database 0 as multiple databases in a single instance is discouraged in favor of multiple instances. https://stackoverflow.com/questions/16221563/whats-the-point-of-multiple-redis-databases

{- | Memory allocation optimization hints (opcode 0xFB).

    These hints help Redis pre-allocate appropriately sized hash tables during
    RDB loading, improving performance and reducing memory fragmentation.

    The hints are estimates based on the database state when the RDB was created:
    - Total key count helps size the main hash table
    - Expiry key count helps size the expiry tracking structures

    These are optimization hints only - the actual counts may differ and Redis
    will handle any discrepancies gracefully.
-}
data ResizeDB = ResizeDB
    { numOfKeys :: Word
    -- ^ Estimated total number of keys in this database (for hash table sizing)
    , numOfKeysWithExpiry :: Word
    -- ^ Estimated number of keys that have expiry times (for expiry tracking structures)
    }
    deriving stock (Show, Eq)

instance Default ResizeDB where
    def = ResizeDB 0 0

{- | Auxiliary field (opcode 0xFA) containing metadata.

    These fields store metadata about the Redis instance and RDB file creation:
    - Redis version that created the file (e.g., "7.0.0")
    - Architecture information (32-bit or 64-bit)
    - File creation timestamp (Unix time)
    - Memory usage statistics at save time

    Format: [0xFA][key-string][value-string]
    Both key and value are encoded using RDB variable-length string encoding.

    Common auxiliary fields:
    - "redis-ver": Redis version (e.g., "7.0.0")
    - "redis-bits": Architecture ("32" or "64")
    - "ctime": Creation timestamp
    - "used-mem": Memory usage in bytes
-}
data AuxField = AuxFieldRedisVer RedisVersion | AuxFieldRedisBits RedisBits | AuxFieldCTime CTime | AuxFieldUsedMem UsedMem | AuxFieldCustom RDBString RDBStringOrIntVal
    deriving stock (Show, Eq)

-- | Memory usage information in bytes at the time of RDB creation
newtype UsedMem = UsedMem Word
    deriving stock (Show, Eq)

-- | RDB file creation timestamp as Unix time
newtype CTime = CTime RDBUnixTimestampS
    deriving stock (Show, Eq)

-- | Redis version string that created this RDB file
newtype RedisVersion = RedisVersion RDBShortString
    deriving stock (Show, Eq)

-- | Architecture information (32-bit or 64-bit Redis build)
data RedisBits = RedisBits32 | RedisBits64
    deriving stock (Show, Eq)

{- | Redis value types supported in RDB format.

    Each value type has a specific encoding scheme:
    - String (0): Simple byte sequences, may use special integer encoding
    - List (1): Sequence of string elements
    - Set (2): Unordered collection of unique strings
    - Sorted Set (3): Ordered collection with scores
    - Hash (4): Key-value pairs (like a nested dictionary)
    - Zipmap (9): Compact hash representation for small hashes
    - Ziplist (10-14): Compact representations for small collections

    Currently only string types are implemented.
-}
data ValueType = Str
    deriving stock (Show, Eq, Ord)

{- | RDB format opcode constants
    These single-byte values mark different section types in the RDB file format
-}
eofOpCodeByteTag :: Word8
eofOpCodeByteTag = 0xFF

selectDBOpCodeByteTag :: Word8
selectDBOpCodeByteTag = 0xFE

keyValWithExpiryInSpCodeByteTag :: Word8
keyValWithExpiryInSpCodeByteTag = 0xFD

keyValWithExpiryInMSOpCodeByteTag :: Word8
keyValWithExpiryInMSOpCodeByteTag = 0xFC

resizeDBOpCodeByteTag :: Word8
resizeDBOpCodeByteTag = 0xFB

auxiliaryFieldOpCodeByteTag :: Word8
auxiliaryFieldOpCodeByteTag = 0xFA

stringValueTypeByteId :: Word8
stringValueTypeByteId = 0

-- | Binary instances for RDB format serialization and deserialization
{-# WARNING in "x-unsafe-internals" defaultChecksum "This value is exported for testing purposes only" #-}
defaultChecksum :: CheckSum
defaultChecksum = toChecksum 0

instance RDBBinary RDBFile where
    rdbPut (RDBFile magicString version auxFieldEntries dbEntries) = do
        rdbPut magicString
        rdbPut version
        mapM_ rdbPut auxFieldEntries
        mapM_ rdbPut dbEntries
        rdbPut EOF
        generatedChecksum <- State.get
        lift $ putWord64le (fromChecksum generatedChecksum)

    rdbGet = do
        config <- ask
        magicString <- rdbGet @RDBMagicString
        version <- rdbGet @RDBVersion
        auxFieldEntries <- many $ rdbGet @AuxField
        dbEntries <- manyTill (rdbGet @RDbEntry) (rdbGet @EOF) -- Parse database entries until EOF marker
        let weShouldVerifyChecksum = not config.skipChecksumValidation && version >= RDBv5 && config.generateChecksum
        when weShouldVerifyChecksum $ do
            generatedChecksum <- State.get
            -- From RDB v5 and up there should always be a checksum at the end of the rdbFile, even if it is just 0 https://github.com/redis/redis/blob/38d16a82eb4a8b0e393b51cc3e9144dc7b413fd1/src/rdb.c#L3678
            checksum <- lift (toChecksum <$> getWord64le)
            if checksum == generatedChecksum || checksum == defaultChecksum
                then pure ()
                else
                    throwRDBError $
                        MKRDBErrorArg
                            ValidationError
                            "RDB Checksum Validation"
                            ("Wrong RDB checksum expected: " <> genericShow generatedChecksum <> " but got: " <> genericShow checksum)

        pure (RDBFile magicString version auxFieldEntries dbEntries)

{- | Database entry encoding/decoding.
    Each database starts with SelectDB, followed by ResizeDB hints, then key-value pairs.
-}
instance RDBBinary RDbEntry where
    rdbPut (RDbEntry entryId resizeDBEntry keyValEntries) = do
        rdbPut entryId
        rdbPut resizeDBEntry
        mapM_ rdbPut keyValEntries

    rdbGet = do
        entryId <- rdbGet @SelectDB <|> pure def -- Default to SelectDB 0 if not present
        resizeDBEntry <- rdbGet @ResizeDB <|> pure def -- Default to ResizeDB 0 0 if not present
        keyValEntries <- many $ rdbGet @KeyValueOpCode
        pure $ RDbEntry entryId resizeDBEntry keyValEntries

-- | RDB version is stored as 4 ASCII bytes (e.g., "0003")
instance RDBBinary RDBVersion where
    rdbPut = \case
        RDBv4 -> genericRDBPutUsing (putByteString "0004")
        RDBv5 -> genericRDBPutUsing (putByteString "0005")
        RDBv7 -> genericRDBPutUsing (putByteString "0007")
        CustomVer (a, b, c, d) -> genericRDBPutUsing (putByteString (BSC.pack [a, b, c, d]))
    rdbGet = do
        versionStr <- genericRDBGetUsing (getByteString 4)
        case versionStr of
            "0004" -> pure RDBv4
            "0005" -> pure RDBv5
            "0007" -> pure RDBv7
            _ -> do
                let versionStrParser = (,,,) <$> AC.digit <*> AC.digit <*> AC.digit <*> AC.digit
                case AC.parseOnly versionStrParser versionStr of
                    Right parsedVersion -> pure $ CustomVer parsedVersion
                    Left _ -> do
                        throwRDBError $
                            MKRDBErrorArg
                                ValidationError
                                "RDB Version Parsing"
                                ("Unsupported or invalid RDB version: " <> genericShow versionStr)

instance RDBBinary RDBMagicString where
    rdbPut Redis = genericRDBPutUsing (putByteString "REDIS")
    rdbGet = do
        magicStr <- genericRDBGetUsing (getByteString 5)
        if magicStr == "REDIS"
            then pure Redis
            else
                throwRDBError $
                    MKRDBErrorArg
                        ValidationError
                        "RDB magic string check"
                        ("Invalid RDB magic string: " <> genericShow magicStr)

-- | KeyValueOpCode encoding with proper opcode prefixes
instance RDBBinary KeyValueOpCode where
    rdbPut = \case
        FCOpCode keyValWithExpiryInMS -> rdbPut keyValWithExpiryInMS
        KeyValOpCode keyValWithNoExpiryInfo -> rdbPut keyValWithNoExpiryInfo
        FDOpcode keyValWithExpiryInS -> rdbPut keyValWithExpiryInS
    rdbGet =
        -- Try parsing different key-value types in order of specificity
        asum
            [ FDOpcode <$> rdbGet
            , FCOpCode <$> rdbGet
            , KeyValOpCode <$> rdbGet -- Should be last since it's most permissive
            ]

-- | EOF marker is a single 0xFF byte
instance RDBBinary EOF where
    rdbPut EOF = genericRDBPut @Word8 eofOpCodeByteTag
    rdbGet = do
        tag <- genericRDBGet @Word8
        if tag == eofOpCodeByteTag
            then pure EOF
            else
                throwRDBError $
                    MKRDBErrorArg
                        ValidationError
                        "EOF op code check"
                        ("Expected EOF marker (" <> showFiniteBits eofOpCodeByteTag <> ") got " <> showFiniteBits tag <> " instead.")

-- | Millisecond expiry: 0xFC + 8-byte little-endian timestamp + value type + key + value
instance RDBBinary KeyValWithExpiryInMS where
    rdbPut (KeyValWithExpiryInMS expiryTimeMs valueType encodedKey encodedValue) = do
        genericRDBPut @Word8 keyValWithExpiryInMSOpCodeByteTag
        rdbPut expiryTimeMs
        rdbPut valueType
        rdbPut encodedKey
        rdbPut encodedValue

    rdbGet = do
        tag <- genericRDBGet @Word8
        if tag /= keyValWithExpiryInMSOpCodeByteTag
            then
                throwRDBError $
                    MKRDBErrorArg
                        ValidationError
                        "KeyValWithExpiryInMS op code check"
                        ("Expected KeyValWithExpiryInMS marker (" <> showFiniteBits keyValWithExpiryInMSOpCodeByteTag <> ") got " <> showFiniteBits tag <> " instead.")
            else KeyValWithExpiryInMS <$> rdbGet <*> rdbGet <*> rdbGet <*> rdbGet

-- | Second expiry: 0xFD + 4-byte little-endian timestamp + value type + key + value
instance RDBBinary KeyValWithExpiryInS where
    rdbPut (KeyValWithExpiryInS expiryTimeS valueType encodedKey encodedValue) = do
        genericRDBPut @Word8 keyValWithExpiryInSpCodeByteTag
        rdbPut expiryTimeS
        rdbPut valueType
        rdbPut encodedKey
        rdbPut encodedValue

    rdbGet = do
        tag <- genericRDBGet @Word8
        if tag /= keyValWithExpiryInSpCodeByteTag
            then
                throwRDBError $
                    MKRDBErrorArg
                        ValidationError
                        "KeyValWithExpiryInS op code check"
                        ("Expected KeyValWithExpiryInS marker (" <> showFiniteBits keyValWithExpiryInSpCodeByteTag <> ") got " <> showFiniteBits tag <> " instead.")
            else KeyValWithExpiryInS <$> rdbGet <*> rdbGet <*> rdbGet <*> rdbGet

-- | No expiry format: value type + key + value (no opcode prefix)
instance RDBBinary KeyValWithNoExpiryInfo where
    rdbPut (KeyValWithNoExpiryInfo valueType encodedKey encodedValue) = do
        rdbPut valueType
        rdbPut encodedKey
        rdbPut encodedValue

    rdbGet = KeyValWithNoExpiryInfo <$> rdbGet <*> rdbGet <*> rdbGet

{- | Auxiliary field encoding with 0xFA opcode prefix.
    Format: [0xFA][key-string][value-string]
    This handles all auxiliary field types with their specific encodings.
-}
instance RDBBinary AuxField where
    rdbPut x = do
        genericRDBPut @Word8 auxiliaryFieldOpCodeByteTag -- Redis auxiliary field opcode
        case x of
            AuxFieldRedisVer ver -> rdbPut ver
            AuxFieldRedisBits bits -> rdbPut bits
            AuxFieldCTime time -> rdbPut time
            AuxFieldUsedMem mem -> rdbPut mem
            AuxFieldCustom key value -> do
                rdbPut key
                rdbPut value

    rdbGet = do
        tag <- genericRDBGet @Word8
        if tag /= auxiliaryFieldOpCodeByteTag
            then
                throwRDBError $
                    MKRDBErrorArg
                        ValidationError
                        "AuxField opcode check"
                        ("Expected AuxField marker (" <> showFiniteBits auxiliaryFieldOpCodeByteTag <> ") got " <> showFiniteBits tag <> " instead.")
            else do
                asum
                    [ AuxFieldRedisVer <$> rdbGet @RedisVersion
                    , AuxFieldRedisBits <$> rdbGet @RedisBits
                    , AuxFieldCTime <$> rdbGet @CTime
                    , AuxFieldUsedMem <$> rdbGet @UsedMem
                    , AuxFieldCustom <$> rdbGet @RDBString <*> rdbGet @RDBStringOrIntVal
                    ]

{- | Redis architecture bits encoding (32-bit or 64-bit).
    Stores "redis-bits" key with "32" or "64" value as variable-length strings.
-}
instance RDBBinary RedisBits where
    rdbPut x =
        do
            -- Write "redis-bits" key followed by architecture value
            rdbPut @RDBShortString $$(refineTH "redis-bits")
            case x of
                RedisBits32 -> rdbPut @RDBShortString $$(refineTH "32")
                RedisBits64 -> rdbPut @RDBShortString $$(refineTH "64")
    rdbGet = do
        -- Parse and validate "redis-bits" key
        key <- rdbGet @RDBShortString -- "redis-bits" is 10 bytes
        if key /= $$(refineTH "redis-bits")
            then
                throwRDBError $
                    MKRDBErrorArg
                        ValidationError
                        "redis-bits prefix check"
                        ("Expected redis-bits prefix \"redis-bits\" got " <> genericShow key <> " instead")
            else do
                -- Parse architecture value ("32" or "64")
                architectureVal <- rdbGet @RDBShortString
                case unrefine architectureVal of
                    "32" -> pure RedisBits32
                    "64" -> pure RedisBits64
                    _ ->
                        throwRDBError $
                            MKRDBErrorArg
                                ParseError
                                "redis-bits value parsing"
                                ("Unknown redis-bits aux field value: " <> genericShow architectureVal)

{- | Redis version string encoding.
    Stores "redis-ver" key with version string value (e.g., "7.0.0").
-}
instance RDBBinary RedisVersion where
    rdbPut (RedisVersion version) = do
        -- Write "redis-ver" key followed by version string
        rdbPut @RDBShortString $$(refineTH "redis-ver")
        rdbPut version
    rdbGet = do
        -- Parse and validate "redis-ver" key
        key <- rdbGet @RDBShortString -- "redis-ver" is 10 bytes
        if key /= $$(refineTH "redis-ver")
            then
                throwRDBError $
                    MKRDBErrorArg
                        ValidationError
                        "redis-ver prefix check"
                        ("Expected redis-ver prefix \"redis-ver\" got " <> genericShow key <> " instead")
            else do
                -- Parse version string value
                RedisVersion <$> rdbGet @RDBShortString

{- | Creation time (ctime) encoding as Unix timestamp.
    Stores "ctime" key with POSIXTime formatted as string value.
-}
instance RDBBinary CTime where
    rdbPut (CTime cTime) = do
        -- Write "ctime" key followed by timestamp as string
        rdbPut @RDBShortString $$(refineTH "ctime")
        -- Using the Data.ByteString.Char8.pack to go from string to ByteString since we expect (show posixTime) to be an ASCII string (made up of only numeric symbols)
        -- We also convert to string like is done here: https://github.com/redis/redis/blob/f6f16746e1d4bc51960158d9a896e1aa0a2c7dbd/src/rdb.c#L1257
        rdbPut (toRDBString (BSC.pack . show $ cTime.getRDBUnixTimestampS))

    rdbGet = do
        -- Parse and validate "ctime" key
        key <- rdbGet @RDBShortString -- "ctime" is 5 bytes
        if key /= $$(refineTH "ctime")
            then
                throwRDBError $
                    MKRDBErrorArg
                        ValidationError
                        "ctime prefix check"
                        ("Expected ctime prefix \"ctime\" got " <> genericShow key <> " instead")
            else do
                -- Parse timestamp string and convert to numeric value
                timeStr <- rdbGet @RDBString
                let cTimeM = BSC.readWord32 . fromRDBString $ timeStr
                -- \^ Using the Data.ByteString.Char8 to unpack to string since we expect an ASCII string (made up of only numeric symbols)
                case cTimeM of
                    Just (cTime, "") -> pure $ CTime (RDBUnixTimestampS cTime)
                    _ ->
                        throwRDBError $
                            MKRDBErrorArg
                                ParseError
                                "ctime field format/value check"
                                "Invalid POSIX time format/value in ctime field: expected an integer with no trailing characters"

{- | Memory usage (used-mem) encoding in bytes.
    Stores "used-mem" key with memory usage as variable-length string value.
-}
instance RDBBinary UsedMem where
    rdbPut (UsedMem mem) = do
        -- Write "used-mem" key followed by memory value as string
        rdbPut @RDBShortString $$(refineTH "used-mem")
        rdbPut (toRDBString (BSC.pack . show $ mem))

    rdbGet = do
        -- Parse and validate "used-mem" key
        key <- rdbGet @RDBShortString -- "used-mem" is 8 characters
        if key /= $$(refineTH "used-mem")
            then
                throwRDBError $
                    MKRDBErrorArg
                        ValidationError
                        "used-mem prefix check"
                        ("Expected used-mem prefix \"used-mem\" got " <> genericShow key <> " instead")
            else do
                -- Parse memory value string and convert to integer
                memVal <- fromRDBString <$> rdbGet @RDBString
                case BSC.readWord memVal of
                    Just (memInt, "") -> pure $ UsedMem memInt
                    _ ->
                        throwRDBError $
                            MKRDBErrorArg
                                ParseError
                                "used-mem field format/value check"
                                "Invalid used-mem format/value: expected an integer with no trailing characters"

{- | Database selection (SelectDB) encoding with 0xFE opcode.
    Format: [0xFE][database-index-as-string]
    Indicates which Redis database the following entries belong to.
-}
instance RDBBinary SelectDB where
    rdbPut (SelectDB dbIndex) = do
        -- Write database selection opcode
        genericRDBPut @Word8 selectDBOpCodeByteTag -- Redis SelectDB opcode
        -- Write database index using variable-length encoding
        rdbPut dbIndex
    rdbGet = do
        -- Read and validate database selection opcode
        tag <- genericRDBGet @Word8
        if tag /= selectDBOpCodeByteTag
            then
                throwRDBError $
                    MKRDBErrorArg
                        ValidationError
                        "SelectDB prefix check"
                        ("Expected SelectDB prefix (" <> showFiniteBits selectDBOpCodeByteTag <> ") got " <> showFiniteBits tag <> " instead.")
            else do
                -- Parse database index from variable-length encoding
                SelectDB <$> rdbGet @RDBLengthPrefix6

{- | Database resize hints (ResizeDB) encoding with 0xFB opcode.
    Format: [0xFB][total-keys-hint][expiry-keys-hint]
    Provides memory allocation hints for hash table sizing optimization.
-}
instance RDBBinary ResizeDB where
    rdbPut (ResizeDB numOfKeys numOfKeysWithExpiry) = do
        genericRDBPut @Word8 resizeDBOpCodeByteTag -- Redis ResizeDB opcode
        rdbPut . toRDBLengthPrefix $ numOfKeys
        rdbPut . toRDBLengthPrefix $ numOfKeysWithExpiry

    rdbGet = do
        tag <- genericRDBGet @Word8
        if tag /= resizeDBOpCodeByteTag
            then
                throwRDBError $
                    MKRDBErrorArg
                        ValidationError
                        "ResizeDB prefix check"
                        ("Expected ResizeDB prefix (" <> showFiniteBits resizeDBOpCodeByteTag <> ") got " <> showFiniteBits tag <> " instead.")
            else do
                numOfKeys <- rdbGet @RDBLengthPrefix6
                numOfKeysWithExpiry <- rdbGet @RDBLengthPrefix6
                pure $ ResizeDB (fromIntegral . unrefine $ numOfKeys) (fromIntegral . unrefine $ numOfKeysWithExpiry)

{- | Redis value type encoding for RDB format.
    Each type has a specific byte identifier followed by type-specific data.
    Currently only string type (0) is implemented in this codebase.
-}
instance RDBBinary ValueType where
    rdbPut = \case
        Str -> genericRDBPut @Word8 stringValueTypeByteId -- Redis string type identifier
    rdbGet = do
        tag <- genericRDBGet @Word8
        case tag of
            0 -> pure Str
            _ ->
                throwRDBError $
                    MKRDBErrorArg
                        ParseError
                        "ValueType byte check"
                        ("Unknown or unsupported RDB value type: " <> genericShow tag)
