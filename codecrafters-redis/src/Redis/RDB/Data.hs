{-# LANGUAGE MultiWayIf #-}
{-# LANGUAGE TypeFamilies #-}
{-# OPTIONS_GHC -Wno-orphans #-}
{-# OPTIONS_GHC -Wno-type-defaults #-}

module Redis.RDB.Data (
    RDBStringOrIntVal (..),
    RDBString (..),
    RDBShortString,
    RDBMediumString,
    RDBLongString,
    RDBExtraLongString,
    RDBInt8 (..),
    RDBInt16 (..),
    RDBInt32 (..),
    RDBFloat (..),
    RDBDouble (..),
    RDBLengthPrefix6,
    RDBLengthPrefix14,
    RDBLengthPrefix32,
    RDBLengthPrefix64,
    RDBUnixTimestampS (..),
    RDBUnixTimestampMS (..),
    toRDBString,
    fromRDBString,
    toRDBStringOrIntVal,
    fromPosixTimeToRDBUnixTimestampMS,
    toPosixTimeFromRDBUnixTimestampMS,
    fromPosixTimeToRDBUnixTimestampS,
    toPosixTimeFromRDBUnixTimestampS,

    -- ** For testing
    toRDBLengthPrefix,
    shortStringLenCutoff,
    mediumStringLenCutoff,
    longStringLenCutoff,
) where

import Redis.RDB.Binary

import Data.Bits qualified as Bits
import Data.ByteString qualified as BS
import Data.ByteString.Char8 qualified as BSC

import Control.Applicative (Alternative (..), asum)
import Control.DeepSeq (NFData (..))
import Control.Monad.Reader (ask)
import Data.Binary.Get (
    getByteString,
    getDoublele,
    getFloatle,
    getInt16le,
    getInt32le,
    getWord32be,
    getWord32le,
    getWord64be,
    getWord64le,
 )
import Data.Binary.Put (
    putByteString,
    putDoublele,
    putFloatle,
    putInt16le,
    putInt32le,
    putWord32be,
    putWord32le,
    putWord64be,
    putWord64le,
 )
import Data.Data (Proxy (..))
import Data.Either (isRight)
import Data.Fixed (Pico)
import Data.Int (Int16, Int32, Int8)
import Data.Maybe (isNothing)
import Data.Time.Clock (secondsToNominalDiffTime)
import Data.Time.Clock.POSIX (POSIXTime)
import Data.Word (Word16, Word32, Word64, Word8)
import GHC.Generics (Generic)
import GHC.TypeLits (natVal)
import Redis.RDB.Config (RDBConfig (..))
import Redis.RDB.LZF (LzfCompressedByteString (..), lzfCompress, lzfDecompress)
import Redis.RDB.Rerefined (refineErrorWithContext)
import Redis.Utils (genericShow, millisecondsToSeconds, secondsToMilliseconds)
import Rerefined (Refined, refine, unrefine, unsafeRefine)
import Rerefined.Orphans ()
import Rerefined.Predicate (Predicate (..), Refine (..))
import Rerefined.Predicates (And, CompareLength, CompareValue, Sign (..), validateVia)
import Rerefined.Predicates.Operators (type (.<), type (.<=), type (.>), type (.>=))
import Bits.Show (showFiniteBits)

{-
    Okay, so the RDB spec describes what a variant of variable-length encoding that we shall refer to as rdb-variable-length encoding since this variant differs subtly from the standard variable-length encoding. Now, that said, the rdb-variable length encoding is mostly for the length of things to ensure that the length is stored smartly and efficiently, not using more space/bytes than necessary. However, Redis also uses this encoding/algorithm to sometimes encode integers hence we have the RDBLengthPrefix6, RDBLengthPrefix14, and RDBLengthPrefix32, types which encode integers as if they were a length of a string but without the accompany byte sequence you'd expect for a string.

    Note that these types differ from the RDBInt* types in that the RDBInt* types use a special encoding based off the rdb-variable-length encoding to encode integers as strings with a length as the first byte followed by a byte sequence representing the integer quantity. So in this case we aren't just encoding the length alone and considering it an integer during decoding, we encode the a special prefix byte followed by the encoded integer value
-}

-- When we refer to length-prefix we really mean length-prefix encoding as per the RDB variable length encoding scheme outlined here https://rdb.fnordig.de/file_format.html#length-encoding

-- RDB strings are encoded using the RDB length encoding scheme ([len][data]): https://github.com/redis/redis/blob/38d16a82eb4a8b0e393b51cc3e9144dc7b413fd1/src/rdb.c#L440

-- Supports strings with sizes up to 64 bits by virtue of there being support for length prefixes up to 64 bits: https://github.com/redis/redis/blob/38d16a82eb4a8b0e393b51cc3e9144dc7b413fd1/src/rdb.c#L157

data RDBStringLength = ShortString | MediumString | LongString | ExtraLongString

instance Predicate ShortString where
    type PredicateName d ShortString = "ShortString"

instance Predicate MediumString where
    type PredicateName d MediumString = "MediumString"

instance Predicate LongString where
    type PredicateName d LongString = "LongString"

instance Predicate ExtraLongString where
    type PredicateName d ExtraLongString = "ExtraLongString"

type RDBShortString = Refined ShortString BS.ByteString
type RDBShortStringCompareLen = (And (CompareLength (.>=) 0) (CompareLength (.<) SixBitUpperBound))

instance Refine ShortString BS.ByteString where
    validate = validateVia @RDBShortStringCompareLen

type RDBMediumString = Refined MediumString BS.ByteString
type RDBMediumStringCompareLen = (And (CompareLength (.>=) SixBitUpperBound) (CompareLength (.<) FourteenBitUpperBound))

instance Refine MediumString BS.ByteString where
    validate = validateVia @RDBMediumStringCompareLen

type RDBLongString = Refined LongString BS.ByteString
type RDBLongStringCompareLen = (And (CompareLength (.>=) FourteenBitUpperBound) (CompareLength (.<) ThirtyTwoBitUpperBound))

instance Refine LongString BS.ByteString where
    validate = validateVia @RDBLongStringCompareLen

type RDBExtraLongString = Refined ExtraLongString BS.ByteString
type RDBExtraLongStringCompareLen = (CompareLength (.>=) ThirtyTwoBitUpperBound)

instance Refine ExtraLongString BS.ByteString where
    validate = validateVia @RDBExtraLongStringCompareLen

-- NOTE: The rdb variable length encoding also includes special support for serializing integers as string as defined here https://rdb.fnordig.de/file_format.html#integers-as-string. The resulting byte sequence is in little-endian. Below are the types that represent integers to be serialized according to the special rdb variable - length encoding rules for integers

-- Only supports integers up to 32 bits: https://github.com/redis/redis/blob/38d16a82eb4a8b0e393b51cc3e9144dc7b413fd1/src/rdb.c#L251

newtype RDBInt8 = RDBInt8 {rdbInt8 :: Int8}
    deriving stock (Show, Eq, Ord, Generic)
    deriving anyclass (NFData)

newtype RDBInt16 = RDBInt16 {rdbInt16 :: Int16}
    deriving stock (Show, Eq, Ord, Generic)
    deriving anyclass (NFData)

newtype RDBInt32 = RDBInt32 {rdbInt32 :: Int32}
    deriving stock (Show, Eq, Ord, Generic)
    deriving anyclass (NFData)

-- Types for floating point values stored in RDB files

newtype RDBFloat = RDBFloat {rdbFloat :: Float}
    deriving stock (Show, Eq, Ord, Generic)
    deriving anyclass (NFData)

newtype RDBDouble = RDBDouble {rdbDouble :: Double}
    deriving stock (Show, Eq, Ord, Generic)
    deriving anyclass (NFData)

-- As mentioned above, the rdb specification serializes strings using a length encoding (what we've opted to call rdb variable length encoding). For certain integral values, rather than serializing those as rdb string integers, redis will sometimes serializes integral data as a "length" as though it were doing a full length encoding, the difference is, there is no data, just an unsigned integer serialized as though it were part of a full length encoding. We call this part of the full encoding the length prefix and the types below represent unsigned integers serialized as length prefixes with no accompanying data. Big endian is assumed

-- Supports length prefix unsigned integer values up to 64 bits: https://github.com/redis/redis/blob/38d16a82eb4a8b0e393b51cc3e9144dc7b413fd1/src/rdb.c#L157

type SixBitUpperBound = 64 -- Equivalent to (1<<6) in C
type FourteenBitUpperBound = 16384 -- Equivalent to (1<<14) in C
type ThirtyTwoBitUpperBound = 4294967295 -- Equivalent to UINT32_MAX in C

type SixBitLengthPrefixBounds = (And (CompareValue (.>=) Pos 0) (CompareValue (.<) Pos SixBitUpperBound))
type FourteenBitLengthPrefixBounds = (And (CompareValue (.>=) Pos SixBitUpperBound) (CompareValue (.<) Pos FourteenBitUpperBound))
type ThirtyTwoBitLengthPrefixBounds = (And (CompareValue (.>=) Pos FourteenBitUpperBound) (CompareValue (.<=) Pos ThirtyTwoBitUpperBound))
type SixtyFourBitLengthPrefixBounds = (CompareValue (.>) Pos ThirtyTwoBitUpperBound)

-- | 6-bit length prefix
type RDBLengthPrefix6 = Refined SixBitLengthPrefixBounds Word8

-- | 14-bit length prefix
type RDBLengthPrefix14 = Refined FourteenBitLengthPrefixBounds Word16

-- | 32-bit length prefix
type RDBLengthPrefix32 = Refined ThirtyTwoBitLengthPrefixBounds Word32

-- | 64-bit length prefix
type RDBLengthPrefix64 = Refined SixtyFourBitLengthPrefixBounds Word64

-- | RDB Unix timestamp types
newtype RDBUnixTimestampS = RDBUnixTimestampS {getRDBUnixTimestampS :: Word32}
    deriving stock (Show, Eq, Ord)

newtype RDBUnixTimestampMS = RDBUnixTimestampMS {getRDBUnixTimestampMS :: Word64}
    deriving stock (Show, Eq, Ord)

data RDBStringOrIntVal
    = MkRDBInt RDBInt
    | MkRDBString RDBString
    deriving stock (Show, Eq, Generic)
    deriving anyclass (NFData)

data RDBString
    = RDBShortString RDBShortString
    | RDBMediumString RDBMediumString
    | RDBLongString RDBLongString
    | RDBExtraLongString RDBExtraLongString
    deriving stock (Show, Eq, Generic)
    deriving anyclass (NFData)

data RDBInt
    = MkRDBInt8 RDBInt8
    | MkRDBInt16 RDBInt16
    | MkRDBInt32 RDBInt32
    deriving stock (Show, Eq, Generic)
    deriving anyclass (NFData)

data RDBLengthPrefix
    = RDBLengthPrefix6 RDBLengthPrefix6
    | RDBLengthPrefix14 RDBLengthPrefix14
    | RDBLengthPrefix32 RDBLengthPrefix32
    | RDBLengthPrefix64 RDBLengthPrefix64
    deriving stock (Show, Eq)

instance RDBBinary RDBStringOrIntVal where
    rdbPut = \case
        (MkRDBInt rdbInt) -> rdbPut rdbInt
        (MkRDBString rdbString) -> rdbPut rdbString

    rdbGet =
        asum
            [ MkRDBInt <$> rdbGet
            , MkRDBString <$> rdbGet
            ]

instance RDBBinary RDBInt where
    rdbPut = \case
        MkRDBInt8 rdbInt8 -> rdbPut rdbInt8
        MkRDBInt16 rdbInt16 -> rdbPut rdbInt16
        MkRDBInt32 rdbInt32 -> rdbPut rdbInt32

    rdbGet =
        asum
            [ MkRDBInt8 <$> rdbGet
            , MkRDBInt16 <$> rdbGet
            , MkRDBInt32 <$> rdbGet
            ]

instance RDBBinary RDBInt8 where
    rdbPut = rdbVariableLengthEncode8BitInt
    rdbGet = rdbVariableLengthDecode8BitInt

instance RDBBinary RDBInt16 where
    rdbPut = rdbVariableLengthEncode16BitInt
    rdbGet = rdbVariableLengthDecode16BitInt

instance RDBBinary RDBInt32 where
    rdbPut = rdbVariableLengthEncode32BitInt
    rdbGet = rdbVariableLengthDecode32BitInt

instance RDBBinary RDBString where
    rdbPut = \case
        RDBShortString shortStr -> rdbPut shortStr
        RDBMediumString mediumStr -> rdbPut mediumStr
        RDBLongString longStr -> rdbPut longStr
        RDBExtraLongString extraLongStr -> rdbPut extraLongStr

    rdbGet =
        asum
            [ RDBShortString <$> rdbGet
            , RDBMediumString <$> rdbGet
            , RDBLongString <$> rdbGet
            , RDBExtraLongString <$> rdbGet
            ]

instance RDBBinary RDBShortString where
    rdbPut = attemptLzfSerializationWithFallback rdbVariableLengthEncodeShortString
    rdbGet = attemptLzfDeserializationWithFallback (refineErrorWithContext "RDBShortString deserialization") rdbVariableLengthDecodeShortString

instance RDBBinary RDBMediumString where
    rdbPut = attemptLzfSerializationWithFallback rdbVariableLengthEncodeMediumString
    rdbGet = attemptLzfDeserializationWithFallback (refineErrorWithContext "RDBMediumString deserialization") rdbVariableLengthDecodeMediumString

instance RDBBinary RDBLongString where
    rdbPut = attemptLzfSerializationWithFallback rdbVariableLengthEncodeLongString
    rdbGet = attemptLzfDeserializationWithFallback (refineErrorWithContext "RDBLongString deserialization") rdbVariableLengthDecodeLongString

instance RDBBinary RDBExtraLongString where
    rdbPut = attemptLzfSerializationWithFallback rdbVariableLengthEncodeExtraLongString
    rdbGet = attemptLzfDeserializationWithFallback (refineErrorWithContext "RDBExtraLongString deserialization") rdbVariableLengthDecodeExtraLongString

instance RDBBinary RDBLengthPrefix where
    rdbPut = \case
        RDBLengthPrefix6 rdbLengthPrefix6 -> rdbPut rdbLengthPrefix6
        RDBLengthPrefix14 rdbLengthPrefix14 -> rdbPut rdbLengthPrefix14
        RDBLengthPrefix32 rdbLengthPrefix132 -> rdbPut rdbLengthPrefix132
        RDBLengthPrefix64 rdbLengthPrefix144 -> rdbPut rdbLengthPrefix144

    rdbGet =
        asum
            [ RDBLengthPrefix6 <$> rdbGet
            , RDBLengthPrefix14 <$> rdbGet
            , RDBLengthPrefix32 <$> rdbGet
            , RDBLengthPrefix64 <$> rdbGet
            ]

instance RDBBinary RDBLengthPrefix6 where
    rdbPut = rdbEncodeLengthPrefix8
    rdbGet = rdbDecodeLengthPrefix6

instance RDBBinary RDBLengthPrefix14 where
    rdbPut = rdbEncodeLengthPrefix14
    rdbGet = rdbDecodeLengthPrefix14

instance RDBBinary RDBLengthPrefix32 where
    rdbPut = rdbEncodeLengthPrefix32
    rdbGet = rdbDecodeLengthPrefix32

instance RDBBinary RDBLengthPrefix64 where
    rdbPut = rdbEncodeLengthPrefix64
    rdbGet = rdbDecodeLengthPrefix64

instance RDBBinary RDBFloat where
    rdbPut = rdbEncodeFloat
    rdbGet = rdbDecodeFloat

instance RDBBinary RDBDouble where
    rdbPut = rdbEncodeDouble
    rdbGet = rdbDecodeDouble

instance RDBBinary RDBUnixTimestampS where
    rdbPut (RDBUnixTimestampS t) = genericRDBPutUsing (putWord32le t) -- Redis uses little-endian for timestamps

    rdbGet = RDBUnixTimestampS <$> genericRDBGetUsing getWord32le

instance RDBBinary RDBUnixTimestampMS where
    rdbPut (RDBUnixTimestampMS t) = genericRDBPutUsing (putWord64le t) -- Redis uses little-endian for timestamps

    rdbGet = RDBUnixTimestampMS <$> genericRDBGetUsing getWord64le

-- This logic of this function, particularly the bounds/limits in each case, is based off the `rdbSaveLen` redis function: https://github.com/redis/redis/blob/38d16a82eb4a8b0e393b51cc3e9144dc7b413fd1/src/rdb.c#L157
toRDBLengthPrefix :: Word -> RDBLengthPrefix
toRDBLengthPrefix num
    -- These uses of `unsafeRefine` are safe because the guards preceding conditions that the target num is valid within the branch
    | isRight (refine @SixBitLengthPrefixBounds num) = RDBLengthPrefix6 (unsafeRefine . fromIntegral $ num)
    | isRight (refine @FourteenBitLengthPrefixBounds num) = RDBLengthPrefix14 (unsafeRefine . fromIntegral $ num)
    | isRight (refine @ThirtyTwoBitLengthPrefixBounds num) = RDBLengthPrefix32 (unsafeRefine . fromIntegral $ num)
    -- Using `unsafeRefine` here is safe because the guards above ensures that num is within the bounds of SixtyFourBitLengthPrefixBounds, in other words, greater than ThirtyTwoBitUpperBound
    | otherwise = RDBLengthPrefix64 (unsafeRefine . fromIntegral $ num)

{-# WARNING in "x-unsafe-internals" shortStringLenCutoff "This value is exported for testing purposes only" #-}
shortStringLenCutoff :: (Num a) => a
shortStringLenCutoff = fromInteger $ natVal @SixBitUpperBound Proxy

{-# WARNING in "x-unsafe-internals" mediumStringLenCutoff "This value is exported for testing purposes only" #-}
mediumStringLenCutoff :: (Num a) => a
mediumStringLenCutoff = fromInteger $ natVal @FourteenBitUpperBound Proxy

{-# WARNING in "x-unsafe-internals" longStringLenCutoff "This value is exported for testing purposes only" #-}
longStringLenCutoff :: (Num a) => a
longStringLenCutoff = fromInteger $ natVal @ThirtyTwoBitUpperBound Proxy

-- https://github.com/redis/redis/blob/unstable/src/rdb.c#L445, attempts integer encoding for potential integer strings
-- We do not need the random cut off at (len <= 11) since we can already check to determine whether a byte sequence is number by attempting to parse it as such. Arbitrary length cutoffs become redundant
toRDBStringOrIntVal :: BS.ByteString -> RDBStringOrIntVal
toRDBStringOrIntVal byteStr
    | Just num <- tryParseAsInt8 byteStr = MkRDBInt (MkRDBInt8 (RDBInt8 num))
    | Just num <- tryParseAsInt16 byteStr = MkRDBInt (MkRDBInt16 (RDBInt16 num))
    | Just num <- tryParseAsInt32 byteStr = MkRDBInt (MkRDBInt32 (RDBInt32 num))
    -- These uses of `unsafeRefine` are safe because the guard conditions ensure that the `byteStr` is valid for each corresponding case
    | isRight (refine @ShortString byteStr) = MkRDBString (RDBShortString (unsafeRefine byteStr))
    | isRight (refine @MediumString byteStr) = MkRDBString (RDBMediumString (unsafeRefine byteStr))
    | isRight (refine @LongString byteStr) = MkRDBString (RDBLongString (unsafeRefine byteStr))
    -- Using `unsafeRefine` here is safe because the guards above ensure that byteStr is not a stringified number and that its size is within the bounds of SixtyFourBitLengthPrefixBounds, in other words, greater than ThirtyTwoBitUpperBound
    | otherwise = MkRDBString (RDBExtraLongString (unsafeRefine byteStr))

toRDBString :: BS.ByteString -> RDBString
toRDBString byteStr
    -- These uses of `unsafeRefine` are safe because the guard conditions ensure that the `byteStr` is valid for each corresponding case
    | isRight (refine @ShortString byteStr) = RDBShortString (unsafeRefine byteStr)
    | isRight (refine @MediumString byteStr) = RDBMediumString (unsafeRefine byteStr)
    | isRight (refine @LongString byteStr) = RDBLongString (unsafeRefine byteStr)
    -- Using `unsafeRefine` here is safe because the guards above ensure that the size of the byteStr is within the bounds of SixtyFourBitLengthPrefixBounds, in other words, greater than ThirtyTwoBitUpperBound
    | otherwise = RDBExtraLongString (unsafeRefine byteStr)

fromRDBString :: RDBString -> BS.ByteString
fromRDBString (RDBShortString byteStr) = unrefine byteStr
fromRDBString (RDBMediumString byteStr) = unrefine byteStr
fromRDBString (RDBLongString byteStr) = unrefine byteStr
fromRDBString (RDBExtraLongString byteStr) = unrefine byteStr

fromRDBLengthPrefix :: (Num a) => RDBLengthPrefix -> a
fromRDBLengthPrefix = \case
    RDBLengthPrefix6 num -> fromIntegral . unrefine $ num
    RDBLengthPrefix14 num -> fromIntegral . unrefine $ num
    RDBLengthPrefix32 num -> fromIntegral . unrefine $ num
    RDBLengthPrefix64 num -> fromIntegral . unrefine $ num

tryParseAsInt8 :: BS.ByteString -> Maybe Int8
tryParseAsInt8 byteStr = do
    (num, restOfStr) <- BSC.readInt8 byteStr
    if BS.null restOfStr
        then Just num
        else Nothing

tryParseAsInt16 :: BS.ByteString -> Maybe Int16
tryParseAsInt16 byteStr = do
    (num, restOfStr) <- BSC.readInt16 byteStr
    if BS.null restOfStr
        then Just num
        else Nothing

tryParseAsInt32 :: BS.ByteString -> Maybe Int32
tryParseAsInt32 byteStr = do
    (num, restOfStr) <- BSC.readInt32 byteStr
    if BS.null restOfStr
        then Just num
        else Nothing

toPosixTimeFromRDBUnixTimestampS :: RDBUnixTimestampS -> POSIXTime
toPosixTimeFromRDBUnixTimestampS = fromIntegral . (.getRDBUnixTimestampS)

fromPosixTimeToRDBUnixTimestampS :: POSIXTime -> RDBUnixTimestampS
fromPosixTimeToRDBUnixTimestampS = RDBUnixTimestampS . floor @_ @Word32

fromPosixTimeToRDBUnixTimestampMS :: POSIXTime -> RDBUnixTimestampMS
fromPosixTimeToRDBUnixTimestampMS = RDBUnixTimestampMS . floor @_ @Word64 . secondsToMilliseconds

toPosixTimeFromRDBUnixTimestampMS :: RDBUnixTimestampMS -> POSIXTime
toPosixTimeFromRDBUnixTimestampMS = secondsToNominalDiffTime . millisecondsToSeconds . fromIntegral @_ @Pico . (.getRDBUnixTimestampMS)

isValid6BitLength :: (Ord a, Num a) => a -> Bool
isValid6BitLength len = len >= 0 && len < shortStringLenCutoff

isValid14BitLength :: (Ord a, Num a) => a -> Bool
isValid14BitLength len = len >= shortStringLenCutoff && len < mediumStringLenCutoff

isValid32BitLength :: (Ord a, Num a) => a -> Bool
isValid32BitLength len = len >= mediumStringLenCutoff && len < longStringLenCutoff

isValid64BitLength :: (Ord a, Num a) => a -> Bool
isValid64BitLength len = len >= longStringLenCutoff

{- |
    Encode a short string using RDB's variable length encoding
    Format: [00XXXXXX] where XXXXXX is the string's 6-bit length followed by the actual byte string data
-}
rdbVariableLengthEncodeShortString :: RDBShortString -> RDBPut
rdbVariableLengthEncodeShortString refinedByteStr = do
    let byteStr = unrefine refinedByteStr
    -- It's safe to use `unsafeRefine` here because the `Predicate` instance for the refined type underlying `RDBShortString` ensures that the length is within the valid range for a 6-bit length prefix
    let shortRdbByteStrLen = unsafeRefine $ fromIntegral @_ @Word8 $ BS.length byteStr
    let lengthPrefix = shortRdbByteStrLen
    rdbEncodeLengthPrefix8 lengthPrefix
    genericRDBPutUsing . putByteString $ byteStr

-- | Decode a short string using RDB's variable length encoding
rdbVariableLengthDecodeShortString :: RDBGet RDBShortString
rdbVariableLengthDecodeShortString = do
    shortByteStrLen <- rdbDecodeLengthPrefix6
    let shortByteStrLenInt = fromIntegral @_ @Int . unrefine $ shortByteStrLen
    unrefinedByteStr <- genericRDBGetUsing (getByteString shortByteStrLenInt)
    refineErrorWithContext "RDBShortString refinement" unrefinedByteStr

{- | Encode medium length string using RDB's variable length encoding
    Format: [01XXXXXX] [YYYYYYYY] where XXXXXXYYYYYYYY is the the string's 14-bit length followed by the actual byte string data
-}
rdbVariableLengthEncodeMediumString :: RDBMediumString -> RDBPut
rdbVariableLengthEncodeMediumString refinedByteStr = do
    let byteStr = unrefine refinedByteStr
    -- It's safe to use `unsafeRefine` here because the `Predicate` instance for the refined type underlying `RDBMediumString` ensures that the length is within the valid range for a 14-bit length prefix
    let mediumRdbByteStrLen = unsafeRefine . fromIntegral @_ @Word16 $ BS.length byteStr
    let lengthPrefix = mediumRdbByteStrLen
    rdbEncodeLengthPrefix14 lengthPrefix
    genericRDBPutUsing . putByteString $ byteStr

-- | Decode a medium length string using RDB's variable length encoding
rdbVariableLengthDecodeMediumString :: RDBGet RDBMediumString
rdbVariableLengthDecodeMediumString = do
    mediumByteStrLen <- rdbDecodeLengthPrefix14
    let mediumByteStrLenInt = fromIntegral @_ @Int . unrefine $ mediumByteStrLen
    unrefinedByteStr <- genericRDBGetUsing (getByteString mediumByteStrLenInt)
    refineErrorWithContext "RDBMediumString refinement" unrefinedByteStr

{- | Encode a long string using RDB's variable length encoding
    Format: [10000000] [XXXXXXXX] [XXXXXXXX] [XXXXXXXX] [XXXXXXXX] where X's represent the string's 32-bit length followed by the actual byte string data
-}
rdbVariableLengthEncodeLongString :: RDBLongString -> RDBPut
rdbVariableLengthEncodeLongString refinedByteStr = do
    let byteStr = unrefine refinedByteStr
    -- It's safe to use `unsafeRefine` here because the `Predicate` instance for the refined type underlying `RDBLongString` ensures that the length is within the valid range for a 32-bit length prefix
    let longRdbStrLen = unsafeRefine . fromIntegral @_ @Word32 $ BS.length byteStr
    let lengthPrefix = longRdbStrLen
    rdbEncodeLengthPrefix32 lengthPrefix
    genericRDBPutUsing . putByteString $ byteStr

-- | Decode a long string using RDB's variable length encoding
rdbVariableLengthDecodeLongString :: RDBGet RDBLongString
rdbVariableLengthDecodeLongString = do
    longByteStrLen <- rdbDecodeLengthPrefix32
    let longByteStrLenInt = fromIntegral @_ @Int . unrefine $ longByteStrLen
    unrefinedByteStr <- genericRDBGetUsing (getByteString longByteStrLenInt)
    refineErrorWithContext "RDBLongString refinement" unrefinedByteStr

{- | Encode an extra long string using RDB's variable length encoding
    Format: [10000001] [XXXXXXXX]...[XXXXXXXX] where X's represent the string's 64-bit length followed by the actual byte string data
-}
rdbVariableLengthEncodeExtraLongString :: RDBExtraLongString -> RDBPut
rdbVariableLengthEncodeExtraLongString refinedByteStr = do
    let byteStr = unrefine refinedByteStr
    -- It's safe to use `unsafeRefine` here because the `Predicate` instance for the refined type underlying `RDBExtraLongString` ensures that the length is within the valid range for a 64-bit length prefix
    let extraLongRdbStrLen = unsafeRefine . fromIntegral @_ @Word64 $ BS.length byteStr
    let lengthPrefix = extraLongRdbStrLen
    rdbEncodeLengthPrefix64 lengthPrefix
    genericRDBPutUsing . putByteString $ byteStr

-- | Decode an extra long string using RDB's variable length encoding
rdbVariableLengthDecodeExtraLongString :: RDBGet RDBExtraLongString
rdbVariableLengthDecodeExtraLongString = do
    extraLongByteStrLen <- rdbDecodeLengthPrefix64
    let extraLongByteStrLenInt = fromIntegral @_ @Int . unrefine $ extraLongByteStrLen
    unrefinedByteStr <- genericRDBGetUsing (getByteString extraLongByteStrLenInt)
    refineErrorWithContext "RDBExtraLongString refinement" unrefinedByteStr

-- Integer encoding using RDB's special integer length encoding scheme: https://rdb.fnordig.de/file_format.html#integers-as-string

{- | Encode an 8-bit integer using RDB's special integer length encoding scheme
    Format: [11000000] [XXXXXXXX] where XXXXXXXX is the 8-bit signed integer
-}
rdbVariableLengthEncode8BitInt :: RDBInt8 -> RDBPut
rdbVariableLengthEncode8BitInt (RDBInt8 intVal) = do
    genericRDBPut intPrefix8Bit
    genericRDBPut @Int8 intVal

-- | Decode an 8-bit integer using RDB's special integer length encoding scheme
rdbVariableLengthDecode8BitInt :: RDBGet RDBInt8
rdbVariableLengthDecode8BitInt = do
    prefixByte <- genericRDBGet @Word8
    let isPrefixByteValid = verifyPrefixByte prefixByte
    if not isPrefixByteValid
        then
            throwRDBError $
                MKRDBErrorArg
                    ValidationError
                    "RDBInt8 prefix byte verification"
                    ("Unexpected prefix byte while attempting to decode 8 bit integer. Expected " <> showFiniteBits intPrefix8Bit <> ", but got: " <> showFiniteBits prefixByte)
        else RDBInt8 <$> genericRDBGet @Int8
  where
    -- Validate that prefix is correct and type identifier (last 6 bits) is 0
    verifyPrefixByte :: Word8 -> Bool
    verifyPrefixByte = (== intPrefix8Bit)

intPrefix8Bit :: Word8
intPrefix8Bit = 0b11000000

{- | Encode a 16-bit integer using RDB's special integer length encoding scheme
    Format: [11000001] [XXXXXXXX] [XXXXXXXX] where XXXXXXXXXXXXXXXX is the 16-bit signed integer
-}
rdbVariableLengthEncode16BitInt :: RDBInt16 -> RDBPut
rdbVariableLengthEncode16BitInt (RDBInt16 intVal) = do
    genericRDBPut intPrefix16Bit
    genericRDBPutUsing (putInt16le intVal)

-- | Decode a 16-bit integer using RDB's special integer length encoding scheme
rdbVariableLengthDecode16BitInt :: RDBGet RDBInt16
rdbVariableLengthDecode16BitInt = do
    prefixByte <- genericRDBGet @Word8
    let isPrefixByteValid = verifyPrefixByte prefixByte
    if not isPrefixByteValid
        then
            throwRDBError $
                MKRDBErrorArg
                    ValidationError
                    "RDBInt16 prefix byte verification"
                    ("Unexpected prefix byte while attempting to decode 16 bit integer. Expected " <> showFiniteBits intPrefix16Bit <> ", but got: " <> showFiniteBits prefixByte)
        else RDBInt16 <$> genericRDBGetUsing getInt16le
  where
    -- Validate that prefix is correct and type identifier (last 6 bits) is 1
    verifyPrefixByte :: Word8 -> Bool
    verifyPrefixByte = (== intPrefix16Bit)

intPrefix16Bit :: Word8
intPrefix16Bit = 0b11000001

{- | Encode a 32-bit integer using RDB's special integer length encoding scheme
    Format: [11000010] [XXXXXXXX] [XXXXXXXX] [XXXXXXXX] [XXXXXXXX] where X's represent the 32-bit signed integer
-}
rdbVariableLengthEncode32BitInt :: RDBInt32 -> RDBPut
rdbVariableLengthEncode32BitInt (RDBInt32 intVal) = do
    genericRDBPut intPrefix32Bit
    genericRDBPutUsing (putInt32le intVal)

-- | Decode a 32-bit integer using RDB's special integer length encoding scheme
rdbVariableLengthDecode32BitInt :: RDBGet RDBInt32
rdbVariableLengthDecode32BitInt = do
    prefixByte <- genericRDBGet @Word8
    let isPrefixByteValid = verifyPrefixByte prefixByte
    if not isPrefixByteValid
        then
            throwRDBError $
                MKRDBErrorArg
                    ValidationError
                    "RDBInt32 prefix byte verification"
                    ("Unexpected prefix byte while attempting to decode 32 bit integer. Expected " <> showFiniteBits intPrefix32Bit <> ", but got: " <> showFiniteBits prefixByte)
        else RDBInt32 <$> genericRDBGetUsing getInt32le
  where
    -- Validate that prefix is correct and type identifier (last 6 bits) is 2
    verifyPrefixByte :: Word8 -> Bool
    verifyPrefixByte = (== intPrefix32Bit)

intPrefix32Bit :: Word8
intPrefix32Bit = 0b11000010

-- | Floating point encoding as defined here https://github.com/redis/redis/blob/38d16a82eb4a8b0e393b51cc3e9144dc7b413fd1/src/rdb.c#L645-L674
rdbEncodeFloat :: RDBFloat -> RDBPut
rdbEncodeFloat (RDBFloat floatVal) = genericRDBPutUsing (putFloatle floatVal)

rdbDecodeFloat :: RDBGet RDBFloat
rdbDecodeFloat = RDBFloat <$> genericRDBGetUsing getFloatle

rdbEncodeDouble :: RDBDouble -> RDBPut
rdbEncodeDouble (RDBDouble doubleVal) = genericRDBPutUsing (putDoublele doubleVal)

rdbDecodeDouble :: RDBGet RDBDouble
rdbDecodeDouble = RDBDouble <$> genericRDBGetUsing getDoublele

-- | Length prefix encoding functions. These functions encode standalone length prefix values following the rdb variable length encoding, but without any accompanying data bytes.

{- | Encode a 6-bit length value
    Format: [00XXXXXX] where XXXXXX is the 6-bit length
-}
rdbEncodeLengthPrefix8 :: RDBLengthPrefix6 -> RDBPut
rdbEncodeLengthPrefix8 lenVal = do
    -- The value is already constrained to fit in 6 bits by the type system
    -- Top 2 bits are implicitly 00, so we can write the length directly
    genericRDBPut @Word8 . unrefine $ lenVal

-- | Decode a 6-bit length value
rdbDecodeLengthPrefix6 :: RDBGet RDBLengthPrefix6
rdbDecodeLengthPrefix6 = do
    byte <- genericRDBGet @Word8
    -- Verify the prefix, the first two bits, is 00 (top 2 bits should be 0)
    let byteHasExpectedPrefix = (byte `Bits.shiftR` 6) == 0
    if not byteHasExpectedPrefix
        then
            throwRDBError $
                MKRDBErrorArg
                    ValidationError
                    "RDBLengthPrefix6 prefix byte verification"
                    ("Unexpected prefix for 6-bit length. Expected first two bits to be 00, but got: " <> showFiniteBits byte)
        else refineErrorWithContext "RDBLengthPrefix6 refinement" byte

{- | Encode a 14-bit length prefix
    Format: [01XXXXXX][YYYYYYYY] where XXXXXXYYYYYYYY is the 14-bit length
-}
rdbEncodeLengthPrefix14 :: RDBLengthPrefix14 -> RDBPut
rdbEncodeLengthPrefix14 refinedLenVal = do
    let lenVal = unrefine refinedLenVal
    let prefixByte = bytePrefixFor14BitLengthPrefix
    let byteFromLenValToBeCombinedWithPrefix = fromIntegral @_ @Word8 (lenVal `Bits.shiftR` 8)
    let sixBitsMask = 0b00111111
    let byteFromLenValToBeCombinedWithPrefixWithFirstTwoBitsReset = byteFromLenValToBeCombinedWithPrefix Bits..&. sixBitsMask
    let prefixWithBitsFromLenVal = prefixByte Bits..|. byteFromLenValToBeCombinedWithPrefixWithFirstTwoBitsReset
    let remainingByteFromLenVal = fromIntegral @_ @Word8 lenVal

    genericRDBPut @Word8 prefixWithBitsFromLenVal
    genericRDBPut @Word8 remainingByteFromLenVal

-- | Decode a 14-bit length prefix
rdbDecodeLengthPrefix14 :: RDBGet RDBLengthPrefix14
rdbDecodeLengthPrefix14 = do
    firstByte <- genericRDBGet @Word8
    -- Mask to extract the first two bits of the first byte and reset the remaining six bits
    let prefixMask = 0b11000000
    let byteHasExpectedPrefix = (firstByte Bits..&. prefixMask) == bytePrefixFor14BitLengthPrefix
    if not byteHasExpectedPrefix
        then
            throwRDBError $
                MKRDBErrorArg
                    ValidationError
                    "RDBLengthPrefix14 prefix byte verification"
                    ("Unexpected prefix for 14-bit length. Expected " <> showFiniteBits bytePrefixFor14BitLengthPrefix <> ", but got: " <> showFiniteBits firstByte)
        else do
            secondByte <- genericRDBGet @Word8
            -- We know that the last six bits of the first byte and all eight bits of the second byte represent the length, so we want to extract the last six bits of the first byte and combine them with the second byte to form the full 14-bit length
            let sixBitsMask = 0b00111111
            let lastSixBitsFromFirstByte = fromIntegral @_ @Word16 (firstByte Bits..&. sixBitsMask)
            let remainingByte = fromIntegral @_ @Word16 secondByte -- To Word16 for combining
            let unRefinedFullLengthVal = (lastSixBitsFromFirstByte `Bits.shiftL` 8) Bits..|. remainingByte -- Combine to form full 14-bit length
            refineErrorWithContext "RDBLengthPrefix14 refinement" unRefinedFullLengthVal

bytePrefixFor14BitLengthPrefix :: Word8
bytePrefixFor14BitLengthPrefix = 0b01000000

{- | Encode a 32-bit length prefix
    Format: [10000000] [XXXXXXXX]...[XXXXXXXX] where X's represent the 32-bit length
-}
rdbEncodeLengthPrefix32 :: RDBLengthPrefix32 -> RDBPut
rdbEncodeLengthPrefix32 refinedLenVal = do
    let lenVal = unrefine refinedLenVal
    genericRDBPut @Word8 bytePrefixFor32BitLengthPrefix
    genericRDBPutUsing (putWord32be lenVal)

-- | Decode a 32-bit length prefix (0-4294967295)
rdbDecodeLengthPrefix32 :: RDBGet RDBLengthPrefix32
rdbDecodeLengthPrefix32 = do
    prefixByte <- genericRDBGet @Word8
    if prefixByte /= bytePrefixFor32BitLengthPrefix
        then
            throwRDBError $
                MKRDBErrorArg
                    ValidationError
                    "RDBLengthPrefix32 prefix byte verification"
                    ("Unexpected prefix for 32-bit length. Expected " <> showFiniteBits bytePrefixFor32BitLengthPrefix <> ", but got: " <> showFiniteBits prefixByte)
        else refineErrorWithContext "RDBLengthPrefix32 refinement" =<< genericRDBGetUsing getWord32be

bytePrefixFor32BitLengthPrefix :: Word8
bytePrefixFor32BitLengthPrefix = 0b10000000

{- | Encode a 64-bit length prefix
    Format: [10000001] [XXXXXXXX]...[XXXXXXXX] where X's represent the 64-bit length
-}
rdbEncodeLengthPrefix64 :: RDBLengthPrefix64 -> RDBPut
rdbEncodeLengthPrefix64 refinedLen = do
    let len = unrefine refinedLen
    genericRDBPut @Word8 bytePrefixFor64BitLengthPrefix
    genericRDBPutUsing (putWord64be len)

-- | Decode a 64-bit length prefix (0-18446744073709551615)
rdbDecodeLengthPrefix64 :: RDBGet RDBLengthPrefix64
rdbDecodeLengthPrefix64 = do
    prefixByte <- genericRDBGet @Word8
    if prefixByte /= bytePrefixFor64BitLengthPrefix
        then
            throwRDBError $
                MKRDBErrorArg
                    ValidationError
                    "RDBLengthPrefix64 prefix byte verification"
                    ("Unexpected prefix for 64-bit length. Expected " <> showFiniteBits bytePrefixFor64BitLengthPrefix <> ", but got: " <> showFiniteBits prefixByte)
        else refineErrorWithContext "RDBLengthPrefix64 refinement" =<< genericRDBGetUsing getWord64be

bytePrefixFor64BitLengthPrefix :: Word8
bytePrefixFor64BitLengthPrefix = 0b10000001

{-
    Encode LZF compressed data in the format of [prefix][compressedLen][originalLen][data]
    https://github.com/redis/redis/blob/81df8deca614e46cb113974dbac0b9ec3e6724e7/src/rdb.c#L362
-}
rdbEncodeLzfCompressedData :: LzfCompressedByteString -> RDBPut
rdbEncodeLzfCompressedData LzfCompressedByteString{lzfCompressedByteString, compressedLen, originalLen} = do
    genericRDBPut rdbLzfCompressedDataBinaryPrefix
    rdbPut . toRDBLengthPrefix . fromIntegral $ compressedLen
    rdbPut . toRDBLengthPrefix . fromIntegral $ originalLen
    genericRDBPutUsing (putByteString lzfCompressedByteString)

{-
    Decode LZF compressed data from an RDB file
    https://github.com/redis/redis/blob/81df8deca614e46cb113974dbac0b9ec3e6724e7/src/rdb.c#L383
-}
rdbDecodeLzfCompressedData :: RDBGet BS.ByteString
rdbDecodeLzfCompressedData = do
    prefixByte <- genericRDBGet @Word8
    if prefixByte /= rdbLzfCompressedDataBinaryPrefix
        then
            throwRDBError $
                MKRDBErrorArg
                    ValidationError
                    "LZF prefix byte verification"
                    ("Unexpected LZF compressed data prefix byte. Expected: " <> showFiniteBits rdbLzfCompressedDataBinaryPrefix <> ", but got: " <> showFiniteBits prefixByte)
        else do
            compressedLen <- fromRDBLengthPrefix @Int <$> rdbGet @RDBLengthPrefix
            originalLen <- fromRDBLengthPrefix @Int <$> rdbGet @RDBLengthPrefix
            lzfCompressedData <- genericRDBGetUsing (getByteString compressedLen)

            let mDecompressedData = lzfDecompress (LzfCompressedByteString lzfCompressedData compressedLen originalLen)

            maybe
                ( throwRDBError $
                    MKRDBErrorArg
                        ParseError
                        "LZF decompression"
                        ("Failed to decompress LZF compressed data. Original length: " <> genericShow originalLen <> ", Compressed length: " <> genericShow compressedLen)
                )
                pure
                mDecompressedData

rdbLzfCompressedDataBinaryPrefix :: Word8
rdbLzfCompressedDataBinaryPrefix = 0b11000011

attemptLzfSerializationWithFallback :: forall (p :: RDBStringLength). (Refined p BS.ByteString -> RDBPut) -> Refined p BS.ByteString -> RDBPut
attemptLzfSerializationWithFallback fallbackSerialization refinedByteStr =
    do
        config <- ask
        let byteStr = unrefine refinedByteStr
            originalLen = BS.length byteStr
            mExpectedCompressionSize = calculateExpectedCompressionSize originalLen
            mCompressedData = lzfCompress byteStr mExpectedCompressionSize
        if
            | not config.useLzfCompression -> fallbackSerialization refinedByteStr
            | originalLen <= minimumDataLengthForSufficientCompression -> fallbackSerialization refinedByteStr
            | isNothing mExpectedCompressionSize -> fallbackSerialization refinedByteStr
            | Just lzfCompressedData <- mCompressedData -> rdbEncodeLzfCompressedData lzfCompressedData
            | otherwise -> fallbackSerialization refinedByteStr

-- The type signature is this way because we want `f` to only have the "ability" of MonadFail from RDBGet, not anything else
attemptLzfDeserializationWithFallback :: (MonadRDBError m, m a ~ RDBGet a) => (BS.ByteString -> m a) -> RDBGet a -> RDBGet a
attemptLzfDeserializationWithFallback f fallbackDeserialization = do
    config <- ask
    if config.useLzfCompression
        then (rdbDecodeLzfCompressedData >>= f) <|> fallbackDeserialization
        else fallbackDeserialization

{-
    This is necessary to avoid issues with very small inputs that cannot be effectively compressed. Due to the way lzf compressions works, small inputs may not have enough data to compress and the end result may even be expanded due to the overhead of the compression algorithm. Input that is smaller than or equal to this threshold is unlikely to benefit from compression. This threshold was chosen per this code in the redis codebase https://github.com/redis/redis/blob/38d16a82eb4a8b0e393b51cc3e9144dc7b413fd1/src/rdb.c#L455
-}
minimumDataLengthForSufficientCompression :: Int
minimumDataLengthForSufficientCompression = 20

-- For an lzf compression to be considered "sufficient/worth it" the compressed size ought to be 4 bytes smaller than the original (4 bytes compression at least)
-- As defined here: https://github.com/redis/redis/blob/38d16a82eb4a8b0e393b51cc3e9144dc7b413fd1/src/rdb.c#L368
-- As in the redis code, if we cannot get lzf compression to shave off at least 4 bytes from the original size, we don't want to do it
-- This function returns a Maybe to encapsulate the condition `val > 4` because as stated in the redis code "We require at least four bytes compression for this to be worth it". If we didn't return a Maybe, we'd have to do the `> 4` check wherever this function is used
calculateExpectedCompressionSize :: Int -> Maybe Int
calculateExpectedCompressionSize num =
    let val = num - 4
     in if val > 4 then Just val else Nothing
