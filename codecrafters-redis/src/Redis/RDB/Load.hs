module Redis.RDB.Load (
    RDBStateInfo (..),
    loadRDBFile,
    loadStoreFromRDBDump,
) where

import Path
import Redis.RDB.Data
import Redis.RDB.Format
import Redis.Server.Settings
import Redis.Store.Data

import Data.HashMap.Strict qualified as HashMap
import Data.List.NonEmpty qualified as NE
import Effectful.Exception qualified as Eff
import Effectful.FileSystem qualified as Eff
import Redis.RDB.Binary qualified as Binary

import Control.Exception (SomeException, displayException)
import Data.Foldable (foldl')
import Effectful (Eff, (:>))
import Effectful.Log (Log)
import Prettyprinter (Pretty (pretty), line, (<+>))
import Redis.Server.Settings.Get (genRDBConfigFromSettings, getRDBDumpFilePathFromSettings)
import Redis.ServerState (Store, StoreKey (..), StoreValue (..))
import Redis.Utils (logDebug)

data RDBStateInfo = RDBStateInfo
    { redisVer :: Maybe RedisVersion
    , redisBits :: Maybe RedisBits
    , creationTime :: Maybe CTime
    , usedMem :: Maybe UsedMem
    }
    deriving stock (Eq, Show)

loadStoreFromRDBDump :: (Eff.FileSystem :> es, Log :> es) => ServerSettings -> Eff es (Maybe (Store, RDBStateInfo))
loadStoreFromRDBDump settings = do
    let rdbFilePath = getRDBDumpFilePathFromSettings settings
    let rdbConfig = genRDBConfigFromSettings settings
    rdbFileE <-
        Eff.catch @SomeException (Binary.decodeFileOrFail @_ @RDBFile rdbConfig (fromSomeFile rdbFilePath)) $ \err -> pure . Left $ (0, displayException err)

    case rdbFileE of
        Left err -> do
            let debugMsg =
                    show $
                        "Failed to decode RDB file:"
                            <+> pretty (show rdbFilePath)
                            <> line
                            <> pretty err

            logDebug debugMsg
            pure Nothing
        Right rdbFileContents -> pure . Just . loadRDBFile $ rdbFileContents

loadRDBFile :: RDBFile -> (Store, RDBStateInfo)
loadRDBFile rdbFile = (store, stateInfo)
  where
    -- We only support loading the first RDB entry from the RDB file since our server doesn't support multiple databases
    store = maybe HashMap.empty (HashMap.fromList . loadKeyValueEntries . NE.head) (NE.nonEmpty rdbFile.dbEntries)
    stateInfo = foldl' loadRDBStateInfoFromAuxFields (RDBStateInfo{redisVer = Nothing, redisBits = Nothing, creationTime = Nothing, usedMem = Nothing}) rdbFile.auxFieldEntries

loadKeyValueEntries :: RDbEntry -> [(StoreKey, StoreValue)]
loadKeyValueEntries RDbEntry{keyValEntries} = map loadKeyValueEntry keyValEntries

loadKeyValueEntry :: KeyValueOpCode -> (StoreKey, StoreValue)
loadKeyValueEntry (FCOpCode KeyValWithExpiryInMS{..}) =
    let key = StoreKey $ fromRDBStringOrIntVal encodedKey
        val = fromRDBStringOrIntVal encodedValue
        ttl = toUnixTimestampMSFromRDBUnixTimestampMS expiryTimeMs
     in ( key
        , StoreValue
            { value = MkRedisStr (RedisStr val)
            , ttlTimestamp = Just ttl
            }
        )
loadKeyValueEntry (FDOpcode KeyValWithExpiryInS{..}) =
    let key = StoreKey $ fromRDBStringOrIntVal encodedKey
        val = fromRDBStringOrIntVal encodedValue
        ttl = toUnixTimestampMSFromRDBUnixTimestampS expiryTimeS
     in ( key
        , StoreValue
            { value = MkRedisStr (RedisStr val)
            , ttlTimestamp = Just ttl
            }
        )
loadKeyValueEntry (KeyValOpCode KeyValWithNoExpiryInfo{..}) =
    let key = StoreKey $ fromRDBStringOrIntVal encodedKey
        val = fromRDBStringOrIntVal encodedValue
     in (key, StoreValue{value = MkRedisStr (RedisStr val), ttlTimestamp = Nothing})

loadRDBStateInfoFromAuxFields :: RDBStateInfo -> AuxField -> RDBStateInfo
loadRDBStateInfoFromAuxFields currentStateInfo (AuxFieldRedisVer redisVer) =
    currentStateInfo{redisVer = Just redisVer}
loadRDBStateInfoFromAuxFields currentStateInfo (AuxFieldRedisBits redisBits) =
    currentStateInfo{redisBits = Just redisBits}
loadRDBStateInfoFromAuxFields currentStateInfo (AuxFieldCTime cTime) =
    currentStateInfo{creationTime = Just cTime}
loadRDBStateInfoFromAuxFields currentStateInfo (AuxFieldUsedMem usedMem) =
    currentStateInfo{usedMem = Just usedMem}
loadRDBStateInfoFromAuxFields stateInfo _ = stateInfo
