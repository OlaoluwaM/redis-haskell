module Redis.RDB.Save (
    saveRedisStoreToRDB,
) where

import Redis.RDB.Data
import Redis.RDB.Format
import Redis.Store.Data
import Redis.Store.Timestamp

import Data.HashMap.Strict qualified as HashMap

import Data.Maybe (isJust)
import Data.Time.Clock.POSIX (POSIXTime)
import Redis.Server.Version (redisVersion)
import Redis.ServerState (Store, StoreKey (..), StoreValue (..))
import Rerefined (unsafeRefine)

saveRedisStoreToRDB :: POSIXTime -> Store -> RDBFile
saveRedisStoreToRDB creationTime store =
    let totalNumOfKeys = calcNumOfKeysInStore store
        numOfKeysWithTTL = calculateNumOfKeysWithTTLInStore store
        keyValEntries = saveKeyValueEntries store
     in RDBFile
            { magicString = Redis
            , version = RDBv7
            , auxFieldEntries = saveAuxFields creationTime
            , dbEntries =
                [ RDbEntry
                    { resizeDBEntry = ResizeDB totalNumOfKeys numOfKeysWithTTL
                    , keyValEntries
                    , entryId = defaultSelectDB -- We only support a single database (database 0) for now as any more is discouraged in favor of multiple redis instances https://stackoverflow.com/questions/16221563/whats-the-point-of-multiple-redis-databases
                    }
                ]
            }

-- These aux fields represent the context around the creation of the RDB file
saveAuxFields :: POSIXTime -> [AuxField]
saveAuxFields creationTime =
    [ AuxFieldRedisVer (RedisVersion $ unsafeRefine redisVersion)
    , AuxFieldRedisBits RedisBits64 -- TODO: Is there any way to determine whether a program is being run on a 32 bit system vs on a 64 bit system?
    , AuxFieldCTime . CTime . fromUnixTimestampMSToRDBUnixTimestampS . mkUnixTimestampMSFromPOSIXTime $ creationTime
    ]

calcNumOfKeysInStore :: Store -> Word
calcNumOfKeysInStore store = fromIntegral $ length $ HashMap.keys store

calculateNumOfKeysWithTTLInStore :: Store -> Word
calculateNumOfKeysWithTTLInStore = calcNumOfKeysInStore . HashMap.filter hasTTL
  where
    hasTTL :: StoreValue -> Bool
    hasTTL StoreValue{ttlTimestamp} = isJust ttlTimestamp

saveKeyValueEntries :: Store -> [KeyValueOpCode]
saveKeyValueEntries store = map (uncurry mkRDBKeyValEntry) (HashMap.toList store)
  where
    mkRDBKeyValEntry :: StoreKey -> StoreValue -> KeyValueOpCode
    mkRDBKeyValEntry key storeVal = do
        case (storeVal.value, storeVal.ttlTimestamp) of
            (MkRedisStr strVal, Just ttl) -> saveKeyValEntryWithExpiry key strVal ttl
            (MkRedisStr strVal, Nothing) -> saveKeyValEntryWithoutExpiry key strVal
            x -> error $ "Only string values are supported right now in the Redis store. So this error really shouldn't have happened " <> show x

    saveKeyValEntryWithExpiry :: StoreKey -> RedisStr -> UnixTimestampMS -> KeyValueOpCode
    saveKeyValEntryWithExpiry (StoreKey key) (RedisStr val) ttl =
        let rdbStringKey = toRDBStringOrIntVal key
            rdbStringVal = toRDBStringOrIntVal val
         in FCOpCode $
                -- The redis codebase serializes all TTL values to milliseconds regardless of the precision they were set with. Perhaps all TTL values are stored internally in milliseconds? https://github.com/redis/redis/blob/38d16a82eb4a8b0e393b51cc3e9144dc7b413fd1/src/rdb.c#L1200
                KeyValWithExpiryInMS
                    { valueType = Str
                    , encodedKey = rdbStringKey
                    , encodedValue = rdbStringVal
                    , expiryTimeMs = fromUnixTimestampMSToRDBUnixTimestampMS ttl
                    }

    saveKeyValEntryWithoutExpiry :: StoreKey -> RedisStr -> KeyValueOpCode
    saveKeyValEntryWithoutExpiry (StoreKey key) (RedisStr val) =
        let rdbStringKey = toRDBStringOrIntVal key
            rdbStringVal = toRDBStringOrIntVal val
         in KeyValOpCode $
                KeyValWithNoExpiryInfo
                    { valueType = Str
                    , encodedKey = rdbStringKey
                    , encodedValue = rdbStringVal
                    }
