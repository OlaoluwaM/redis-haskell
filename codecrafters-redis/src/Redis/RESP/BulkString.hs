module Redis.RESP.BulkString (
    BulkString (..),
    bulkStringParser,
    mapBulkString,
    decodeUtf8BulkString',
    toOptionString,
    terminatorSeqParser,
    seqTerminator,
    notTerminatorSeq,

    -- ** Testing/Internal
    nullBulkStringParser,
    nonNullBulkStringParser,
) where

import Data.Attoparsec.ByteString.Char8 qualified as AC
import Data.ByteString.Char8 qualified as BS

import Control.Applicative (Alternative ((<|>)))
import Control.Monad (mfilter, when)
import Data.Attoparsec.ByteString (Parser)
import Data.ByteString (ByteString)
import Data.Functor (($>))
import Data.String.Interpolate (i)
import Data.Text (Text)
import Data.Text.Encoding (decodeUtf8')
import Data.Text.Encoding.Error (UnicodeException)
import Redis.Helpers (withCustomError)

-- | BulkString data type representing either a string with content or a null bulk string
data BulkString = BulkString ByteString | NullBulkString
    deriving stock (Eq, Show)

-- | Parser for RESP bulk strings (both null and non-null)
bulkStringParser :: Parser BulkString
bulkStringParser = nullBulkStringParser <|> nonNullBulkStringParser

-- | Parser for null bulk strings ($-1\r\n)
nullBulkStringParser :: Parser BulkString
nullBulkStringParser = withCustomError (AC.char '$' *> (AC.char '-' *> AC.char '1') *> terminatorSeqParser $> NullBulkString) "Invalid Null RESP BulkString"

-- | Parser for non-null bulk strings ($<length>\r\n<data>\r\n)
nonNullBulkStringParser :: Parser BulkString
nonNullBulkStringParser = do
    len <- withCustomError (AC.char '$' *> AC.signed AC.decimal <* terminatorSeqParser) "Invalid RESP BulkString string"
    when (len < 0) $ fail "Invalid. Expected a potential non-null bulkstring but found the beginnings of a null bulkstring instead"

    isEndOfInput <- AC.atEnd

    case (len == 0, isEndOfInput) of
        (False, True) -> fail "BulkString is not supposed to be empty"
        _ -> pure ()

    mainStr <- withCustomError ((BS.all notTerminatorSeq `mfilter` AC.take len) <* terminatorSeqParser) (genLenMismatchErr len)
    pure $ BulkString mainStr
  where
    genLenMismatchErr :: Int -> String
    genLenMismatchErr len' = [i|There seems to be mismatch between the purported BulkString length (#{len'}) and it's actual length or perhaps the contents of your bulk string contain a sequence terminator such as '\r' or '\n'|]

-- | Apply a function to the ByteString content of a BulkString
mapBulkString :: (ByteString -> ByteString) -> BulkString -> BulkString
mapBulkString f (BulkString bs) = BulkString (f bs)
mapBulkString _ NullBulkString = NullBulkString

-- | Decode a BulkString to UTF-8 Text
decodeUtf8BulkString' :: BulkString -> Either UnicodeException Text
decodeUtf8BulkString' NullBulkString = Right ""
decodeUtf8BulkString' (BulkString bytes) = decodeUtf8' bytes

-- | Convert a list of BulkStrings to a space-separated ByteString
toOptionString :: [BulkString] -> ByteString
toOptionString = BS.intercalate " " . map bulkStringToByteString
  where
    bulkStringToByteString NullBulkString = ""
    bulkStringToByteString (BulkString bytes) = bytes

-- | RESP sequence terminator
terminatorSeqParser :: Parser ()
terminatorSeqParser = AC.string seqTerminator $> ()

-- | Check if a character is not a terminator sequence character
notTerminatorSeq :: Char -> Bool
notTerminatorSeq c = not $ c == '\r' || c == '\n'

-- | RESP sequence terminator string
seqTerminator :: ByteString
seqTerminator = "\r\n"
